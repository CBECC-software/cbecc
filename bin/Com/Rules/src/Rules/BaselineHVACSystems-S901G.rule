// HVAC - Baseline HVAC System Assignment
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2014, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES LOSS OF USE, DATA, OR PROFITS OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  



// ---------- Section 3.1.2 - Baseline HVAC Systems ----------------------------

// ---------- Determine baseline system for building ---------------------------
// First calculate floor area used to determine baseline system type by subtracing:
//   - Heating-only spaces that do not include cooling in the proposed design


RULE NEW Bldg:PredominantOcc
  DATATYPE
    Enumeration
  LONGFORM
    PredominantOccupancy
  DESCRIPTION
    "The predominant occupancy of the Building."
  OPTION
    Nonresidential
    Residential
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( TotCondFlrArea = 0 )
    then "Nonresidential"   
    else if( ResFlrArea > NonResFlrArea )
    then "Residential"
    else "Nonresidential"   
    endif endif
  SIZING_BASELINE
    zp:PredominantOcc
ENDRULE

RULE NEW Bldg:ResExcptArea
  DATATYPE
    Float
  LONGFORM
    ResidentialExceptionalArea
  DESCRIPTION
    "The residential floor area that is subtracted from the nonresidential
     floor area if nonresidential is predominant and the residential area is greater
     than the PRMRM Section 3.1.2 (a) 20,000 ft2 threshold."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( PredominantOcc = "Nonresidential" .AND.
        ResFlrArea > 20000 )
    then ResFlrArea
    else 0
    endif
  SIZING_BASELINE
    zp:ResExcptArea
ENDRULE

RULE NEW Bldg:NonResExcptArea
  DATATYPE
    Float
  LONGFORM
    NonresidentialExceptionalArea
  DESCRIPTION
    "The nonresidential floor area that is subtracted from the residential
     floor area if residential is predominant and the nonresidential area is greater
     than the PRMRM Section 3.1.2 (a) 20,000 ft2 threshold."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( PredominantOcc = "Residential" .AND.
        NonResCondFlrAreaAllHt > 20000 )
    then NonResCondFlrAreaAllHt
    else 0
    endif
  SIZING_BASELINE
    zp:NonResExcptArea
ENDRULE

RULE NEW Bldg:OtherExcptArea
  DATATYPE
    Float
  LONGFORM
    OtherExceptionArea
  DESCRIPTION
    "The total floor area of PRMRM 3.1.2 exceptions (c) through (e) that is
     subtracted from the total conditioned area used to determine the 
     predominant baseline system type."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
// TO DO: Add exception areas when available
     0
  SIZING_BASELINE
    zp:OtherExcptArea
ENDRULE

RULE NEW Bldg:NonResPredominantHtgFuelSrc
  DATATYPE
    Integer
  LONGFORM
    NonresidentialPredominantHeatingFuelSource
  DESCRIPTION
    "A flag that indcates the predominant heating fuel source for the Nonresidential
     portion of the Building."
  HELP
    "The flag values are:
    -1 = Unconditioned, or no heating
     0 = Electic or other
     1 = Fossil fuel or fossil/electric"
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( NonResFlrArea = 0 )
    then -1
    else if( NonResCondFlrAreaFossilHt > NonResCondFlrAreaElecHt )
    then 1
    else 0
    endif endif
  SIZING_BASELINE
    zp:NonResPredominantHtgFuelSrc
ENDRULE

RULE NEW Bldg:ResPredominantHtgFuelSrc
  DATATYPE
    Integer
  LONGFORM
    RsidentialPredominantHeatingFuelSource
  DESCRIPTION
    "A flag that indcates the predominant heating fuel source for the Residential
     portion of the Building."
  HELP
    "The flag values are:
    -1 = Unconditioned, or no heating
     0 = Electic or other
     1 = Fossil fuel or fossil/electric"
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( ResFlrArea = 0 )
    then -1
    else if( ResCondFlrAreaFossilHt > ResCondFlrAreaElecHt )
    then 1
    else 0
    endif endif
  SIZING_BASELINE
    zp:ResPredominantHtgFuelSrc
ENDRULE

RULE NEW Bldg:NonResFuelSrcExcptArea
  DATATYPE
    Float
  LONGFORM
    NonresidentialFuelSourceExceptionArea
  DESCRIPTION
    "The floor area of the nonresidential, non-predominant fuel heating source if greater
     then PRMRM Section 3.1.2 (a) 20,000 ft2 threshold."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( NonResPredominantHtgFuelSrc = 1 .AND. 
        NonResCondFlrAreaElecHt > 20000 )
    then 
      Max( NonResCondFlrAreaElecHt, 0 )
    else
    if( NonResPredominantHtgFuelSrc = 0 .AND. 
        NonResCondFlrAreaFossilHt > 20000 )
    then
      Max( NonResCondFlrAreaFossilHt, 0 )
    else 0 
    endif endif
  SIZING_BASELINE
    zp:NonResFuelSrcExcptArea
ENDRULE

RULE NEW Bldg:ResFuelSrcExcptArea
  DATATYPE
    Float
  LONGFORM
    ResidentialFuelSourceExceptionArea
  DESCRIPTION
    "The floor area of the residential, non-predominant fuel heating source if greater
     then PRMRM Section 3.1.2 (a) 20,000 ft2 threshold."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( ResPredominantHtgFuelSrc = 1 .AND. 
        NonResCondFlrAreaElecHt > 20000 )
    then Max( ResCondFlrAreaElecHt, 0 )
    else
    if( ResPredominantHtgFuelSrc = 0 .AND. 
        NonResCondFlrAreaFossilHt > 20000 )
    then Max( ResCondFlrAreaFossilHt, 0 )
    else 0 
    endif endif
  SIZING_BASELINE
    zp:ResFuelSrcExcptArea
ENDRULE

RULE NEW Bldg:NonResPredominantFlrArea
  DATATYPE
    Float
  LONGFORM
    NonresidentialPredominantFloorArea
  HELP
    "The conditioned floor area of the predominant heating conditioned, assuming
     the fossil fuel category is the predominant source."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    if( PredominantOcc = "Nonresidential" )  
    then Max( NonResCondFlrAreaAllHt - NonResFuelSrcExcptArea, 0 )
    else NonResExcptArea
    endif
  SIZING_BASELINE
    zp:NonResPredominantFlrArea
ENDRULE

// Define predominant and non-predominant baseline system numbers
RULE NEW Bldg:BaseNonResPredominantSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineNonresidentialPredominantSystemNumber 
  INPUTCLASS
    NotInput
  DESCRIPTION
    "The nonresidential predominant baseline system number."  
  SIZING_PROPOSED
    if( PredominantOcc = "Nonresidential" .OR. NonResExcptArea > 0 ) 
    then
      if( NonResPredominantHtgFuelSrc = 1 )
      then // NonResPredominantHtgFuelSrc = 1 (Fossil)
        if( NonResPredominantFlrArea < 25000 .AND. 
            NonResStoryCntFossilHt <= 3 )
        then 3 // SZAC
        else if( ( NonResPredominantFlrArea < 25000 .AND. 
                   NonResStoryCntFossilHt <= 5 ) 
                 .OR.
                 ( NonResPredominantFlrArea <= 150000 .AND. 
                   NonResStoryCntFossilHt <= 5 ) )
        then 5 // PVAV w/ HW Reheat
        else 7 // VAV w/ HW Reheat
        endif endif
      else // NonResPredominantHtgFuelSrc = 0 (Electric)
        if( NonResPredominantFlrArea < 25000 .AND. 
            NonResStoryCntElecHt <= 3 )
        then 4 // SZHP
        else if( ( NonResPredominantFlrArea < 25000 .AND. 
                   NonResStoryCntElecHt <= 5 ) 
                 .OR.
                 ( NonResPredominantFlrArea <= 150000 .AND. 
                   NonResStoryCntElecHt <= 5 ) )
        then 6 // PVAV w/ Elec PFPBs
        else 8 // VAV w/ Elec PFPBs
        endif endif
      endif
    else 0 // Baseline design has no nonresidential baseline HVAC systems
    endif
  SIZING_BASELINE
    zp:BaseNonResPredominantSysNum
ENDRULE

RULE NEW Bldg:BaseNonResNonPredominantSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineNonresidentialNonPredominantSystemNumber 
  INPUTCLASS
    NotInput
  DESCRIPTION
    "The nonresidential NON-predominant baseline system number."  
  SIZING_PROPOSED
    if( ( PredominantOcc = "Nonresidential" .OR. NonResExcptArea > 0 ) .AND.
        NonResFuelSrcExcptArea > 0 )
    then 
      if( NonResPredominantHtgFuelSrc = 0 )
      then // NonResPredominantHtgFuelSrc = 0 (Electric), so NonpredominantFuelSrc = 1 (Fossil)
        if( NonResPredominantFlrArea < 25000 .AND. 
            NonResStoryCntFossilHt <= 3 )
        then 3 // SZAC
        else if( ( NonResPredominantFlrArea < 25000 .AND. 
                   NonResStoryCntFossilHt <= 5 ) 
                 .OR.
                 ( NonResPredominantFlrArea <= 150000 .AND. 
                   NonResStoryCntFossilHt <= 5 ) )
        then 5 // PVAV w/ HW Reheat
        else 7 // VAV w/ HW Reheat
        endif endif
      else // NonResPredominantHtgFuelSrc = 1 (Fossil), so NonpredominantFuelSrc = 0 (Electric)
        if( NonResPredominantFlrArea < 25000 .AND. 
            NonResStoryCntElecHt <= 3 )
        then 4 // SZHP
        else if( ( NonResPredominantFlrArea < 25000 .AND. 
                   NonResStoryCntElecHt <= 5 ) 
                 .OR.
                 ( NonResPredominantFlrArea <= 150000 .AND. 
                   NonResStoryCntElecHt <= 5 ) )
        then 6 // PVAV w/ Elec PFPBs
        else 8 // VAV w/ Elec PFPBs
        endif endif
      endif
    else 0 // Baseline design has no nonresidential, non-predominant baseline HVAC systems
    endif
  SIZING_BASELINE
    zp:BaseNonResNonPredominantSysNum
ENDRULE

RULE NEW Bldg:BaseResPredominantSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineResidentialNonPredominantSystemNumber 
  INPUTCLASS
    NotInput
  DESCRIPTION
    "The residential predominant baseline system number."  
  SIZING_PROPOSED
    if( PredominantOcc = "Residential" .OR. ResExcptArea > 0 ) 
    then
      if( ResPredominantHtgFuelSrc = 1 )
      then 1 // ResPredominantHtgFuelSrc is 1 (Fossil), use PTAC
      else 2 // ResPredominantHtgFuelSrc is 0 (Electric), use PTHP
      endif
    else 0 // Baseline design has no residential baseline HVAC systems
    endif
  SIZING_BASELINE
    zp:BaseResPredominantSysNum
ENDRULE

RULE NEW Bldg:BaseResNonPredominantSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineResidentialNonPredominantSystemNumber 
  INPUTCLASS
    NotInput
  DESCRIPTION
    "The residential NON-predominant baseline system number."  
  SIZING_PROPOSED
    if( ( PredominantOcc = "Residential" .OR. ResExcptArea > 0 ) .AND.
        ResFuelSrcExcptArea > 0 )
    then
      if( ResPredominantHtgFuelSrc = 0 )
      then 1 // ResPredominantHtgFuelSrc = 1 (Fossil), so NonpredominantFuelSrc = 0 (Electric), PTHP
      else 2 // ResPredominantHtgFuelSrc = 0 (Electric), so NonpredominantFuelSrc = 1 (Fossil), PTAC
      endif
    else 0 // Baseline design has no residential, non-predominant baseline HVAC systems
    endif
  SIZING_BASELINE
    zp:BaseResNonPredominantSysNum
ENDRULE

// -----------------------------------------------------------------------------
//            Create baseline fluid systems
// -----------------------------------------------------------------------------

// ---------- Create baseline HW system -------------------------------
RULE NEW Proj:BaseHWFluidSysRef
  DATATYPE
    FluidSys
  LONGFORM
    BaselineHotWaterFluidSystemReference    
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC )
    then
      if( MaxChild( Bldg:TotCondFlrArea) > 0 .AND.
        ( MaxChild( Bldg:BaseResPredominantSysNum ) = 1 .OR.
          MaxChild( Bldg:BaseResNonPredominantSysNum ) = 1 .OR.
          MaxChild( Bldg:BaseNonResPredominantSysNum ) = 5 .OR.
          MaxChild( Bldg:BaseNonResNonPredominantSysNum ) = 5 .OR.
          MaxChild( Bldg:BaseNonResPredominantSysNum ) = 7 .OR.
          MaxChild( Bldg:BaseNonResNonPredominantSysNum ) = 7 )
        ) 
      then RuleLibrary( FluidSys, "BaseHWSystem" ) 
      else UNDEFINED
      endif
    else UNDEFINED
    endif
 ENDRULE
// Connect baseline Blr to baseline HW plant FluidSegs and bring in primary Pump
RULE Blr:FluidSegOutRef
  SIZING
    if( FluidSys:IsBaseSys > 0 )
    then RuleLibrary( FluidSeg, "BaseHWPriSupSeg", 0, "BaseHWSystem" )
    else UNCHANGED
    endif
ENDRULE
RULE Blr:FluidSegInRef
  SIZING
    if( FluidSys:IsBaseSys > 0 )
    then RuleLibrary( FluidSeg, "BaseHWPriRetSeg", 0, "BaseHWSystem" )
    else UNCHANGED
    endif
ENDRULE

// ---------- Create baseline CW system -------------------------------
RULE NEW Proj:BaseCWFluidSysRef
  DATATYPE
    FluidSys
  LONGFORM
    BaselineCondenserWaterFluidSystemReference    
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC )
    then 
      if( MaxChild( Bldg:TotCondFlrArea ) > 0 .AND.
          ( MaxChild( Bldg:BaseNonResPredominantSysNum ) = 7 .OR.
            MaxChild( Bldg:BaseNonResNonPredominantSysNum ) = 7 .OR.
            MaxChild( Bldg:BaseNonResPredominantSysNum ) = 8 .OR.
            MaxChild( Bldg:BaseNonResNonPredominantSysNum ) = 8 )
        ) 
      then RuleLibrary( FluidSys, "BaseCWSystem" ) 
      else UNDEFINED
      endif
    else UNDEFINED
    endif
 ENDRULE
RULE HtRej:FluidSegOutRef
  SIZING
    if( FluidSys:IsBaseSys > 0 )
    then RuleLibrary( FluidSeg, "BaseCWPriSupSeg", 0, "BaseCWSystem" )
    else UNCHANGED
    endif
ENDRULE
RULE HtRej:FluidSegInRef
  SIZING
    if( FluidSys:IsBaseSys > 0 )
    then RuleLibrary( FluidSeg, "BaseCWPriRetSeg", 0, "BaseCWSystem" )
    else UNCHANGED
    endif
ENDRULE

// ---------- Create baseline ChW system -------------------------------
RULE NEW Proj:BaseChWFluidSysRef
  DATATYPE
    FluidSys
  LONGFORM
    BaselineChilledWaterFluidSystemReference    
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC )
    then 
      if( LocalCompAssigned( BaseCWFluidSysRef ) )
      then RuleLibrary( FluidSys, "BasePriSecChWSystem" ) 
      else UNDEFINED
      endif
    else UNDEFINED
    endif
 ENDRULE

// Connect baseline Chlr to baseline ChW plant FluidSegs and bring in primary Pump
RULE Chlr:EvapFluidSegOutRef
  SIZING
    if( FluidSys:IsBaseSys > 0 )
    then RuleLibrary( FluidSeg, "BasePriSecChWPriSupSeg", 0, "BasePriSecChWSystem" )
    else UNCHANGED
    endif
ENDRULE
RULE Chlr:EvapFluidSegInRef
  SIZING
    if( FluidSys:IsBaseSys > 0 )
    then RuleLibrary( FluidSeg, "BasePriSecChWPriRetSeg", 0, "BasePriSecChWSystem" )
    else UNCHANGED
    endif
ENDRULE
RULE Chlr:CndsrFluidSegOutRef
  SIZING
    if( FluidSys:IsBaseSys > 0 .AND. CompExists( FluidSeg, "BaseCWPriRetSeg" ) )
    then "BaseCWPriRetSeg"
    else UNCHANGED
    endif
ENDRULE
RULE Chlr:CndsrFluidSegInRef
  SIZING
    if( FluidSys:IsBaseSys > 0 .AND. CompExists( FluidSeg, "BaseCWPriSupSeg" ) )
    then "BaseCWPriSupSeg"
    else UNCHANGED
    endif
ENDRULE

// Bring in secondary loop FluidSegs and secondary Pump
RULE NEW FluidSys:SecSupSegRef
  DATATYPE
    FluidSeg
  LONGFORM
    SecondarySupplySegmentReference
  INPUTCLASS
    NotInput
  SIZING
    if( IsBaseSys .AND. Type = "ChilledWater" )
    then RuleLibrary( FluidSeg, "BasePriSecChWSecSupSeg", 0, "BasePriSecChWSystem" )
    else UNDEFINED
    endif
ENDRULE
RULE NEW FluidSys:SecRetSegRef
  DATATYPE
    FluidSeg
  LONGFORM
    SecondaryReturnSegmentReference
  SIZING
    if( IsBaseSys .AND. Type = "ChilledWater" )
    then RuleLibrary( FluidSeg, "BasePriSecChWSecRetSeg", 0, "BasePriSecChWSystem" )
    else UNDEFINED
    endif
ENDRULE



// -----------------------------------------------------------------------------
//            Create baseline flr-by-flr multizone HVAC systems
// -----------------------------------------------------------------------------
RULE NEW Story:BaseHVACArea
  DATATYPE
    Float
  LONGFORM
    BaselineHVACArea
  DESCRIPTION
    "The area used to determine whether a baseline flr-by-flr HVAC system should
     be created for the Story."
  INPUTCLASS
    NotInput
  SIZING_PROPOSED
    SumChildrenIf( Spc:CondFlrArea, Spc:IsNewHVAC = 1 ) - 
    SumChildrenIf( Spc:ProcArea, Spc:IsNewHVAC = 1 )
// Subtract out slave zone area baseline multizone systems tracks proposed
// control/slave zone relationship
//    - SumChildrenIf( Spc:CondFlrArea, Spc:IsCtrlZn = -1 )
  SIZING_BASELINE
    zp:BaseHVACArea
ENDRULE 

// Create predominant nonresidential floor-by-floor system, if applicable
RULE NEW Story:BasePredominantFuelFlrSysRef
  DATATYPE
    AirSys
  LONGFORM
    BaselinePredominantFuelFloorSystemReference
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC .AND. Bldg:BaseNonResPredominantSysNum > 0)
    then 
      if( ( NonResFlrAreaNewHVACWithMult - 
            ( ProcAreaNew * Mult ) - 
            ( HtgOnlySysAreaNew * Mult ) 
          ) > 0 )
          // Floor has nonresidential space with new HVAC, so create baseline system
;        .OR.
;        ( IsNewEnv .AND. Proj:CompType != "NewComplete" ) ) 
;        // Is Partial Envelope, so need to create baseline HVAC systems for proposed
      then
        if( Bldg:NonResPredominantHtgFuelSrc = 1 .AND. 
            Story:NonResCondFlrAreaFossilHt > 0 )
        then 
          switch( Bldg:BaseNonResPredominantSysNum )
            case 5  : // PVAV w/ HW Reheat
              RuleLibrary( AirSys, "BaseAirSys5", 1 ) 
            case 7  : // VAV w/ HW reheat 
              RuleLibrary( AirSys, "BaseAirSys7", 1 ) 
            default : UNDEFINED
          endswitch
        else
        if( Bldg:NonResPredominantHtgFuelSrc = 0 .AND. 
            Story:NonResCondFlrAreaElecHt > 0 )
        then 
          switch( Bldg:BaseNonResPredominantSysNum )
            case 6  : // PVAV w/ PFP boxes 
              RuleLibrary( AirSys, "BaseAirSys6", 1 ) 
            case 8  : // VAV w/ PFP boxes 
              RuleLibrary( AirSys, "BaseAirSys8", 1 ) 
            default : UNDEFINED
          endswitch
        else UNDEFINED
        endif endif
      else UNDEFINED
      endif
    else UNCHANGED
    endif
 ENDRULE
// Non-predominant system, if applicable
RULE NEW Story:BaseNonPredominantFuelFlrSysRef
  DATATYPE
    AirSys
  LONGFORM
    BaselineNonePredominantFuelFloorSystemReference
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC .AND. Bldg:BaseNonResNonPredominantSysNum > 0 )
    then 
      if( ( NonResFlrAreaNewHVACWithMult - 
            ( ProcAreaNew * Mult ) - 
            ( HtgOnlysysAreaNew * Mult )  
          ) > 0 )
          // Floor has nonresidential space with new HVAC, so create baseline system
;        .OR.
;        ( IsNewEnv .AND. Proj:CompType != "NewComplete" ) ) 
;        // Is Partial Envelope, so need to create baseline HVAC systems for proposed
      then
        if( Bldg:NonResPredominantHtgFuelSrc = 0 .AND. 
            Story:NonResCondFlrAreaFossilHt > 0 )
        then // Predominant is Electric, so non-predominant is Fossil
          switch( Bldg:BaseNonResNonPredominantSysNum )
            case 5  : // PVAV w/ HW Reheat
              RuleLibrary( AirSys, "BaseAirSys5", 1 ) 
            case 7  : // VAV w/ HW reheat 
              RuleLibrary( AirSys, "BaseAirSys7", 1 ) 
            default : UNDEFINED
          endswitch
        else
        if( Bldg:NonResPredominantHtgFuelSrc = 1 .AND. 
            Story:NonResCondFlrAreaElecHt > 0 )
        then // Predominant is Fossil, so non-predominant is Electric
          switch( Bldg:BaseNonResNonPredominantSysNum )
            case 6  : // PVAV w/ PFP boxes 
              RuleLibrary( AirSys, "BaseAirSys6", 1 ) 
            case 8  : // VAV w/ PFP boxes 
              RuleLibrary( AirSys, "BaseAirSys8", 1 ) 
            default : UNDEFINED
          endswitch
        else UNDEFINED
        endif endif
      else UNDEFINED
      endif
    else UNCHANGED
    endif
 ENDRULE

// -----------------------------------------------------------------------------
//            Create baseline single-zone HVAC systems
// -----------------------------------------------------------------------------
RULE NEW ThrmlZn:IsResZnSys
  DATATYPE
    Integer
  LONGFORM
    IsResidentialZoneSystem
  INPUTCLASS
    NotInput
  SIZING
    if( ( Bldg:BaseResPredominantSysNum > 0 .OR.
          Bldg:BaseResNonPredominantSysNum > 0 )
        .AND. IsRes )
    then 1 // Zone gets single-zone Residential system
    else if( PredominantOcc = "Residential" .AND. NonResExcptArea = 0 )
    then 1 // Zone gets single-zone Residential system
    else 0 // Zone does not get single-zone Residential system
    endif endif
ENDRULE

RULE NEW ThrmlZn:IsNonResZnSys
  DATATYPE
    Integer
  LONGFORM
    IsNonresidentialZoneSystem
  INPUTCLASS
    NotInput
  SIZING
    if( ( Bldg:BaseNonResPredominantSysNum = 3 .OR.
          Bldg:BaseNonResNonPredominantSysNum = 3 )
         .OR.
        ( Bldg:BaseNonResPredominantSysNum = 4 .OR.
          Bldg:BaseNonResNonPredominantSysNum = 4 )
      )
    then 1 // Zone gets single-zone Nonresidential system
    else 0 // Zone does not get single-zone Nonresidential system
    endif
ENDRULE

// Create single-zone baseline AirSystems
RULE NEW ThrmlZn:BaseZnAirSysRef
  DATATYPE
    AirSys
  LONGFORM
    BaselineZoneAirSysReference
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC .AND. 
        IsCtrlZn > 0 .AND.
        NotEnclosedZn = 0 .AND.
        IsExistingHVAC <= 0 ) 
    then
      if( HtgOnlySysArea > 0 ) // Zone is heated-only warehouse
      then // Section 3.1.2 Exception (e) systems
        if( HtgFuelSrc = 1 )
        then // HV w/ gas furnace
          RuleLibrary( AirSys, "BaseAirSys9", 1 ) 
        else // HV w/ elec furnace
          RuleLibrary( AirSys, "BaseAirSys10", 1 ) 
        endif
      else // Other nonresidential single-zone systems
      if( ( Bldg:BaseNonResPredominantSysNum > 0 .OR.
            Bldg:BaseNonResNonPredominantSysNum > 0 ) 
          .AND. IsNonResZnSys = 1 .AND. IsResZnSys = 0 )
      then // Zone gets a single-zone Nonresidential system
        if( Bldg:BaseNonResNonPredominantSysNum > 0 )
        then // There is a non-predominant Nonresidential system
          switch( Bldg:BaseNonResNonPredominantSysNum )
            case 3 : // Fossil, SZ-AC
              RuleLibrary( AirSys, "BaseAirSys3", 1 )
            case 4 : // Electric, SZ-HP
              RuleLibrary( AirSys, "BaseAirSys4", 1 )
            default : UNDEFINED
          endswitch
        else // There is NO non-predominant Nonresidential system
          switch( Bldg:BaseNonResPredominantSysNum )
            case 3 : // Fossil, SZ-AC
              RuleLibrary( AirSys, "BaseAirSys3", 1 )
            case 4 : // Electric, SZ-HP
              RuleLibrary( AirSys, "BaseAirSys4", 1 )
            default : UNDEFINED
          endswitch
        endif
      else UNDEFINED
      endif endif
    else UNCHANGED
    endif
ENDRULE

// Create baseline ZoneSystems
RULE NEW ThrmlZn:BaseZnZnSysRef
  DATATYPE
    ZnSys
  LONGFORM
    BaselineZoneZoneSystemReference
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC .AND. 
        IsCtrlZn > 0 .AND.
        NotEnclosedZn = 0 .AND.
        IsExistingHVAC <= 0 ) 
    then
      if( IsResZnSys )
      then // Zone is eligible for a Residential system
        if( Bldg:BaseResNonPredominantSysNum > 0 )
        then // There is a non-predominant Residential system
          switch( Bldg:BaseResNonPredominantSysNum )
            case 1 : // PTAC
              RuleLibrary( ZnSys, "BaseZnSys1", 1 )
            case 2 : // PTHP
              RuleLibrary( ZnSys, "BaseZnSys2", 1 )
            default : UNDEFINED
          endswitch
        else // There is NO non-predominant Residential system
          switch( Bldg:BaseResPredominantSysNum )
            case 1 : // PTAC
              RuleLibrary( ZnSys, "BaseZnSys1", 1 )
            case 2 : // PTHP
              RuleLibrary( ZnSys, "BaseZnSys2", 1 )
            default : UNDEFINED
          endswitch
        endif
      else UNDEFINED
      endif
    else UNCHANGED
    endif
ENDRULE


// -----------------------------------------------------------------------------
//            Connect baseline water coils to baseline fluid systems
// -----------------------------------------------------------------------------
// Assign HW system to main system unit coils
RULE CoilHtg:FluidSegInRef
  SIZING
    if( Proj:CreateHVAC .AND. 
        LocalCompAssigned( FluidSegInRef ) = 0 .AND.
        Type = "HotWater" )
    then "BaseHWPriSupSeg"
    else UNCHANGED
    endif
ENDRULE
RULE CoilHtg:FluidSegOutRef
  SIZING
    if( Proj:CreateHVAC .AND. 
        LocalCompAssigned( FluidSegOutRef ) = 0 .AND.
        Type = "HotWater" )
    then "BaseHWPriRetSeg"
    else UNCHANGED
    endif
ENDRULE
// Assign CHW to main system cooling coils
RULE CoilClg:FluidSegInRef
  SIZING
    if( Proj:CreateHVAC .AND. 
        LocalCompAssigned( FluidSegInRef ) = 0 .AND.
        Type = "ChilledWater" )
    then "BasePriSecChWSecSupSeg"
    else UNCHANGED
    endif
ENDRULE
RULE CoilClg:FluidSegOutRef
  SIZING
    if( Proj:CreateHVAC .AND. 
        LocalCompAssigned( FluidSegOutRef ) = 0 .AND.
        Type = "ChilledWater" )
    then "BasePriSecChWSecRetSeg"
    else UNCHANGED
    endif
ENDRULE


// Assign baseline floor system to spaces
// See ThrmlZn:NumStory rule for check that spaces in a ThrmlZn do not span
// differnt BuildingStory objects
// First, set a flag at Spc that indicates whether there is a 
// Story:XxxFlrSysRef (AirSys)
RULE NEW Spc:HasBaseFlrSys
  DATATYPE
    Integer
  LONGFORM
    HasFloorSystemReference
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC .AND. 
        OccClass = "Nonresidential" .AND.
        ThrmlZnRef:IsExistingHVAC <= 0 )
    then
      if( ParentStatus( BasePredominantFuelFlrSysRef ) > 0 .OR.
          ParentStatus( BaseNonPredominantFuelFlrSysRef ) > 0 ) ; .OR.
;          ParentStatus( LabFlrSysRef ) > 0 )
      then 1
      else 0
      endif
    else 0
    endif    
ENDRULE
// If there is a FlrSys, set a reference to that AirSys object at the space
RULE NEW Spc:BaseFlrSysRefAtSpc
  DATATYPE
    AirSys
  LONGFORM
    BaselineFloorSystemReferenceAtSpace
  INPUTCLASS
    NotInput
  SIZING
    if( HasBaseFlrSys > 0 .AND. ThrmlZnRef:IsNonResZnSys = 0 )
    then // The space served by a floor-by-floor system
      if( ParentCompAssigned( BaseNonPredominantFuelFlrSysRef ) .AND. 
          ( Bldg:NonResPredominantHtgFuelSrc = 1 .AND. HtgFuelSrc = 0 ) )
      then // Space is served by the non-predominant fuel floor-by-floor system
        Story:BaseNonPredominantFuelFlrSysRef
      else // Space is served by the predominant fuel floor-by-floor system
        Story:BasePredominantFuelFlrSysRef
      endif
;      if( LabArea > 0 .AND. ParentStatus( LabFlrSysRef ) > 0 )
;      then Parent( LabFlrSysRef )
;      else Parent( BaseFlrSysRef )
;      endif
    else UNDEFINED
    endif    
ENDRULE
// Set a reference to the Spc at the ThrmlZn if the Spc has a FlrSys
RULE NEW ThrmlZn:HasBaseFlrSysSpcRef
  DATATYPE
    Spc
  LONGFORM
    HasBaseFloorSystemSpaceReference
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC .AND. 
        IsExistingHVAC <= 0 )
    then MaxRevRefComp( Spc:ThrmlZnRef, Spc:HasBaseFlrSys )
    else UNDEFINED
    endif    
ENDRULE
// Set ThrmlZn:BaseFlrSysRefAtZn to be the same as Spc:FlrSysRefAtSpc 
RULE NEW ThrmlZn:BaseFlrSysRefAtZn
  DATATYPE
    AirSys
  LONGFORM
    BaseFloorSystemReference
  INPUTCLASS
    NotInput
  SIZING
    if( Proj:CreateHVAC .AND. 
        LocalStatus( HasBaseFlrSysSpcRef ) > 0 )
    then HasBaseFlrSysSpcRef:BaseFlrSysRefAtSpc
    else UNDEFINED
    endif
ENDRULE

// ---------- Assign baseline systems to zones ---------------------------------
// Primary air conditioning system reference
RULE ThrmlZn:PriAirCondgSysRef
// Proposed system assignment is defined by user.
// See ThermalZone-General.rule for DESCRIPTION and HELP     
  SIZING
    if( Type = "Unconditioned" )
    then UNDEFINED
    else
    if( Proj:CreateHVAC .AND. 
        Type = "Conditioned" .AND.
        IsExistingHVAC <= 0 .AND.
        NotEnclosedZn = 0 )
    then     
      if( LocalCompAssigned( BaseZnAirSysRef ) )
      then // Zone served by a baseline single-zone AirSys
        BaseZnAirSysRef
      else if( LocalCompAssigned( BaseZnZnSysRef ) )
      then // Zone served by a baseline single-zone ZnSys
        BaseZnZnSysRef
      else if( LocalCompAssigned( BaseFlrSysRefAtZn ) )
      then // Zone served by a baseline floor AirSys
        BaseFlrSysRefAtZn
      else UNDEFINED
      endif endif endif
    else UNCHANGED
    endif endif
ENDRULE

// Evaluate rule again to assign slave zones of proposed design as slave zones
// of the baseline design if baseline is a single-zone AirSys, i.e. BaseZnAirSysRef
// is assigned. 
RULE ThrmlZn:PriAirCondgSysRef
// Proposed system assignment is defined by user.
// See ThermalZone-General.rule for DESCRIPTION and HELP     
  SIZING
    if( Proj:CreateHVAC .AND. 
        Type = "Conditioned" .AND.
        IsExistingHVAC <= 0 .AND.
        NotEnclosedZn = 0 .AND.
        IsCtrlZn = -1 .AND. // This is a slave zone
        LocalCompAssigned( SlaveCtrlZnRef ) )
    then 
      if( LocalCompAssigned( HasBaseFlrSysSpcRef ) )
      then // Baseline system is flr-by-flr, leave slave zones assigned to it
        UNCHANGED 
      else // Baseline system is single-zone, assign slave zones to
           // same baseline system as proposed control zone
        SlaveCtrlZnRef:PriAirCondgSysRef
      endif
    else UNCHANGED
    endif
ENDRULE

// Ventilation system reference
RULE ThrmlZn:VentSysRef
// Proposed transformation definitions initialized to be the same as user input.  
  SIZING
    if( Proj:CreateHVAC .AND. 
        LocalCompAssigned( PriAirCondgSysRef ) .AND.
        IsExistingHVAC <= 0 .AND.
        VentIsRequired .AND.
        NotEnclosedZn = 0 .AND.
        VentSrc = "Forced" )
    then PriAirCondgSysRef 
      // For baseline design, vent system is same as heating/cooling system
    else UNCHANGED
    endif
ENDRULE


// ---------- Create baseline terminal units -----------------------------------
RULE ThrmlZn:TrmlUnitRef  
// Property defined and proposed rules in ThermalZone-General.rule
  SIZING
    if( Proj:CreateHVAC .AND. 
        Type = "Conditioned" .AND.
        IsExistingHVAC <= 0 .AND.
        NotEnclosedZn = 0 )
      then
      if( IsCtrlZn > 0 .AND. 
          LocalCompAssigned( PriAirCondgSysRef ) = ComponentType ("AirSys") )
      // Conditioned zones that have zone thermostats
      then
        switch( PriAirCondgSysRef:BaseSysNum )
          case 3  : // SZAC
            RuleLibrary( TerminalUnit, "BaseUnctrl TrmlUnit", 1, PriAirCondgSysRef ) 
          case 4  : // SZHP
            RuleLibrary( TerminalUnit, "BaseUnctrl TrmlUnit", 1, PriAirCondgSysRef ) 
          case 5  : // PVAV w/ HW Reheat
            RuleLibrary( TerminalUnit, "BaseVAVBox TrmlUnit", 1, PriAirCondgSysRef )   
          case 6  : // PVAV w/ PFP Boxes
            RuleLibrary( TerminalUnit, "BasePFPBox TrmlUnit", 1, PriAirCondgSysRef ) 
          case 7  : // VAV w/ HW Reheat
            RuleLibrary( TerminalUnit, "BaseVAVBox TrmlUnit", 1, PriAirCondgSysRef )    
          case 8  : // VAV w/ PFP Boxes
            RuleLibrary( TerminalUnit, "BasePFPBox TrmlUnit", 1, PriAirCondgSysRef ) 
          case 9  : // HV w/ gas furnace
            RuleLibrary( TerminalUnit, "BaseUnctrl TrmlUnit", 1, PriAirCondgSysRef ) 
          case 10 : // HV w/ elec furnace
            RuleLibrary( TerminalUnit, "BaseUnctrl TrmlUnit", 1, PriAirCondgSysRef ) 
          default : UNDEFINED
         endswitch
      else if( LocalCompAssigned( PriAirCondgSysRef ) = ComponentType ("AirSys") )
      then
        if( PriAirCondgSysRef:Type = "PVAV" .OR. PriAirCondgSysRef:Type = "VAV" )
        then // Conditioned zones that are not control zones
          switch( PriAirCondgSysRef:BaseSysNum )
            case 5  : // PVAV w/ HW Reheat
              RuleLibrary( TerminalUnit, "BaseVAVBox TrmlUnit", 1, PriAirCondgSysRef ) 
            case 6  : // PVAV w/ PFP Boxes
              RuleLibrary( TerminalUnit, "BasePFPBox TrmlUnit", 1, PriAirCondgSysRef ) 
            case 7  : // VAV w/ HW Reheat
              RuleLibrary( TerminalUnit, "BaseVAVBox TrmlUnit", 1, PriAirCondgSysRef ) 
            case 8  : // VAV w/ PFP Boxes
              RuleLibrary( TerminalUnit, "BasePFPBox TrmlUnit", 1, PriAirCondgSysRef ) 
            default : UNDEFINED
          endswitch
        else // Conditioned slave zone
          RuleLibrary( TerminalUnit, "BaseUnctrl TrmlUnit", 1, PriAirCondgSysRef )
        endif
      else UNDEFINED // Zone served by ZnSys, and TrmlUnit is not used
      endif endif
    else UNCHANGED
    endif         
ENDRULE
// Assign terminal unit zone served to the thermal zone
RULE TrmlUnit:ZnServedRef
// Proposed transformation definitions initialized to be the same as user input.  
  SIZING
    if( AirSys:BaseSysNum > 0 .AND. 
        ValidOr( NotEnclosedZn, 0 ) = 0 .AND.
        IsNew )
    then MaxRevRefComp( ThrmlZn:TrmlUnitRef, ThrmlZn:FlrArea )
    else UNCHANGED
    endif
ENDRULE
// Assign TerminalUnits to AirSegments
RULE TrmlUnit:PriAirSegRef
  SIZING
    if( AirSys:BaseSysNum > 0 .AND. 
        LocalCompAssigned( ZnServedRef ) .AND.
        ValidOr( NotEnclosedZn, 0 ) = 0 .AND.
        IsNew )
    then // Assign terminal unit by baseline cooling system reference
      ZnServedRef:PriAirCondgSysRef:SupAirSegRef 
    else UNCHANGED
    endif
ENDRULE
// Assign Control Zone for single zone systems
RULE AirSys:CtrlZnRef
  SIZING
    if( ValidOr( BaseSysNum, 0 ) > 0 .AND.
        IsNew )
    then
      if( BaseSysNum = 3 .OR. // SZAC
          BaseSysNum = 4 .OR. // SZHP
          BaseSysNum = 9 .OR. // HV w/ gas furnace
          BaseSysNum = 10 )   // HV w/ elec furnace
      then MaxRevRefComp( ThrmlZn:PriAirCondgSysRef, ThrmlZn:IsCtrlZn )
      else UNDEFINED
      endif
    else UNCHANGED
    endif
ENDRULE
// Assign HW system to terminal unit coils
RULE AirSys:TrmlUnit:CoilHtg:FluidSegInRef
  SIZING
    if( AirSys:BaseSysNum > 0 .AND. 
        LocalCompAssigned( FluidSegInRef ) = 0 .AND.
        Type = "HotWater" .AND.
        ValidOr( TrmlUnit:NotEnclosedZn, 0 ) = 0 .AND.
        IsNew )
    then "BaseHWPriSupSeg"
    else UNCHANGED
    endif
ENDRULE
RULE AirSys:TrmlUnit:CoilHtg:FluidSegOutRef
  SIZING
    if( AirSys:BaseSysNum > 0 .AND. 
        LocalCompAssigned( FluidSegOutRef ) = 0 .AND.
        Type = "HotWater" .AND.
        ValidOr( TrmlUnit:NotEnclosedZn, 0 ) = 0 .AND.
        IsNew )
    then "BaseHWPriRetSeg"
    else UNCHANGED
    endif
ENDRULE


// For all systems, including proposed SZVAV, revise the TrmlUnit:Type
// This is the current workaround for simulating in E+ 
RULE AirSys:TrmlUnit:Type
  SIZING
    if( ( AirSys:Type = "SZVAVAC" .OR. AirSys:Type = "SZVAVHP" )
        .AND. Type = "Uncontrolled" )
    then "VAVNoReheatBox"
    else UNCHANGED
    endif
ENDRULE


// -----------------------------------------------------------------------------
// Establish baseline system number at object
RULE NEW ThrmlZn:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    if( LocalCompAssigned( PriAirCondgSysRef ) )
    then PriAirCondgSysRef:BaseSysNum
    else 0
    endif
  ANNUAL
    z:BaseSysNum
ENDRULE
RULE NEW Spc:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    if( LocalCompAssigned( ThrmlZnRef ) )
    then ThrmlZnRef:BaseSysNum
    else 0
    endif
  ANNUAL
    z:BaseSysNum
ENDRULE
RULE NEW TrmlUnit:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    AirSys:BaseSysNum
  ANNUAL
    z:BaseSysNum
ENDRULE
RULE NEW OACtrl:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    AirSys:BaseSysNum
  ANNUAL
    z:BaseSysNum
ENDRULE
RULE NEW Fan:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    if( ParentComponentType() = "ZnSys" )
    then ZnSys:BaseSysNum
    else AirSys:BaseSysNum
    endif
  ANNUAL
    z:BaseSysNum
ENDRULE
RULE NEW CoilClg:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    if( ParentComponentType() = "ZnSys" )
    then ZnSys:BaseSysNum
    else AirSys:BaseSysNum
    endif
  ANNUAL
    z:BaseSysNum
ENDRULE
RULE NEW CoilHtg:BaseSysNum
  DATATYPE
    Integer
  LONGFORM
    BaselineSystemNumber
  SIZING
    if( ParentComponentType() = "ZnSys" )
    then ZnSys:BaseSysNum
    else AirSys:BaseSysNum
    endif
  ANNUAL
    z:BaseSysNum
ENDRULE

// Set flag to indicate if the system is a proposed system
RULE NEW AirSys:IsPropSys
  DATATYPE
    Integer
  LONGFORM
    IsProposedSystem
  SIZING
    if( IfValidAnd( BaseSysNum > 0 ) = 0 )
    then 1
    else 0
    endif
  ANNUAL
    z:IsPropSys
ENDRULE
RULE NEW ZnSys:IsPropSys
  DATATYPE
    Integer
  LONGFORM
    IsProposedSystem
  SIZING
    if( IfValidAnd( BaseSysNum > 0 ) = 0 )
    then 1
    else 0
    endif
  ANNUAL
    z:IsPropSys
ENDRULE
