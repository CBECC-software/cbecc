// ThermalZone - Zone Level Sizing
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  

//  This rule file addresses the following building descriptors:

     

// ********** Design Supply Air Temperatures ***********************************
RULE NEW ThrmlZn:IsExtZn
  DATATYPE
    Integer
  LONGFORM
    IsExteriorZone
  DESCRIPTION
    "A flag that specifies if a thermal zone is an Interior or Exterior zone." 
  HELP
    "For the purposes of the ACM, an exterior zone is defined as one that has 
     any exterior walls, ceilings or floors. An interior zone is defined as 
     having no surfaces exposed to ambient or below-grade conditions.
     See building descriptor 'Design Airflow' for detail."
  REFERENCE 
    NACM Section 5.6.5.1
  INPUTCLASS 
    NotInput
  DEFAULT
    if( MaxRevRef( Spc:ThrmlZnRef, Spc:IsExtSpc ) > 0 )
    then 1 // Zone has exterior exposure
    else 0 // Zone has no exterior exposure
    endif
  SIZING
    u:IsExtZn
  ANNUAL
    z:IsExtZn
ENDRULE

TABLE DsgnTStatSetpt
  SchGrpName            MaxHtgSetpt       MinClgSetpt
  "Assembly"            70.0              75.0
  "Data"                60.0              80.0  
  "Health"              70.0              75.0
  "Laboratory"          70.0              75.0
  "Manufacturing"       70.0              75.0
  "Office"              70.0              75.0
  "Parking"             60.0              85.0
  "ResidentialLiving"   68.0              78.0
  "ResidentialCommon"   68.0              78.0
  "Restaurant"          70.0              75.0
  "Retail"              70.0              75.0
  "School"              70.0              75.0
  "Warehouse"           70.0              75.0
ENDTABLE


// ==================== COOLING ================================================
RULE ThrmlZn:ClgDsgnSizingFac
  DESCRIPTION
    "The factor used to adjust the design cooling air flow. 
     Applicable to sizing runs only."
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Cooling Sizing Factor."
  INPUTCLASS 
    NotInput
  MINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  MAXIMUM
    2.0
  DEFAULT
    if( IsCond )
    then 1.0
    else UNDEFINED
    endif
  SIZING
    if( IsCond )
    then 1.0
    else UNDEFINED
    endif
// TO DO: Add logic for adjusting sizing factor if BASELINE UMLHs too high 
  ANNUAL  
    z:ClgDsgnSizingFac
ENDRULE
RULE ThrmlZn:ClgDsgnFlow
  DESCRIPTION
    "The air flow provided to the thermal zone at the design cooling condition.  
     Applicable to sizing runs only."
  INPUTCLASS 
    NotInput
  SIZING
    UNDEFINED // Not input, sizing is based on ClgDsgnSupAirTemp
  ANNUAL  
    // Calculated from z:HtgDsgnFlowSim. This value reflects zone multipliers, 
    // so SDD reported value is divided by ThrmlZn:Mult
    if( IsCond .AND. IfValidAnd( z:ClgDsgnFlowSim >= 0 ) ) 
    then z:ClgDsgnFlowSim / Mult
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:ClgDsgnFlowSim
  DESCRIPTION
    "The air flow provided to the thermal zone at the design cooling conditions,
     For simulation only, includes zone multipliers."  
  HELP
    "Used only for bi-directional communication with simulation engine."
  INPUTCLASS 
    NotInput
//SIZING
    // Autosize
    // Source for Autosize data:
    // E+ Report: HVAC Sizing Summary -> Zone Heating -> Calculated Design Air Flow
    // This simulation value includes zone multipliers   
  ANNUAL
    UNDEFINED
ENDRULE
RULE ThrmlZn:ClgDsgnSupAirTempDiff
  DESCRIPTION
    "The temperature difference between the cooling supply air temperature
     and room air temperature used for sizing zone/system air flows." 
  HELP
    "This value, combined with the design zone temperature setpoint, is used to 
     calculated the default design supply air temperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.3
  INPUTCLASS 
    Default
  MINIMUM
    3
  COMMONMAXIMUM
    21
  MAXIMUM
    40
  DEFAULT : T24N
    if( IsCond .AND. LocalCompAssigned( PriAirCondgSysRef ) )
    then
;      if( IsExtZn > 0 )
;      then 20
;      else 15
;      endif
      if( IfValidAnd( HasClg = 0 ) )
      then UNDEFINED
      else if( LocalCompAssigned( PriAirCondgSysRef ) = ComponentType( "ZnSys" ) )
      then 20
      else if( PriAirCondgSysRef:IsMultiZnSys )
      then
        if( IsExtZn = 0 )
        then 15
        else 20
        endif
      else 20
      endif endif endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IfValidAnd( HasClg > 0 ) )
    then 20
    else UNDEFINED
    endif
  SIZING : T24N
    if( IsCond .AND. LocalCompAssigned( PriAirCondgSysRef )
        .AND. Proj:IsBaseHVAC )
    then
;      if( IsExtZn > 0 )
;      then 20
;      else 15
;      endif
      if( zp:HtgOnlySysArea > 0 )
      then UNDEFINED // Proposed design meets criteria for heating only system
      else if( ( PriAirCondgSysRef:Type = "PVAV" .OR. PriAirCondgSysRef:Type = "VAV" )
               .AND. IsExtZn = 0 )
      then 15
      else 20 // 20F for all single zone systems, see GC issue 857
      endif endif
    else u:ClgDsgnSupAirTempDiff
    endif
  SIZING : S901G ECBC
    if( IsCond .AND. Proj:IsBaseHVAC )
    then 
      if( zp:HtgOnlySysArea > 0 )
      then UNDEFINED
      else 20
      endif
    else u:ClgDsgnSupAirTempDiff
    endif
  ANNUAL
    z:ClgDsgnSupAirTempDiff
ENDRULE
RULE ThrmlZn:ClgDsgnSupAirTemp
  DESCRIPTION
    "The design cooling supply air temperature for sizing zone/system air flows." 
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Cooling Design Supply Air Temperature, and is used for sizing with
     ! - Zone Cooling Design Supply Air Temperature Input Method = SupplyAirTemperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.3
  INPUTCLASS 
    Default
  MINIMUM
    50
  COMMONMINIMUM
    54
  COMMONMAXIMUM
    66
  MAXIMUM
    75
  DEFAULT : T24N
    if( IsCond .AND. LocalCompAssigned( PriAirCondgSysRef ) )
    then
      if( HasClg = 0 )
      then UNDEFINED
      else if( IsRes )
      then 58
      else if( CompRmArea > 0 )
      then 60
      else if( LocalCompAssigned( PriAirCondgSysRef ) = ComponentType( "ZnSys" ) )
      then 55
      else if( PriAirCondgSysRef:IsMultiZnSys .AND. IsExtZn = 0 )
      then 60
      else 55
      endif endif endif endif endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IsCond )
    then
      if( HasClg = 0 )
      then UNDEFINED
      else ValidOr( ClgTstatSchMinVal, 75) - ValidOr( ClgDsgnSupAirTempDiff, 0 )
      endif
    else UNDEFINED
    endif
// TO DO: Uncomment when Research mode is supported
; CHECKSIM
;   if( Proj:HVACAutoSizing = 1 .AND. 
;       IsCond .AND.
;       LocalStatus( ClgDsgnSupAirTemp ) = 0 )
;   then 
;     PostError("The CoolingDesignSupplyAirTemperature for ThermalZone '%s' is 
;                a required input if AutoSizing HVAC equipment.")
;   else UNCHANGED
;   endif
  SIZING : T24N
    if( IsCond .AND. LocalStatus( PredominantFuncGrp ) > 0 
        .AND. Proj:IsBaseHVAC )
    then
      if( zp:HtgOnlySysArea > 0 )
      then UNDEFINED      
      else if( CompRmAreaNew > 0 .OR. CompRmAreaAltered > 0 )
      then 60
      else DsgnTStatSetpt:MinClgSetpt("SchGrpName", PredominantFuncGrp) -
           ClgDsgnSupAirTempDiff
      endif endif
    else u:ClgDsgnSupAirTemp
    endif
  SIZING : S901G ECBC
    if( IsCond .AND. Proj:IsBaseHVAC )
    then
      if( zp:HtgOnlySysArea > 0 )
      then UNDEFINED 
      else ValidOr( ClgTstatSchMinVal, 75) - ValidOr( ClgDsgnSupAirTempDiff, 0 )
      endif
    else u:ClgDsgnSupAirTemp
    endif
  ANNUAL 
    z:ClgDsgnSupAirTemp
ENDRULE

// ==================== HEATING ================================================
RULE ThrmlZn:HtgDsgnSizingFac
  DESCRIPTION
    "The factor used to adjust the design heating air flow. Applicable to sizing
     runs only."
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Heating Sizing Factor."
  INPUTCLASS 
    NotInput
  MINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  MAXIMUM
    2.0
  DEFAULT
    if( IsCond )
    then 1.0
    else UNDEFINED
    endif
  SIZING
    if( IsCond )
    then 1.0
    else UNDEFINED
    endif
// TO DO: Add logic for adjusting sizing factor if BASELINE UMLHs too high 
  ANNUAL  
    z:HtgDsgnSizingFac 
ENDRULE
RULE ThrmlZn:HtgDsgnFlow
  DESCRIPTION
    "The air flow provided to the thermal zone at the design heating condition.  
     Applicable to sizing runs only."  
  INPUTCLASS 
    NotInput
  SIZING
    UNDEFINED // Not input, sizing is based on HtgDsgnSupAirTemp
  ANNUAL  
    // Calculated from z:HtgDsgnFlowSim. This value reflects zone multipliers, 
    // so SDD reported value is divided by ThrmlZn:Mult
    if( IsCond .AND. IfValidAnd( z:HtgDsgnFlowSim >= 0 ) ) 
    then z:HtgDsgnFlowSim / Mult
    else UNDEFINED
    endif
ENDRULE
RULE ThrmlZn:HtgDsgnFlowSim
  DESCRIPTION
    "The air flow provided to the thermal zone at the design heating condition,
     for simulation."  
  HELP
    "Used only for bi-directional communication with simulation engine."
  INPUTCLASS 
    NotInput
//SIZING
    // Autosize
    // Source for Autosize data:
    // E+ Report: HVAC Sizing Summary -> Zone Heating -> Calculated Design Air Flow
    // This simulation value includes zone multipliers  
  ANNUAL
    UNDEFINED
ENDRULE
RULE ThrmlZn:HtgDsgnSupAirTempDiff
  DESCRIPTION
    "The temperature difference between the heating supply air temperature
     and room air temperature for sizing zone/system airflows."
  HELP
    "This value, combined with the design zone temperature setpoint, is used to 
     calculated the design supply air temperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.4
  INPUTCLASS 
    Default
  DEFAULT : T24N
    if( IsCond )
    then
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then ValidOr( HtgDsgnSupAirTemp, 105 ) - ValidOr( HtgTstatSchMaxVal, 70 )
      else if( IsRes )
      then 27
      else 25
      endif endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IsCond )
    then
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then ValidOr( HtgDsgnSupAirTemp, 105 ) - ValidOr( HtgTstatSchMaxVal, 70 )
      else 20
      endif
    else UNDEFINED
    endif
  SIZING : T24N
    if( IsCond .AND. LocalStatus( PredominantFuncGrp ) > 0
        .AND. Proj:IsBaseHVAC )
    then 95 - (DsgnTStatSetpt:MaxHtgSetpt("SchGrpName", PredominantFuncGrp))
    else u:HtgDsgnSupAirTempDiff
    endif
  SIZING : S901G ECBC
    if( IsCond .AND. Proj:IsBaseHVAC )   
    then
      if( HtgOnlySysArea > 0 )
      then 105 - ValidOr( HtgTstatSchMaxVal, 70 )
      else 20
      endif
    else u:HtgDsgnSupAirTempDiff
    endif
  ANNUAL
    z:HtgDsgnSupAirTempDiff
ENDRULE
RULE ThrmlZn:HtgDsgnSupAirTemp
  DESCRIPTION
    "The design heating supply air temperature for sizing zone/system air flows." 
  HELP
    "For EnergyPlus, this property translates to Sizing:Zone, 
     ! - Zone Heating Design Supply Air Temperature, and is used for sizing with
     ! - Zone Heating Design Supply Air Temperature Input Method = SupplyAirTemperature."
  REFERENCE 
    NACM Section 5.6.5.1
    NACM Section 5.7.2.4
  INPUTCLASS 
    Default
  MINIMUM
    60
  COMMONMINIMUM
    74
  COMMONMAXIMUM
    106
  MAXIMUM
    120
  DEFAULT : T24N
    if( IsCond )
    then
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then 95 // Change to 105F?
      else 95 
      endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IsCond )
    then 
      if( IfValidAnd( HtgOnlySysArea > 0 ) )
      then 105
      else ValidOr( HtgTstatSchMaxVal, 70 ) + ValidOr( HtgDsgnSupAirTempDiff, 20 )
      endif
    else UNDEFINED
    endif
// TO DO: Uncomment when Research mode is supported
; CHECKSIM
;   if( Proj:HVACAutoSizing = 1 .AND. 
;       IsCond .AND.
;       LocalStatus( HtgDsgnSupAirTemp ) = 0 )
;   then 
;     PostError("The HeatingDesignSupplyAirTemperature for ThermalZone '%s' is 
;                a required input if AutoSizing HVAC equipment.")
;   else UNCHANGED
;   endif
  SIZING : T24N
    if( IsCond )
    then
      if( Proj:IsBaseHVAC )
      then
        if( HtgOnlySysArea > 0 )
        then 95 // Change to 105F?
        else 95
        endif
      else u:HtgDsgnSupAirTemp
      endif
    else UNDEFINED
    endif
  SIZING : S901G ECBC
    if( IsCond )
    then
      if( Proj:IsBaseHVAC )
      then
        if( HtgOnlySysArea > 0 )
        then 105
        else ValidOr( HtgTstatSchMaxVal, 70 ) + ValidOr( HtgDsgnSupAirTempDiff, 20 )
        endif
      else u:HtgDsgnSupAirTemp
      endif
    else UNDEFINED
    endif
  ANNUAL
    z:HtgDsgnSupAirTemp 
ENDRULE
RULE ThrmlZn:HtgDsgnMaxFlowFrac
  DESCRIPTION
    "The maximum supply air flow fraction during heating for sizing zone reheat
     coils capacity" 
  HELP
    "This property impacts the behavior for EnergyPlus Sizing:Zone. 
     A zero or UNDEFINED value results in Sizing:Zone !- Heating Design Air Flow Method 
     to be DesignDay.  A non-zero value results in DesignDayWithLimit, which should only 
     be used for VAV terminals and SZVAV."
  REFERENCE 
    NACM Section 5.6.5.1
  INPUTCLASS 
    Default
  MINIMUM
    0
  COMMONMINIMUM
    0.15
  COMMONMAXIMUM
    0.51
  MAXIMUM
    1.0
  DEFAULT
    if( IsCond .AND. 
        LocalCompAssigned( TrmlUnitRef ) .AND.
        LocalCompAssigned( PriAirCondgSysRef ) )
      then
      if( TrmlUnitRef:Type = "VAVReheatBox" )
      then
        if( TrmlUnitRef:ReheatCtrlMthd = "DualMaximum" )
        then 
          if( IfValidAnd( TrmlUnitRef:PriAirFlowMax > 0 ) .AND.
              IfValidAnd( TrmlUnitRef:HtgAirFlowMax > 0 ) )
          then Min(0.5, TrmlUnitRef:HtgAirFlowMax/TrmlUnitRef:PriAirFlowMax)
          else 0.5
          endif
        else 
        if( IfValidAnd( TrmlUnitRef:PriAirFlowMax > 0 ) .AND.
            IfValidAnd( TrmlUnitRef:HtgAirFlowMax > 0 ) ) 
        then Min(0.2, TrmlUnitRef:HtgAirFlowMax/TrmlUnitRef:PriAirFlowMax)
        else 0.2
        endif endif  
      else if( TrmlUnitRef:Type = "ParallelFanBox" )
      then 0.5 // For 90.1
      else if( PriAirCondgSysRef:Type = "SZVAVAC" .OR.
               PriAirCondgSysRef:Type = "SZVAVHP" )
      then 0.5 // SZVAV control currently limited to 50%
      else 1.0 // All other cases assume max flow in heating is 100%
      endif endif endif
    else UNDEFINED
    endif
// TO DO: Uncomment when Research mode is supported
; CHECKSIM
;   if( LocalCompAssigned( TrmlUnitRef ) = 0 )
;   then UNCHANGED
;   else
;   if( Proj:HVACAutoSizing = 1 .AND. 
;       IsCond .AND.
;       TrmlUnitRef:Type = "VAVReheatBox" .AND.
;       LocalStatus( HtgDsgnMaxFlowFrac ) = 0 )
;   then 
;     PostError("The HeatingDesignMaxFlowFracton for ThermalZone '%s' is 
;                a required input if AutoSizing HVAC equipment.", Name)
;   else UNCHANGED
;   endif endif
  SIZING
    if( IsCond .AND. LocalCompAssigned( TrmlUnitRef ) )
      then
      if( Proj:IsBaseHVAC )
      then
        if( TrmlUnitRef:Type = "VAVReheatBox" )
        then 
          if( TrmlUnitRef:ReheatCtrlMthd = "DualMaximum" )          
          then 0.5
          else 0.2
          endif
        else if( TrmlUnitRef:Type = "ParallelFanBox" )
        then 0.5 // For 90.1
        else if( PriAirCondgSysRef:Type = "SZVAVAC" .OR.
                 PriAirCondgSysRef:Type = "SZVAVHP" )
        then 0.5 // SZVAV control currently limited to 50%
        else 1.0
        endif endif endif
      else u:HtgDsgnMaxFlowFrac
      endif
    else UNDEFINED
    endif
  ANNUAL
    z:HtgDsgnMaxFlowFrac
ENDRULE


// ** debugging rule evaluation duration **
RULE NEW Proj:RuleEvalDuration
  DEFAULT 
    LogDuration( "            time to evaluate Thermal Zone DEFAULT rules:  %.3f seconds" )
  SIZING_PROPOSED 
    LogDuration( "            time to evaluate Thermal Zone SIZING_PROPOSED rules:  %.3f seconds" )
  SIZING_BASELINE
    LogDuration( "            time to evaluate Thermal Zone SIZING_BASELINE rules:  %.3f seconds" )
  ANNUAL_PROPOSED
    LogDuration( "            time to evaluate Thermal Zone ANNUAL_PROPOSED rules:  %.3f seconds" )
  ANNUAL_BASELINE
    LogDuration( "            time to evaluate Thermal Zone ANNUAL_BASELINE rules:  %.3f seconds" )
ENDRULE

