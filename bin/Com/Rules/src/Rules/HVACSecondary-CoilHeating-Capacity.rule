// HVAC Secondary Systems - Heating Coils - General
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------


//  This rule file addresses the following building descriptors:
//  Section 5.7.6.1 - General
//      Heating Source
//  Section 5.7.6.3 - Hydronic/Steam Heating Coils
//      Heating Coil Capacity
//  Section 5.7.6.4 - Furnace
//      Furnace Capacity
//  Section 5.7.6.5 - Electric Heat Pump
//      Electric Heat Pump Heating Capacity
//      Electric Heat Pump Heating Capacity Adjustment Curve
//      Electric Heat Pump Supplemental Heating Capacity


// -----------------------------------------------------------------------------
// Data model structure QC rules
RULE CoilHtg:FluidSegInRef
  DESCRIPTION
    "Defines the inlet/supply-side fluid segment of hydronic coils."
  HELP
    "Required input for Type = 'HotWater' coils and 'HeatPump' coils with 
     CondenserType = 'Water'."
  INPUTCLASS 
    CondRequired    
  CHECKSIM
    if( Type = "HotWater" ) then
      if( LocalCompAssigned( FluidSegInRef ) = 0 ) then
        PostError("The inlet fluid segment of coil '%s' must be defined", Name)
      else if( FluidSegInRef:ParentFluidSysType != "HotWater" .AND.
               FluidSegInRef:ParentFluidSysType != "CondenserWater" .AND.
               FluidSegInRef:ParentFluidSysType != "ServiceHotWater" ) then
        PostError("The inlet fluid segment of coil '%s' must belong to a 
                   HotWater, CondenserWater, or ServiceHotWater system.", Name)      
      else UNCHANGED
      endif endif
    else if( Type = "HeatPump" .AND. CndsrType = "WaterSource" ) then
      if( LocalCompAssigned( FluidSegInRef ) = 0 ) then
        PostError("The inlet fluid segment of coil '%s' must be defined", Name)
      else if( FluidSegInRef:ParentFluidSysType != "CondenserWater" ) then
        PostError("The inlet fluid segment of coil '%s' must belong to a 
                   CondenserWater system.", Name)      
    else UNCHANGED
      endif endif
    else UNCHANGED
    endif endif
  SIZING
    if( ( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) ) .OR.
        Type = "Resistance" .OR.
        Type = "Furnace" )
    then UNDEFINED // Purge reference for when not applicable
    else UNCHANGED
    endif
  ANNUAL
    z:FluidSegInRef
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:FluidSegOutRef
  DESCRIPTION
    "Defines the outlet/return-side fluid segment of hydronic coils."
  HELP
    "Required input for Type = 'HotWater' coils and 'HeatPump' coils with 
     CondenserType = 'Water'."
  INPUTCLASS 
    CondRequired    
  CHECKSIM
    if( Type = "HotWater" ) then
      if( LocalCompAssigned( FluidSegOutRef ) = 0 ) then
        PostError("The outlet fluid segment of coil '%s' must be defined", Name)
      else if( FluidSegOutRef:ParentFluidSysType != "HotWater" .AND.
               FluidSegOutRef:ParentFluidSysType != "CondenserWater" .AND.
               FluidSegOutRef:ParentFluidSysType != "ServiceHotWater" ) then
        PostError("The outlet fluid segment of coil '%s' must belong to a 
                   HotWater or ServiceHotWater system.", Name)      
      else UNCHANGED
      endif endif
    else if( Type = "HeatPump" .AND. CndsrType = "WaterSource" ) then
      if( LocalCompAssigned( FluidSegOutRef ) = 0 ) then
        PostError("The outlet fluid segment of coil '%s' must be defined.", Name)
      else if( FluidSegOutRef:ParentFluidSysType != "CondenserWater" ) then
        PostError("The outlet fluid segment of coil '%s' must belong to a 
                   CondenserWater system.", Name)      
    else UNCHANGED
      endif endif
    else UNCHANGED
    endif endif
  SIZING
    if( ( Type = "HeatPump" .AND. IfValidAnd( CndsrType = "Air" ) ) .OR.
        Type = "Resistance" .OR.
        Type = "Furnace" )
    then UNDEFINED // Purge reference for when not applicable
    else UNCHANGED
    endif
  ANNUAL
    z:FluidSegOutRef
ENDRULE

// -----------------------------------------------------------------------------
// ParentComponentType set to property of local component, for screen rules
RULE NEW CoilHtg:ParentComp
  DATATYPE
    Enumeration
  LONGFORM
    ParentComponent
  DESCRIPTION
    "ComponentType of Parent; used for screen edit rules and conditional enums."
  INPUTCLASS 
    NotInput 
  OPTION
    AirSys
    ZnSys
    TrmlUnit
  DEFAULT
    if( ParentComponentType() = "AirSeg" )
    then "AirSys"
    else if( ParentComponentType() = "ZnSys" )
    then "ZnSys"
    else ParentComponentType()
    endif endif
  SIZING
    if( ParentComponentType() = "AirSeg" )
    then "AirSys"
    else if( ParentComponentType() = "ZnSys" )
    then "ZnSys"
    else ParentComponentType()
    endif endif
  ANNUAL
    z:ParentComp
ENDRULE

// -----------------------------------------------------------------------------
// SystemType set to property of local component, for screens, rules etc
RULE NEW CoilHtg:SysType
  DATATYPE
    Enumeration
  LONGFORM
    SystemType
  DESCRIPTION
    "Type of Parent; used for system type specific rules and conditional enums."
  INPUTCLASS 
    NotInput 
  OPTION
    PVAV
    VAV
    SZAC
    SZHP
    SZVAVAC
    SZVAVHP
    HV
    FPFC
    WSHP
    PTAC
    PTHP
    Baseboard
    Exhaust
    SPVAC
    SPVHP    
    NA
  DEFAULT
    if( ParentComp = "AirSys" )
    then AirSys:Type
    else if( ParentComp = "ZnSys" )
    then ZnSys:Type
    else "NA"
    endif endif
  SIZING
    if( ParentComp = "AirSys" )
    then AirSys:Type
    else if( ParentComp = "ZnSys" )
    then ZnSys:Type
    else "NA"
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
// Echo system count for reference in UI and capacity/efficiency adjustment rules
RULE CoilHtg:SysCnt
  DESCRIPTION
    "Echo of Air/System:Count, or if the Coil parent is a TerminalUnit, the 
     SystemCount is TerminalUnit:SystemCount * TerminalUnit:Count."
  HELP
    "The number of duplicate systems can only be >1 when all attributes of 
     the system are the same.  If Count is specified to be >1, all parameters
     (capacities, power, etc) should be specified for the single piece of equipment.
     The ruleset will apply multipliers for the final simulation."
  INPUTCLASS
    NotInput
  REPORTPRECISION
    0
  DEFAULT
    if( ParentComp = "AirSys" )
    then // AirSystem
      AirSys:Cnt
    else if( ParentComp = "ZnSys" )
    then // ZoneSystem
      ZnSys:Cnt
    else if( ParentComp = "TrmlUnit" )
    then // TerminalUnit
      TrmlUnit:Cnt
    else 0
    endif endif endif
  SIZING
    if( ParentComp = "AirSys" )
    then // AirSystem
      Parent2( Cnt )
    else if( ParentComp = "ZnSys" )
    then // ZoneSystem
      ZnSys:Cnt 
    else if( ParentComp = "TrmlUnit" )
    then // TerminalUnit
      TrmlUnit:SysCnt
    else 0
    endif endif endif
  ANNUAL
   z:SysCnt
ENDRULE
// Echo system cooling capacity for efficiency check rules
RULE NEW CoilHtg:SysClgCap
  DATATYPE
    Float
  LONGFORM
    SystemCoolingCapacity
  DESCRIPTION
    "The capacity of the systems correpsonding cooling coil. Used for minimum
     efficency and QC checks."
  INPUTCLASS
    NotInput
  DEFAULT
    if( ParentComp = "AirSys" )
    then // AirSystem
      AirSys:ClgCap
    else if( ParentComp = "ZnSys" )
    then // ZoneSystem
      ZnSys:ClgCap
    else UNDEFINED
    endif endif
  SIZING
    if( ParentComp = "AirSys" )
    then // AirSystem
      AirSys:ClgCap
    else if( ParentComp = "ZnSys" )
    then // ZoneSystem
      ZnSys:ClgCap
    else UNDEFINED
    endif endif
  ANNUAL
   z:SysClgCap
ENDRULE
// Define parent system object reference at coil
RULE NEW CoilHtg:ParentAirSysRef
  DATATYPE
    AirSys
  LONGFORM
    AirSystemReference
  DESCRIPTION
    "Reference property for accessing values of system from coils."
  INPUTCLASS
    NotInput
  DEFAULT
    if( ParentComp = "AirSys" .OR. ParentComp = "TrmlUnit" )
    then AirSys:Name
    else UNDEFINED
    endif
  SIZING
    if( ParentComp = "AirSys" .OR. ParentComp = "TrmlUnit" )
    then AirSys:Name
    else UNDEFINED
    endif
  ANNUAL
   z:ParentAirSysRef
ENDRULE
RULE NEW CoilHtg:ParentZnSysRef
  DATATYPE
    ZnSys
  LONGFORM
    ParentZoneSystemReference
  DESCRIPTION
    "Reference property for accessing values of system from coils."
  INPUTCLASS
    NotInput
  DEFAULT
    if( ParentComp = "ZnSys" )
    then ZnSys:Name
    else UNDEFINED
    endif
  SIZING
    if( ParentComp = "ZnSys" )
    then ZnSys:Name
    else UNDEFINED
    endif
  ANNUAL
   z:ParentZnSysRef
ENDRULE
// For FluidSys:CtrlType rule
RULE NEW CoilHtg:CondHVACZnCntWithMult
  DATATYPE
    Integer
  LONGFORM
    ConditionedHVACZoneCountWithMult
  DESCRIPTION
    "The number of conditioned HVAC zones of the coil's parent system."
  INPUTCLASS
    NotInput
  DEFAULT
    if( ParentComp = "AirSys" .OR. ParentComp = "TrmlUnit" )
    then // AirSystem
      AirSys:CondHVACZnCntWithMult
    else if( ParentComp = "ZnSys" )
    then // ZoneSystem
      ZnSys:CondHVACZnCntWithMult
    else 0
    endif endif
  SIZING
    if( ParentComp = "AirSys" .OR. ParentComp = "TrmlUnit" )
    then // AirSystem
      AirSys:CondHVACZnCntWithMult
    else if( ParentComp = "ZnSys" )
    then // ZoneSystem
      ZnSys:CondHVACZnCntWithMult
    else 0
    endif endif
  ANNUAL
    z:CondHVACZnCntWithMult   
ENDRULE


// ********** Status ***********************************************************
RULE CoilHtg:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  INPUTCLASS 
    NotInput
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New'
;   New
;   Existing 
  DEFAULT
// Status defined from top-down. If parent system is altered, child objects
// are defaulted to Existing
    if( Parent( IsNew ) ) then "New" else "Existing" endif
// SIZING and ANNUAL rules not used; status of any new objects created by rules 
// is defined by BEMEnums default
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW CoilHtg:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" ) then 1 else 0 endif
  SIZING : T24N
    if( Status = "New" ) then 1 else 0 endif
  SIZING : S901G ECBC
    if( Status = "New" .AND. Parent( IsNew ) = 1 ) then 1 else 0 endif
  ANNUAL
    z:IsNew
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW CoilHtg:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsNew ) then 0 else 1 endif
  SIZING
    if( IsNew ) then 0 else 1 endif
  ANNUAL
    z:IsExisting
ENDRULE


// ---------- Section 5.7.6.1 - General ----------------------------------------
// ********** Heating Source ***************************************************
RULE CoilHtg:Type
  DESCRIPTION
    "The type of heating coil."
  REFERENCE 
    NACM Section 5.7.6.1
  INPUTCLASS 
    Compulsory   
// Enums defined in BEMBase since property referenced in Library file     
;  OPTION
;    - specify -
;    Resistance
;    Furnace
;    HeatPump  
;    HotWater
;    Steam           // Not yet supported
;  DEFAULT
;    - specify -
  CHECKSIM
    if( ParentComp = "ZnSys" )
    then 
      if( Type = "HeatPump" .AND. 
          ZnSys:Type != "SZHP" .AND. 
          ZnSys:Type != "PTHP" .AND. 
          ZnSys:Type != "WSHP" .AND. 
          ZnSys:Type != "SPVHP" )
      then
        PostError("Coil object '%s' is Type = 'HeatPump', which is only supported
                   for ZoneSystems of Type = SZHP, PTHP, WSHP or SPVHP.", Name)
      else UNCHANGED
      endif
    else UNCHANGED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then // Baseline system definitions defined by Library_HVAC.txt file.
      UNCHANGED
    else u:Type
    endif
  ANNUAL
    z:Type
ENDRULE

// -----------------------------------------------------------------------------
// Fuel Source
RULE CoilHtg:FuelSrc
  DESCRIPTION
    "The fuel source (if any) of the heating coil."
  INPUTCLASS 
    Default   
// Enums defined in BEMBase since property referenced in Library file      
;  OPTION
;    Electric   
;    Gas 
;    Oil            // Not supported
  DEFAULT
    switch ( Type )
      case "Resistance" : "Electric" 
      case "HeatPump"   : "Electric"
      case "Furnace"    : "Gas"
      default : UNDEFINED
    endswitch
  CHECKSIM
    if( FuelSrc = "Oil (no longer valid)" ) then
      PostError( "Gas and Electric are the only currently supported fuels 
                  in CBECC-Com.  Revise the input for CoilHtg '%s'", Name )
    else if( Proj:GasType = "None" .AND. FuelSrc = "Gas" )
    then
      PostError("Heating coil '%s' is gas fired but the project 
                 gas type is specified as none.  Change the fuel source 
                 for the coil or the project gas type.", Name)
    else
      UNCHANGED
    endif endif
  SIZING
    if( Type = "HotWater" .OR. Type = "Steam" )
    then UNDEFINED
    else if( BaseSysNum > 0 )
    then UNCHANGED // Baseline system definitions defined by Library_HVAC.txt file.
    else u:FuelSrc
    endif endif
  ANNUAL
    z:FuelSrc
ENDRULE

// ********** Condenser Type ***************************************************
RULE CoilHtg:CndsrType
  DESCRIPTION
    "The type of condenser for heat pump heating system."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Default     
  OPTION
    Air
    WaterSource
;    GroundwaterSource  // Not supported
;    GroundSource       // Not supported
  DEFAULT
    if( Type = "HeatPump" )
    then "Air"
    else UNDEFINED
    endif
  CHECKCODE
    if( Type = "HeatPump" .AND. LocalStatus( CndsrType ) = 0 ) then
      PostError("Condenser type is a required input for HP coil '%s'.", Name)
    else if( Type = "HeatPump" .AND. CndsrType = "WaterSource" ) then
      if( ParentComp != "ZnSys" ) then
        PostError("Water source heat pump coils such as coil '%s' may not 
                   be assigned to air systems or terminal units.", Name)
      else if( Parent( Type ) != "WSHP" ) then
        PostError("Water source heat pump coils such as coil '%s' may only
                   be assigned to WSHP type zone systems.", Name)
      else UNCHANGED
      endif endif
    else if( Type = "HeatPump" .AND. CndsrType = "Air" ) then
      if( ParentComp = "ZnSys" .AND. Parent( Type ) = "WSHP" ) then
        PostError("Air source heat pump coils such as coil '%s' may not 
                   be assigned to WSHP zone systems.", Name)
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif endif
  SIZING
    if( BaseSysNum > 0 .AND. Type = "HeatPump" )
    then "Air"
    else if( Type = "HeatPump" )
    then u:CndsrType
    else UNDEFINED
    endif endif 
  ANNUAL
    z:CndsrType
ENDRULE


// ---------- Section 5.7.6.3 - Hydronic/Steam Heating Coils -------------------
// ********** Heating Coil Capacity ********************************************
// ---------- Section 5.7.6.4 - Furnace ----------------------------------------
// ********** Furnace Capacity *************************************************
// ---------- Section 5.7.6.5 - Electric Heat Pump -----------------------------
// ********** Electric Heat Pump Heating Capacity ******************************
RULE CoilHtg:SizingRat
  DESCRIPTION
    "Multiplier used to adjust the the gross heating coil capacity."
  REFERENCE 
    NACM Section 5.7.5.1
  INPUTCLASS 
    Prescribed   
  MINIMUM 
    0.5
  COMMONMINIMUM
    1.0
  COMMONMAXIMUM
    1.5
  SIZING 
    1.0
  ANNUAL
    if( z:IsBaseHVAC = 1 )
    then Proj:HtgSizingRat
    else 1.0
    endif
ENDRULE

// Rules for proposed case
// =========================== AirSystem =======================================
RULE AirSys:AirSeg:CoilHtg:CapTotNetRtd
  DESCRIPTION
    "The net capacity of a heating coil in unitary system at AHRI conditions."
  HELP
    "The net capacity is the total heating capacity of the coil after adjusting  
     for fan heat at rated conditions. This is a required input for Type =
     'HeatPump' coils."
  INPUTCLASS
    CondRequired
  MINIMUM 
    1
  COMMONMINIMUM
    300
  COMMONMAXIMUM
    1200000
  UNITS 
    Btu/h
  REPORTPRECISION
    -3
  DEFAULT
    if( Proj:AutoHardSize = 1 )
    then // For PROPOSED AutoHardSizing only
      if( Proj:ModelName != "" )
      then UNCHANGED // TableSizing is used, see Project-TableSizing rules
      else
      if( Type != "HeatPump" .AND. LocalStatus( CapTotGrossRtd ) > 4 ) 
      then // CapTotGrossRtd is user-defined, set net to same as gross for non-HP coils
        CapTotGrossRtd
      else 
      if( Type = "HeatPump" .AND. IfValidAnd( SysClgCap > 0 ) )
      then // Default for HP coils
        1.05 * SysClgCap ; 105% of cooling capacity
      else 
      if( IfValidAnd( AirSys:SupFanCap > 0 ) .AND.
          IfValidAnd( AirSys:HtgDsgnSupAirTemp > AirSys:ClgDsgnSupAirTemp ) )
      then // Default for other coils where HtgDsgn is > ClgDsgn
        1.0944 * AirSys:SupFanCap
        * Min( AirSys:HtgDsgnSupAirTemp - AirSys:ClgDsgnSupAirTemp , 20 )
      else // Default for other coils
      if( IfValidAnd( AirSys:SupFanCap > 0 ) )
      then // Default for other coils
        1.0944 * AirSys:SupFanCap * 30
      else UNDEFINED
      endif endif endif endif endif
    else if( Type != "HeatPump" )
    then CapTotGrossRtd // Net is same as gross for non-HP coils
    else UNDEFINED
    endif endif
  CHECKCODE
    if( Type = "HeatPump" )
    then 
      if( LocalStatus( CapTotNetRtd ) = 0 )
      then // User is required to enter net capacity if is a HeatPump/Furnace
        PostError("Net capacity for coil '%s' a required input for 
                   HeatPump coils", Name)
      else
      if( CapTotNetRtd > 1 .AND. 
          ( IfValidAnd( SysClgCap < CapTotNetRtd * 0.80 ) .OR.
            IfValidAnd( SysClgCap > CapTotNetRtd * 1.20 ) )
        )
      then // User is required to enter net capacity if is a HeatPump/Furnace
        PostWarning("The net heating capacity for HeatPump coil '%s' differs
                     from the system's net cooling capacity by more than 15%.",
                     Name)
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif
  SIZING
    if( BaseSysNum > 0 ) 
    then UNDEFINED
    else if( Type = "HeatPump" )
    then u:CapTotNetRtd
    else u:CapTotGrossRtd
    endif endif
ENDRULE

// =========================== ZoneSystem ======================================
RULE ZnSys:CoilHtg:CapTotNetRtd
  DEFAULT
    if( Proj:AutoHardSize = 1 )
    then // For PROPOSED AutoHardSizing only
      if( Proj:ModelName != "" )
      then UNCHANGED // TableSizing is used, see Project-TableSizing rules
      else
      if( Type != "HeatPump" .AND. LocalStatus( CapTotGrossRtd ) > 4 ) 
      then // CapTotGrossRtd is user-defined, set net to same as gross for non-HP coils
        CapTotGrossRtd
      else
      if( Type = "HeatPump" .AND. SysClgCap > 0 )
      then // Default for HP coils
        1.05 * SysClgCap ; 105% of cooling capacity
      else
      if( IfValidAnd( ZnSys:SupFanCap > 0 ) .AND.
          IfValidAnd( ZnSys:HtgDsgnSupAirTemp > ZnSys:ClgDsgnSupAirTemp ) )
      then // Default for other coils where HtgDsgn is > ClgDsgn
        1.0944 * ZnSys:SupFanCap
        * Min( ZnSys:HtgDsgnSupAirTemp - ZnSys:ClgDsgnSupAirTemp , 20 )
      else // Default for other coils
      if( IfValidAnd( ZnSys:SupFanCap > 0 ) )
      then // Default for other coils
        1.0944 * ZnSys:SupFanCap * 30
      else // Default for baseboards
        ZnSys:TotCondFlrArea * 30 ; 30 Btu/hr-ft2 
      endif endif endif endif endif
    else if( Type != "HeatPump" )
    then // Net is same as gross for non-HP coils
      CapTotGrossRtd 
    else UNDEFINED
    endif endif
  CHECKCODE
    if( Type = "HeatPump" )
    then 
      if( LocalStatus( CapTotNetRtd ) = 0 )
      then // User is required to enter net capacity if is a HeatPump/Furnace
        PostError("Net capacity for coil '%s' a required input for 
                   HeatPump coils", Name)
      else
      if( CapTotNetRtd > 1 .AND. 
          ( IfValidAnd( SysClgCap < CapTotNetRtd * 0.80 ) .OR.
            IfValidAnd( SysClgCap > CapTotNetRtd * 1.20 ) )
        )
      then // User is required to enter net capacity if is a HeatPump/Furnace
        PostWarning("The net heating capacity for HeatPump coil '%s' differs
                     from the system's net cooling capacity by more than 20%.",
                     Name)
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif
  SIZING
    if( BaseSysNum > 0 ) 
    then UNDEFINED
    else if( Type = "HeatPump" )
    then u:CapTotNetRtd
    else u:CapTotGrossRtd
    endif endif
ENDRULE

// -----------------------------------------------------------------------------
// Transfer FanHtRtd from System to CoilHtg
RULE NEW CoilHtg:FanHtRtd
  DATATYPE
    Float
  LONGFORM
    FanHeatRated
  DESCRIPTION
    "See CoilClg:FanHtRtd"
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/h
  DEFAULT   
    if( Type != "HeatPump" )
    then 0 // Fan heat not accounted for with non-HP coils
    else 
    if( ParentComp = "AirSys" )
    then // This is an AirSys
      // Use default assumption for fan heat at rated conditions
      // This value is calculated at CoilClg and transfered to AirSys
      ValidOr( AirSys:FanHtRtd, 0 )
    else if( ParentComp = "ZnSys" )
    then // This is a ZnSys
      // Use default assumption for fan heat at rated conditions
      // This value is calculated at CoilClg and transfered to ZnSys
      ValidOr( ZnSys:FanHtRtd, 0 )
    else 0
    endif endif endif
  SIZING
    if( BaseSysNum > 0 )
    then 0
    else if( Type != "HeatPump" )
    then 0 // Fan heat not accounted for with non-HP coils
    else if( ParentComp = "AirSys" )
    then ValidOr( AirSys:FanHtRtd, 0 )
    else if( ParentComp = "ZnSys" )
    then ValidOr( ZnSys:FanHtRtd, 0 )
    else 0
    endif endif endif endif
ENDRULE

// Transfer PumpPwrRtd from System to CoilHtg
RULE NEW CoilHtg:PumpPwrRtd
  DATATYPE
    Float
  LONGFORM
    PumpPwrRated
  DESCRIPTION
    "See CoilClg:PumpPwrRtd"
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/h
  DEFAULT   
    if( Type != "HeatPump" .OR. CndsrType = "Air")
    then 0 // Pump heat not accounted for with non-HP/air-source coils
    else if( ParentComp = "AirSys" )
    then ValidOr( AirSys:PumpPwrRtd, 0 )
    else if( ParentComp = "ZnSys" )
    then ValidOr( ZnSys:PumpPwrRtd, 0 )
    else 0
    endif endif endif
  SIZING
    if( BaseSysNum > 0 )
    then 0
    else if( Type != "HeatPump" .OR. CndsrType = "Air" )
    then 0 // Pump heat not accounted for with non-HP/air-source coils
    else if( ParentComp = "AirSys" )
    then ValidOr( AirSys:PumpPwrRtd, 0 )
    else if( ParentComp = "ZnSys" )
    then ValidOr( ZnSys:PumpPwrRtd, 0 )
    else 0
    endif endif endif endif
ENDRULE



// ********** Gross Capacty ****************************************************
RULE CoilHtg:CapTotGrossRtd
  DESCRIPTION
    "The gross heating capacity of the coil at AHRI conditions."
  HELP
    "The gross capacity is the total heating capacity without adjustments for 
     fan heat. This is a required input for Type = 'HotWater' and 'Furnace' coils."
  REFERENCE 
    NACM Section 5.7.6.3
    NACM Section 5.7.6.4
    NACM Section 5.7.6.5
  INPUTCLASS 
    CondRequired
  MINIMUM 
    1
  COMMONMINIMUM
    300
  COMMONMAXIMUM
    1200000
  UNITS 
    Btu/h
  REPORTPRECISION
    -3
  DEFAULT
    if( IfValidAnd( CapTotNetRtd > 0 ) )
    then 
      if( Type = "HeatPump" )
      then CapTotNetRtd - FanHtRtd
      else if( Proj:AutoHardSize = 1 )
      then CapTotNetRtd
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  CHECKSIM
    if( (Type = "Resistance" .OR.  
         Type = "Furnace" .OR.
         Type = "HotWater" .OR.
         Type = "Steam" ) .AND. 
        LocalStatus( CapTotGrossRtd ) = 0 )
    then // User is required to enter gross capacity
      PostError("Gross capacity for coil '%s' is a required input for 
                 Resistance, Furnace, HotWater and Steam coils", Name)
    else UNCHANGED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else if( Type != "HeatPump" .AND. 
             IfValidAnd( CapTotGrossRtd > 0 ) )
    then // User has defined gross capacity
      CapTotGrossRtd
    else if( IfValidAnd( CapTotNetRtd > 0 ) )
        then // Calculate gross by subtracting AHRI fan heat to net capacity
      CapTotNetRtd - FanHtRtd
    else 0
    endif endif endif
  ANNUAL
    if( BaseSysNum > 0 )
    then
      if( IfValidAnd( z:CapTotGrossRtdSim >= 0) .AND. 
          SysCnt > 0 ) 
      then // Fan heat not included in adjustment of heating coils.
        if( ParentComp = "ZnSys" )
        then // Is a ZoneSystem
          z:CapTotGrossRtdSim / SysCnt * SizingRat
        else if( ParentComp = "AirSys" ) 
        then // Is an AirSystem
          z:CapTotGrossRtdSim / SysCnt * SizingRat   
        else 0
        endif endif
      else 0
      endif
    else z:CapTotGrossRtd
    endif
ENDRULE

// ------------ ANNUAL rule for FanHtRtd and CapTotNetRtd ----------------------
RULE NEW CoilHtg:FanHtRtd
  ANNUAL
    if( Type != "HeatPump" )
    then 0 // Fan heat not accounted for with non-HP coils
    else if( ParentComp = "AirSys" ) 
    then ValidOr( Parent2( FanHtRtd ), 0 )
    else if( ParentComp = "ZnSys" )
    then ValidOr( Parent( FanHtRtd ), 0 )
    else 0
    endif endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:CapTotNetRtd
  ANNUAL 
    if( BaseSysNum > 0 )
    then
      // Make adjustment for fan heat for baseline systems with HP coils
      // Q_gross = Q_net - Q_fan 
      // Q_net = Q_gross + Q_fan
      CapTotGrossRtd + FanHtRtd
    else z:CapTotNetRtd
    endif
ENDRULE


// ********** Number of Heating Stages *****************************************
RULE CoilHtg:NumHtgStages
  DESCRIPTION
    "The number of heating stages for a furnace or heat pump heating coil."
  HELP
    "This applies to heating coils with more than one stage of heating.  This 
     system is typically a packaged unit with multiple heat pump compressors or
     a furnate with multiple firing rates." 
  REFERENCE 
    NACM Section 5.7.5.2
  INPUTCLASS 
    NotInput
  MINIMUM   
    1
  MAXIMUM 
    1 // Only 1-stage furnaces/heatpumps are currently supported
  REPORTPRECISION
    0
  DEFAULT
    if( ( Type = "HeatPump" .OR. Type = "Furnace" ) .AND. 
        ParentComp = "AirSys" )
    then 1 // Only 1-stage furnaces/heatpumps are currently supported
    else UNDEFINED
    endif
  SIZING
    if( ( Type = "HeatPump" .OR. Type = "Furnace" ) .AND. 
        ParentComp = "AirSys" )
    then // Only 1-stage furnaces/heatpumps are currently supported
      1 
    else UNDEFINED
    endif
  ANNUAL
    z:NumHtgStages
ENDRULE


// ********** Heating Capacity by Stage ****************************************
RULE CoilHtg:CapTotRtdStageFrac[1]
  DESCRIPTION
    "The fraction of total heating capacity (at ARI rated conditions) provided
     for the first [1] stage of heating."
  HELP
    "The property is expressed as an array, with each entry a fraction of the 
     rated hating capacity for the unit (CoilClg:CapacityTotalRated). 
     For example, if the first stage of cooling has rated cooling capacity of 4 
     tons (48,000 Btu/h), and the rated total cooling capacity is 8 tons 
     (96,000 Btu/h), this array property value is '0.5'." 
  REFERENCE 
     NACM Section 5.7.5.2
  INPUTCLASS 
    NotInput
  MINIMUM   
    0
  COMMONMINIMUM
    0.2
  MAXIMUM 
    1.0
  REPORTPRECISION
    2
  DEFAULT
    if( ( Type = "HeatPump" .OR. Type = "Furnace" ) .AND. 
        ParentComp = "AirSys" .AND.
        NumHtgStages = 2 )
    then 0.5   
    else UNDEFINED
    endif
  SIZING
    if( ( Type = "HeatPump" .OR. Type = "Furnace" ) .AND. 
        ParentComp = "AirSys" .AND.
        NumHtgStages = 2 )
    then
      if( BaseSysNum > 0 )
      then UNDEFINED // Sizing performed with 1-stage heating/cooling
      else u:CapTotRtdStageFrac[1]
      endif
    else UNDEFINED
    endif     
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED // Baseline systems are 1-stage heating/cooling
    else z:CapTotRtdStageFrac[1]
    endif
ENDRULE
RULE CoilHtg:CapTotRtdStageFrac[2]
  DESCRIPTION
    "The fraction of total heating capacity (at ARI rated conditions) provided
     for the second [2] stage of heating."
  HELP
    "The property is expressed as an array, with each entry a fraction of the 
     rated total heating capacity for the unit (CoilHtg:CapacityTotalRated). 
     For example, for a system with two heating stages, this array property 
     value is '1.0'." 
  REFERENCE 
    NACM Section 5.7.5.2
  INPUTCLASS 
    NotInput
  MINIMUM   
    0
  COMMONMINIMUM
    0.2
  MAXIMUM 
    1.0
  REPORTPRECISION
    2
  DEFAULT
    if( ( Type = "HeatPump" .OR. Type = "Furnace" ) .AND. 
        ParentComp = "AirSys" .AND.
        NumHtgStages = 2 )
    then 1.0 
    else UNDEFINED
    endif
  SIZING
    if( ( Type = "HeatPump" .OR. Type = "Furnace" ) .AND. 
        ParentComp = "AirSys" .AND.
        NumHtgStages = 2 )
    then
      if( BaseSysNum > 0 )
      then UNDEFINED // Sizing performed with 1-stage heating/cooling
      else u:CapTotRtdStageFrac[2]
      endif
    else UNDEFINED
    endif     
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED // Baseline systems are 1-stage heating/cooling
    else z:CapTotRtdStageFrac[2]
    endif
ENDRULE


// ********** Electric Heat Pump Heating Capacity Capacity Adjustment Curves ***
RULE CoilHtg:HtPumpCap_fTempCrvRef
  DESCRIPTION
    "A curve that describes the adjustment of a heat pump heating coil capacity 
     as a function of temperature."
  REFERENCE 
    NACM Section 5.7.6.5
  INPUTCLASS 
    Prescribed
  SIZING
    if( Type = "HeatPump" )
      then
      if( CndsrType = "WaterSource" )
      then  UNDEFINED
      // Currently the translator is using hard-coded coefficients in the WLHP objects
      //RuleLibrary(QuadplLin, "CoilHtgWSHPQRatio_fTdbRatioTwtRatioCFMRatioGPMRatioSI")
      else RuleLibrary(CrvCubic, "CoilHtgHPQRatio_fToadbSI") 
      endif
    else UNDEFINED // Other E+ heating coil objects do not use capacity curves
    endif
  ANNUAL
   z:HtPumpCap_fTempCrvRef
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:HtPumpCap_fFlowCrvRef
  DESCRIPTION
    "Normalized curve that varies heating capacity as a function of airflow. 
     Used for EnergyPlus heat pump heating coil model only."
  INPUTCLASS 
    Prescribed
  SIZING
    if( Type = "HeatPump" .AND. CndsrType = "Air" )
    then RuleLibrary(CrvQuad, "CoilHtgHPQRatio_fCFMRatio") 
    else UNDEFINED
    endif 
  ANNUAL
   z:HtPumpCap_fFlowCrvRef
ENDRULE


// -----------------------------------------------------------------------------
// Design fluid flow rate, used for sizing E+ hot water coils
// Also is a reference property for WSHP coils 
RULE CoilHtg:FluidFlowRtDsgn
  DESCRIPTION
    "The design water volume flow rate (gpm) through the coil." 
  HELP
    "Required input for Type = 'HotWater' coils and 'HeatPump' coils with 
     CondenserType = 'Water'."
  UNITS
    gpm
  INPUTCLASS 
    CondRequired
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "HotWater" .AND. 
        Proj:AutoHardSize = 1)
    then // For PROPOSED AutoHardSizing only
      if( Proj:ModelName != "" )
      then UNCHANGED // TableSizing is used, see Project-TableSizing rules
      else
      if( LocalStatus( CapTotGrossRtd ) > 0 .AND. 
          IfValidAnd( FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT > 0 ) )
      then CapTotGrossRtd / ( 500.19 * FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT )
      else UNDEFINED
      endif endif
    else if( Type = "HeatPump" .AND. 
             CndsrType = "WaterSource" .AND.
             ( ParentComp = "AirSys" .OR. ParentComp = "ZnSys" ) )
    then
      if( ParentCompAssigned( CoilClgRef ) ) 
      then CoilClgRef:FluidFlowRtDsgn // Set to flow = to value at cooling coil
      else 0
      endif    
    else UNDEFINED
    endif endif
  CHECKSIM
    if( Type = "HotWater" .AND. 
        IfValidAnd( FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT > 0 ) = 0 )
    then
      PostError("The hot water loop serving coil '%s' has an undefined or 0F
                 DesignSupplyWaterDeltaT.", Name)
    else UNCHANGED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else
    if( Type = "HotWater" .OR.
        ( Type = "HeatPump" .AND. CndsrType = "WaterSource" ) )
    then
      if( IfValidAnd( FluidFlowRtDsgn > 0 ) )
      then u:FluidFlowRtDsgn 
      else if( LocalStatus( CapTotGrossRtd ) > 0 .AND. 
               IfValidAnd( FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT > 0 ) )
      then CapTotGrossRtd /( 500.19 * FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT)
      else 0
      endif endif
    else UNDEFINED
    endif endif
  ANNUAL
    if( BaseSysNum > 0 )
    then
      if( Type = "HotWater" .AND. 
          LocalStatus( CapTotGrossRtd ) > 0 .AND. 
          IfValidAnd( FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT > 0 ) )
      then CapTotGrossRtd /( 500.19 * FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT)
      else 0
      endif
    else z:FluidFlowRtDsgn
    endif
ENDRULE   


// Design load on coil
;RULE CoilHtg:FluidLdDsgn
;  DESCRIPTION
;    "The load on fluid side of coil at design conditions."
;  HELP
;    "Used to describe capacity for E+ sizing calculations."
;  UNITS
;    Btu/h
;  INPUTCLASS 
;    NotInput
;  SIZING
;    UNDEFINED
;  ANNUAL
;    if( Type = "HotWater" )
;    // Calculate coil load based on design water flow rate
;    then
;      if( IfValidAnd( FluidFlowRtDsgn > 0 ) .AND.
;          IfValidAnd( FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT > 0 ) ) 
;      then 
;        FluidFlowRtDsgn / SizingRat * 500.19 *
;        FluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT
;      else 0
;      endif
;    else UNDEFINED
;    endif 
;ENDRULE 


// ********** Coil Head ********************************************************
RULE CoilHtg:CoilHdDsgn
  DESCRIPTION
    "The pressure drop though the coil at design conditions." 
  HELP
    "For coils with water-source condensers, the pressure drop is used to calculated
     the simulated EIR by removing the power consumed by pumps from the rated COP.
     In this case, the head is defaulted to be the same as the cooling coil.

     For all other water coils, this property is does not affect the simulation
     of the coil or fluid system.  The pressure drop of the fluid system is
     defined at the pump. If defined, it is used as a reference or reporting only."
  INPUTCLASS 
    Default
  COMMONMINIMUM
    1.0
  COMMONMAXIMUM
    15
  UNITS 
    ft H2O
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "HotWater" )
    then 5.0
    else if( Type = "HeatPump" .AND. 
             CndsrType = "WaterSource" .AND.
             ( ParentComp = "AirSys" .OR. ParentComp = "ZnSys" ) )
    then
      if( ParentCompAssigned( CoilClgRef ) ) 
      then CoilClgRef:CoilHdDsgn // Set to head = to value at cooling coil
      else 5.0
      endif
    else UNDEFINED
    endif endif
  CHECKSIM
    if( Type = "HeatPump" .AND. 
        CndsrType = "WaterSource" .AND.
        ( ParentComp = "AirSys" .OR. ParentComp = "ZnSys" ) )
    then
      if( ParentCompAssigned( CoilClgRef ) )
      then 
        if( IfValidAnd( CoilHdDsgn != CoilClgRef:CoilHdDsgn ) )
        then
          PostWarning("The pressure drop though the condenser of heating coil '%s'
                     is not the same as the pressure drop through the corresponding
                     cooling coil.", Name)
        else UNCHANGED
        endif
      else UNCHANGED
      endif
    else UNCHANGED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else
    if( Type = "HotWater" .OR.
        ( Type = "HeatPump" .AND. CndsrType = "WaterSource" ) )
    then u:CoilHdDsgn
    else UNDEFINED
    endif endif
  ANNUAL
    z:CoilHdDsgn
ENDRULE


// ********** Simulated Capacities *********************************************
RULE CoilHtg:CapTotGrossRtdSim
  DESCRIPTION
    "The total heating capacity at design conditions, for simulation" 
  INPUTCLASS 
    NotInput
  MINIMUM
    0
  UNITS 
    Btu/h
  REPORTPRECISION
    0
  DEFAULT
    if( IfValidAnd( CapTotGrossRtd > 0 ) )
    then CapTotGrossRtd * SysCnt
    else 0
    endif
  SIZING
    // Source for Autosize data:
    // E+ Report: Equipment Summary -> Heating Coils -> Nominal Total Capacity
    // This value reflects multipliers of zone loads
  ANNUAL
    if( IfValidAnd( CapTotGrossRtd > 0 ) )
    then CapTotGrossRtd * SysCnt
    else 1
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW CoilHtg:CapTotNetRtdSim
  DATATYPE
    Float
  LONGFORM
    CapacityTotalNetRatedSimulated
  DESCRIPTION
    "The total (net) cooling capacity at design conditions" 
  INPUTCLASS 
    NotInput
  MINIMUM 
    0
  UNITS 
    Btu/h
  DEFAULT
    if( IfValidAnd( CapTotNetRtd > 0 ) )
    then CapTotNetRtd * SysCnt
    else 0
    endif
  SIZING
    UNDEFINED
  ANNUAL
    if( IfValidAnd( CapTotNetRtd > 0 ) )
    then CapTotNetRtd * SysCnt
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilHtg:FluidFlowRtDsgnSim
  DESCRIPTION
    "The design water volume flow rate (gpm) through the coil, for simulation" 
  INPUTCLASS 
    NotInput
  MINIMUM 
    0
  UNITS 
    gpm
  REPORTPRECISION
    1
  DEFAULT
    if( IfValidAnd( FluidFlowRtDsgn > 0 ) )
    then FluidFlowRtDsgn * SysCnt
    else UNDEFINED
    endif
  SIZING
    // Source for Autosize data:
    // E+ Report: 
    // This value reflects multipliers of zone loads
  ANNUAL
    if( IfValidAnd( FluidFlowRtDsgn > 0 ) )
    then FluidFlowRtDsgn * SysCnt
    else UNDEFINED
    endif
ENDRULE


// ********** Bypass Minimum Efficincy Check ***********************************
RULE CoilHtg:BypassMinEffCheck
  DESCRIPTION
    "A boolean (0/1) flag to indicate whether the minimum efficiency checks for
     the HVAC system are bypassed."
  HELP
    "Selecting this option (value = 1 ) triggers an Exceptional Condition to 
     be shown on the compliance report, and requires code reviewers to manually
     check that the equipment meets the minimum efficiency requirements for the
     applicable equipment category in the Standards. This option should only be
     selected in the following circustances:

     1) The equipment category of the system in the proposed design is not
        supported in CBECC-Com by the combinations of Type/SubType

     2) The equipment was manufactured prior to a change in equipment efficiency
        Standards and does not meet current efficiency requirements, but it is
        still legal to install."
  INPUTCLASS
    Default  
  DEFAULT
    0
  SIZING_PROPOSED
    if( Type = "HotWater" .OR. Type = "Steam" .OR. Type = "Resistance" )
    then 0
    else UNCHANGED
    endif
  ANNUAL_PROPOSED
    z:BypassMinEffCheck
ENDRULE


// ********** Cooling Capacity for Add/Alt rules *******************************
RULE NEW CoilHtg:CapAddAlt
  DATATYPE
    Float
  LONGFORM
    CapacityAdditionAlteration
  DESCRIPTION
    "Gross capacity of heating coil for determining the baseline system for 
     Additions/Alterations. Does not include Type = HotWater or Steam coil capacity,
     but does includes 'SystemCount' multipliers."
  INPUTCLASS
    NotInput  
  DEFAULT
    if( Type != "HotWater" .AND. // Add Steam when these are supported
        IfValidAnd( CapTotGrossRtdSim >= 0 ) )
    then CapTotGrossRtdSim
    else 0
    endif
  SIZING 
    UNDEFINED
  ANNUAL
    if( Type != "HotWater" .AND. // Add Steam when these are supported
        IfValidAnd( CapTotGrossRtdSim >= 0 ) )
    then CapTotGrossRtdSim
    else 0
    endif
ENDRULE



// Properties primarily for S901G ruleset
// ---------- Heating Fuel Source ------------
// For coils
RULE NEW CoilHtg:HtgFuelSrc
  DATATYPE
    Integer
  LONGFORM
    HeatingFuelSource
  DESCRIPTION
    "A flag that indcates the heating fuel source of the CoilHeating object."
  HELP
    "The flag values are:
     0 = Electic or no heating components
     1 = Fossil fuel, fossil/electric, or other"
  INPUTCLASS
    NotInput
  DEFAULT
    if( Type = "HotWater" .AND. LocalCompAssigned( FluidSegInRef ) )
    then FluidSegInRef:ParentFluidSysRef:HtgFuelSrc
    else
    if( Type = "HeatPump" .AND. LocalCompAssigned( FluidSegInRef ) )
    then FluidSegInRef:ParentFluidSysRef:HtgFuelSrc
    else
    if( Type = "Furnace" .AND. FuelSrc != "Electric" ) 
    then 1
    else 0
    endif endif endif
ENDRULE
// For AirSystem
RULE NEW AirSys:HtgFuelSrc
  DATATYPE
    Integer
  LONGFORM
    HeatingFuelSource
  DESCRIPTION
    "A flag that indcates the heating fuel source of the CoilHeating object."
  HELP
    "The flag values are:
     0 = Electic or no heating components
     1 = Fossil fuel, fossil/electric, or other"
  INPUTCLASS
    NotInput
  DEFAULT
    if( SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:HtgFuelSrc = 1 ) > 0 )
    then 1
    else 0
    endif
ENDRULE
// For ZoneSystem
RULE NEW ZnSys:HtgFuelSrc
  DATATYPE
    Integer
  LONGFORM
    HeatingFuelSource
  DESCRIPTION
    "A flag that indcates the heating fuel source of the CoilHeating object."
  HELP
    "The flag values are:
     0 = Electic or no heating components
     1 = Fossil fuel, fossil/electric, or other"
  INPUTCLASS
    NotInput
  DEFAULT
    if( SumChildrenIf( CoilHtg:CapTotNetRtd, CoilHtg:HtgFuelSrc = 1 ) > 0 )
    then 1
    else 0
    endif
ENDRULE