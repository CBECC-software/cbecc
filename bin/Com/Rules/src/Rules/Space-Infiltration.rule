// Thermal Zone - Infiltration
//
// -----------------------------------------------------------------------------
//  Copyright (c) 2012, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
//
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -----------------------------------------------------------------------------
//


// According to 5.3.5 ACM Draft October 11 2012, items:
//     Infiltration Method
//     Infiltration Data
//        Idesign (design rate - cfm/ft² of exterior wall area)
//        A coefficient (overall)
//        B coefficient (temperature)
//        C coefficient (windspeed)
//        D coefficient (windspeed squared)
//     Infiltration schedule name
//
// The main objects in the SDD are:
//     Proj:Bldg:Story:Space:InfiltrationMethod
//     Proj:Bldg:Story:Space:DesignInfiltrationRate
//     Proj:Bldg:Story:Space:InfiltrationModelCoefficientA
//     Proj:Bldg:Story:Space:InfiltrationModelCoefficientB
//     Proj:Bldg:Story:Space:InfiltrationModelCoefficientC
//     Proj:Bldg:Story:Space:InfiltrationModelCoefficientD
//     Proj:Bldg:Story:Space:InfiltrationScheduleReference
//     Proj:Bldg:Story:Space:InfiltrationMethod2
//     Proj:Bldg:Story:Space:DesignInfiltrationRate2
//     Proj:Bldg:Story:Space:InfiltrationModel2CoefficientA
//     Proj:Bldg:Story:Space:InfiltrationModel2CoefficientB
//     Proj:Bldg:Story:Space:InfiltrationModel2CoefficientC
//     Proj:Bldg:Story:Space:InfiltrationModel2CoefficientD
//     Proj:Bldg:Story:Space:InfiltrationSchedule2Reference
//

// -------------- Infiltration Method [1] --------------------------------------
RULE Spc:InfMthd[1]
  DESCRIPTION
    "The method used to calculate infiltration rate, specified by
    the ACM to be leakage through the building walls per unit
    area (cfm/ft²)."
  HELP
    "Methods available to calculate infiltration include effective
    leakage area which is generally applicable for small residential
    scale buildings.  The required component leakage method requires
    the user to specify the average leakage through the building
    wall per unit area (cfm/ft²).  Component leakage rates can also
    be applied to the total building envelope area.  Specification
    of a maximum rate, usually as air changes per hour, is also
    used.  For all methods, the rate will be modified by a schedule. "
  REFERENCE
    ACM-5.3.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
;  OPTION     Specified in BEMEnums
;    FlowSpace
;    FlowArea
;    FlowExteriorArea
;    FlowExteriorWallArea
;    AirChangesPerHour
  DEFAULT : T24N
    "FlowExteriorWallArea"
  DEFAULT : S901G ECBC
    if( LocalCompAssigned(SpcFuncDefaultsRef) )
    then SpcFuncDefaultsRef:InfMthd[1]
    else "FlowExteriorWallArea"
    endif
  CHECKCODE
    if (LocalStatus( InfMthd[1] ) < 4)
    then UNCHANGED
    else
    if( Proj:AnalysisType != "Research" .AND. 
        InfMthd[1] != "FlowExteriorWallArea" )
    then
      PostWarning("Space '%s' has InfiltrationMethod = '%s'. The infiltration
                   method and rate is prescribed as 'FlowExteriorWallArea' for 
                   compliance analysis; user inputs will be ignored.",
                   Name, InfMthd )
    else UNCHANGED
    endif endif
  CHECKSIM
    if( InfMthd[1] = "- specify -" ) 
    then
      PostWarning("The InfiltrationMethod for space '%s' is not specified. It will be 
                   defaulted to 'FlowExteriorWallArea'.", Name)
    else UNCHANGED
    endif  
  SIZING : T24N
    if( Proj:AnalysisType = "Research" )
    then
      if( InfMthd[1] = "- specify -" )
      then "FlowExteriorWallArea"
      else u:InfMthd[1]
      endif
    else "FlowExteriorWallArea"
    endif
  SIZING : S901G ECBC
    if( InfMthd = "- specify -" )
    then "FlowExteriorWallArea"
    else u:InfMthd[1]
    endif
  ANNUAL
    z:InfMthd[1]
ENDRULE

// -------------- Design Infiltration Rate -------------------------------------
RULE Spc:DsgnInfRt[1]
  DESCRIPTION
    "The infiltration rate for the space. A default design value of 0.0448 cfm/SF
     @ 10 mph windspeed must be used unless specific air sealing methods beyond
     requirements of the standard are applied and documented.  "
  HELP
    "The unit for this property depend on the Spc:InfMthd. 
    
    InfMthd = 'FlowSpace', unit is cfm.
    InfMthd = 'AirChangesPerHour', unit is ACH
    All others, unit is cfm/SF"
  REFERENCE
    ACM-5.3.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  MINIMUM
    0
  MAXIMUM
    1.0
  COMMONMAXIMUM
    0.3
  DEFAULT : T24N
    0.0448
  DEFAULT : S901G ECBC
    if( LocalCompAssigned(SpcFuncDefaultsRef) )
    then SpcFuncDefaultsRef:DsgnInfRt[1]
    else( 0.112 * 0.4 * (Proj:TotExtSurfArea / Proj:AboveGrdExtWallArea) )
    endif
  SIZING : S901G ECBC
    if( AnalysisType = "Research" ) then
      u:DsgnInfRt[1]
    else( 0.112 * 0.4 * (Proj:TotExtSurfArea / Proj:AboveGrdExtWallArea) )
    endif
  SIZING
    if( AnalysisType = "Research" ) then
      u:DsgnInfRt[1]
    else 0.0448
    endif
  ANNUAL
    z:DsgnInfRt[1]
ENDRULE



// -------------- Infiltration Schedule ----------------------------------------
RULE Spc:InfSchRef[1]
  DESCRIPTION
    "Reference to a unique identification name for a schedule in a
    library of occupant type"
  HELP
    ""
  REFERENCE
    ACM-5.3.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  DEFAULT : S901G ECBC
    if( LocalCompAssigned(SpcFuncDefaultsRef) ) then
      SpcFuncDefaultsRef:InfSchRef[1]
    else UNCHANGED
    endif
  SIZING
    if( AnalysisType = "Research" ) then
      u:InfSchRef[1]
    else if( u:CondgType != "DirectlyConditioned" .OR.
             LocalStatus(u:CondgType) < 1 .OR.
	     SpcFunc = "Unoccupied-Include in Gross Floor Area" .OR.
	     SpcFunc = "Unoccupied-Exclude from Gross Floor Area" .OR.
	     LocalStatus( SpcFunc ) < 2 ) then
      RuleLibrary( Schedule, "On" )
    else
      RuleLibrary(Schedule, (SpaceFunctionGroups:InfSchRef("FuncGroup",FuncSchGrp)))
    endif endif
  ANNUAL
    z:InfSchRef[1]
ENDRULE


// -------------- Infiltration Equation Coefficients ---------------------------
RULE Spc:InfModelCoefA[1]
  DESCRIPTION
    "The constant coefficient in the infiltration rate calculation
    equation, specified by the ACM to be set to 0.  "
  HELP
    "Infiltration for a time step is found from:
    Infiltration = Idesign x Schedule x (A + B x abs(tzone-toutdoor) +
                   C x windspd + D x windspd^2) "
  REFERENCE
    ACM-5.3.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  MINIMUM
    0
  COMMONMAXIMUM
    1.0
  DEFAULT : T24N
    0
  DEFAULT : S901G ECBC
    if( LocalCompAssigned(SpcFuncDefaultsRef) ) then
      SpcFuncDefaultsRef:InfModelCoefA[1]
    else 0
    endif
  SIZING
    if( AnalysisType = "Research" ) then
      u:InfModelCoefA[1]
    else 0
    endif
  ANNUAL
    z:InfModelCoefA[1]
ENDRULE

// -----------------------------------------------------------------------------
RULE Spc:InfModelCoefB[1]
  DESCRIPTION
    "The temperature coefficient in the infiltration rate calculation
    equation, specified by the ACM to be set to 0.  "
  HELP
    "Infiltration for a time step is found from:
    Infiltration = Idesign x Schedule x (A + B x abs(tzone-toutdoor) +
                   C x windspd + D x windspd^2) "
  REFERENCE
    ACM-5.3.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  MINIMUM
    0
  COMMONMAXIMUM
    0.1
  UNITS
    1/°F
  DEFAULT : T24N
    0
  DEFAULT : S901G ECBC
    if( LocalCompAssigned(SpcFuncDefaultsRef) ) then
      SpcFuncDefaultsRef:InfModelCoefB[1]
    else 0
    endif
  SIZING
    if( AnalysisType = "Research" ) then
      u:InfModelCoefB[1]
    else 0
    endif
  ANNUAL
    z:InfModelCoefB[1]
ENDRULE

// -----------------------------------------------------------------------------
RULE Spc:InfModelCoefC[1]
  DESCRIPTION
    "The windspeed coefficient in the infiltration rate calculation
    equation, specified by the ACM to be set to 0.10016.  "
  HELP
    "Infiltration for a time step is found from:
    Infiltration = Idesign x Schedule x (A + B x abs(tzone-toutdoor) +
                   C x windspd + D x windspd^2) "
  REFERENCE
    ACM-5.3.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  MINIMUM
    0
  COMMONMAXIMUM
    1.0
  UNITS
    1/mph
  DEFAULT : T24N
    0.10016
  DEFAULT : S901G ECBC
    if( LocalCompAssigned(SpcFuncDefaultsRef) ) then
      SpcFuncDefaultsRef:InfModelCoefC[1]
    else 0.10016
    endif
  SIZING
    if( AnalysisType = "Research" ) then
      u:InfModelCoefC[1]
    else 0.10016
    endif
  ANNUAL
    z:InfModelCoefC[1]
ENDRULE

// -----------------------------------------------------------------------------
RULE Spc:InfModelCoefD[1]
  DESCRIPTION
    "The windspeed squared coefficient in the infiltration rate
    calculation equation, specified by the ACM to be set to 0.  "
  HELP
    "Infiltration for a time step is found from:
    Infiltration = Idesign x Schedule x (A + B x abs(tzone-toutdoor) +
                   C x windspd + D x windspd^2) "
  REFERENCE
    ACM-5.3.5
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  MINIMUM
    0
  COMMONMAXIMUM
    0.5
  UNITS
    1/mph2
  DEFAULT : T24N
    0
  DEFAULT : S901G ECBC
    if( LocalCompAssigned(SpcFuncDefaultsRef) ) then
      SpcFuncDefaultsRef:InfModelCoefD[1]
    else 0
    endif
  SIZING
    if( AnalysisType = "Research" ) then
      u:InfModelCoefD[1]
    else 0
    endif
  ANNUAL
    z:InfModelCoefD[1]
ENDRULE

// -------------- Infiltration Method [2] --------------------------------------
// Rules moved to ThermalZone-Exhaust for rule ordering purposes



