// HVAC Secondary Systems - Outside Air Control
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  

//  According ACM Draft v1.0 (Oct Workshop), addresses the following building descriptors:
//  Section 5.7.4.1 - Outside Air Controls
//      Maximum Outside Air Ratio
//      Design Outside Air Flow
//      Outdoor Air Control Method  
//  Section 5.7.4.2 - Economizer Controls
//      Economizer Control Type
//      Economizer Integration Level
//      Economizer High Temperature Lockout
//      Economizer Low Temperature Lockout
//      Economizer High Enthalpy Lockout

// -----------------------------------------------------------------------------
// Data model structure QC rules
RULE OACtrl:AirSegSupRef
  DESCRIPTION
    "Defines the supply air segment of the AirSystem."
  HELP
    "Currently, the only valid AirSegment:Type that can be assigned is 
     'Supply'."
  INPUTCLASS
    Required
  DEFAULT
    if( ParentCompAssigned( AirSys:SupAirSegRef ) )
    then AirSys:SupAirSegRef
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE OACtrl:AirSegRetRef
  DESCRIPTION
    "Defines the return/releif air segment of the AirSystem."
  HELP
    "Currently, the only valid AirSegment:Type that can be assigned is 
     'Return' and 'Relief'."
  INPUTCLASS
    Required
  DEFAULT
    if( ParentCompAssigned( AirSys:SupAirSegRef ) .AND.
        ParentCompAssigned( AirSys:RetAirSegRef ) )
    then 
      if(AirSys:RetAirSegRef = AirSys:SupAirSegRef )
      then UNDEFINED // No return AirSeg has been created
      else AirSys:RetAirSegRef
      endif
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
;RULE OACtrl:OAAirSegRef
;  DESCRIPTION
;    "Defines the outdoor air segment of the AirSystem."
;  HELP
;    "Currently, the only valid AirSegment:Type that can be assigned is 
;     'OutdoorAir'."
;  INPUTCLASS
;    Optional
;ENDRULE
;
;// -----------------------------------------------------------------------------
;RULE OACtrl:ExhAirSegRef
;  DESCRIPTION
;    "Defines the exhaust segment of the AirSystem."
;  HELP
;    "Currently, the only valid AirSegment:Type that can be assigned is 
;     'Relief' and 'Exhaust'."
;  INPUTCLASS
;    Optional
;ENDRULE

// -----------------------------------------------------------------------------
// Echo system count for reference in UI and capacity/efficiency adjustment rules
RULE OACtrl:SysCnt
  DESCRIPTION
    "Echo of AirSys:Count"
  HELP
    "The number of duplicate systems can only be >1 when all attributes of 
     the system are the same.  If Count is specified to be >1, all parameters
     (capacities, power, etc) should be specified for the single piece of equipment.
     The ruleset will apply multipliers for the final simulation."
  INPUTCLASS 
    NotInput   
  REPORTPRECISION
    0
  DEFAULT
   if( ParentComponentType() = "AirSys" )
     then AirSys:Cnt
   else 0
   endif
  SIZING
   if( ParentComponentType() = "AirSys" )
     then AirSys:Cnt
   else 0
   endif
  ANNUAL
    z:SysCnt   
ENDRULE


// ********** Status ***********************************************************
RULE OACtrl:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  INPUTCLASS 
    NotInput
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New' 
;   New
;   Existing 
  DEFAULT
// Status defined from top-down. If parent system is altered, child objects
// are defaulted to Existing
    if( Parent( IsNew ) ) then "New" else "Existing" endif
// SIZING and ANNUAL rules not used; status of any new objects created by rules 
// is defined by BEMEnums default
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW OACtrl:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" ) then 1 else 0 endif
  SIZING : T24N
    if( Status = "New" ) then 1 else 0 endif
  SIZING : S901G ECBC
    if( Status = "New" .AND. Parent( IsNew ) = 1 ) then 1 else 0 endif
  ANNUAL
    z:IsNew
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW OACtrl:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsNew ) then 0 else 1 endif
  SIZING
    if( IsNew ) then 0 else 1 endif
  ANNUAL
    z:IsExisting
ENDRULE

// ********** Heat Recovery Reference ********************************************
RULE OACtrl:HtRcvryRef
  DESCRIPTION
    "Reference to a heat recovery device, if one is part of the system."
  INPUTCLASS
    Optional
  DEFAULT
    
  CHECKCODE
    if( LocalCompAssigned( HtRcvryRef ) .AND. 
        AirSys:SysOARat < ( 1 - Proj:OATolLim ) )
    then
      PostError("Heat recovery devices are currently only available for use with
                 100% outside air systems. Remove the heat recovery object from
                 AirSystem '%s', and contact CBECC-Com support if your
                 system design is not 100% OA and includes heat recovery.",
                 AirSys:Name )
    else UNCHANGED
    endif
ENDRULE


// ********** Design Outside Air Flow Ratio **************************************
RULE NEW AirSys:SysOARat
  DATATYPE
    Float
  LONGFORM
    OutsideAirRatio
  INPUTCLASS
    NotInput
  DEFAULT  
    if( SupFanCap > 0 )
    then SysVentFlow / SupFanCap
    else 0
    endif
  CHECKSIM
    if( SysOARat > 1.005 .AND. IsExhSys = 0 ) 
    then 
      PostWarning("The design OA ratio of AirSystem '%s' is > 0, which indicates
                   the space design ventilation air flow rates are greater than
                   the system supply fan flow. The OA will be limited to the design
                   supply air flow rate.", Name)
    else UNCHANGED
    endif
  SIZING
    UNDEFINED
  ANNUAL
    if( SupFanCap > 0 )
    then SysVentFlow / SupFanCap
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ZnSys:SysOARat
  DATATYPE
    Float
  LONGFORM
    OutsideAirRatio
  INPUTCLASS
    NotInput
  DEFAULT  
    if( SupFanCap > 0 )
    then SysVentFlow / SupFanCap
    else 0
    endif
  SIZING
    UNDEFINED
  ANNUAL
    if( SupFanCap > 0 )
    then SysVentFlow / SupFanCap
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
// The VentSys design OARat echoed at the ThrmlZn
RULE NEW ThrmlZn:PropSysOARat
  DATATYPE
    Float
  LONGFORM
    ProposedSystemOutdoorAirRatio
  DESCRIPTION
    "The ratio of outside air to design supply air flow, for the proposed design."
  HELP
    "Determined at system, and then transfered to zone"
  INPUTCLASS 
    NotInput
  DEFAULT
    if( LocalCompAssigned( VentSysRef ) )
    then VentSysRef:SysOARat
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSys:PropSysOARat
  DATATYPE
    Float
  LONGFORM
    ProposedSystemOutdoorAirRatio
  DESCRIPTION
    "The minimum ratio of outside air to design supply air flow, for the proposed design."
  INPUTCLASS 
    NotInput
  SIZING
    MinRevRef( ThrmlZn:VentSysRef, ThrmlZn:PropSysOARat )
ENDRULE

// -----------------------------------------------------------------------------
// Set design OA flow at OA control, for reference only
RULE OACtrl:DsgnOAFlow
  DESCRIPTION
    "The rate of outside air that needs to be delivered by the system at 
     design conditions."
  HELP
    "Minimum ventilation requirements specified by Standard 120(b)2 as the greater
     of 15 cfm/person and the minimum ventilation rates specified in Appendix 5.4
     For systems serving laboratory spaces, the system shall be 100% outside air, 
     with ventilation rates determined by the Authority Having Jurisdiction."
  REFERENCE 
    NACM Section 5.7.4.1
  INPUTCLASS 
    NotInput
  MINIMUM 
    0
  COMMONMAXIMUM
    10000
  UNITS 
    cfm
// Design ventilation rate defined at ThermalZone, and summed to parent AirSystem
  REPORTPRECISION
    0
  DEFAULT
    AirSys:SysVentFlow  
  SIZING
    AirSys:SysVentFlow  
  ANNUAL
    z:DsgnOAFlow
ENDRULE


// ********** Ventilation Control Method ***************************************
RULE AirSys:VentCtrl
  DESCRIPTION
    "For multizone systems, the method used to determine the amount of outside 
     air that needs to be delivered by the system."
  HELP
    "This control method addresses how the system determines the outside air 
     flow rate on an hourly basis. Options include:

     Average Flow: The outside air delivered by the system is the sum of the 
     outside air requirement for each zone, without taking into account the 
     position of the VAV damper in each zone. The assumption is that there is 
     mixing between zones through the return air fan.

     Critical Zone: (NOT YET SUPPORTED) The critical zone is the zone with the 
     highest ratio of outside air to supply air. The assumption is that there is
     no mixing between zones. This method will provide greater outside air than 
     the average flow method because when the critical zone sets the outside air
     fraction at the system, the other zones are getting greater outside air 
     than required."
  REFERENCE 
    NACM Section 5.7.4.1
  INPUTCLASS 
    NotInput
  OPTION
    AverageFlow
;    CriticalZone (NOT YET SUPPORTED) 
  DEFAULT
    "AverageFlow"
  SIZING
    if( BaseSysNum > 0 )
    then "AverageFlow" 
    else u:VentCtrl
    endif
  ANNUAL
    z:VentCtrl
ENDRULE


// ********** Outside Air Schedule Method **************************************
RULE OACtrl:OASchMthd
  DESCRIPTION
    "The method used to describe the minimum amount of ventilation (outdoor) air 
     that is provided by the system."
  HELP
    "The minimum amount of outdoor air the system provides on an hourly basis can
     described in three different ways:

     Constant: The minimum outdoor air flow rate is not limited to when the system 
     availabilty schedule is set to off (0).  The system will provide the design 
     minimum outdoor air flow rate (or hourly rate defined by DCV controls) 
     during all hours the system runs, including if the system cycles on during 
     off hours.  This control method does not disable the economizer from 
     operating when the minimum outside air is scheduled to be 0.

     FollowHVACAvailability: The minimum amount of outdoor is zero when the system 
     availability schedule is set to off (0), but otherwise is allowed to be up 
     to the maximum OA fraction for all hours the system is schedule on (1).   
     The beginning and end hours of this schedule can be modified to deviate from 
     the system availabilty schedule by adjusting the start offset hour 
     properties.  These properties shift the hours that the OA fraction is set to
     zero.  For example, if system availabilty schedule includes a one hour morning
     warm-up period prior to occupancy, the start offset hour would be set to -1. 
     This control method does not disable the economizer from operating when the
     minimum outside air is scheduled to be 0.

     Scheduled: The amount of outdoor air provided by the system is determined by
     multiplying the sytem total (design) air flow rate by the hourly fraction 
     value. However, this schedule does not limit the economizer control from 
     increasing the maximum economizer air flow."
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  OPTION
    Constant
    FollowHVACAvailability
    Scheduled
  DEFAULT
    "FollowHVACAvailability"
  CHECKCODE : T24N
    if( IfValidAnd( OASchMthd != "FollowHVACAvailability" ) )
    then
      PostWarning("The outside air schedule method for OutsideAirControl '%s' 
                   will be reset to 'FollowHVACAvailability' for compliance 
                   analysis.", Name )
    else UNCHANGED
    endif
  SIZING : T24N
    "FollowHVACAvailability" 
  SIZING : S901G ECBC
    if( BaseSysNum > 0 )
    then "FollowHVACAvailability" 
    else u:OASchMthd
    endif
  ANNUAL
    z:OASchMthd
ENDRULE


// ********** Outside Air Schedule Reference ***********************************
RULE OACtrl:OASchRef
  DESCRIPTION
    "A schedule that defines the minimum outdoor air flow rate as a fraction of 
     the design outdoor air flow rate."
  HELP
    "If OutdoorAirScheduleMethod = Scheduled, defines the fractional schedule that
     limits the amount of outdoor air delivered by the system each hour. The
     schedule values are multiplied times the design OA flow rate, but does not
     limit the economizer control from increasing the maximum economizer air flow."
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Optional
  CHECKCODE : T24N
    if( IfValidAnd( OASchMthd = "Scheduled" ) .AND. 
        LocalCompAssigned( OASchRef ) ) 
    then
      PostWarning("The user-defined outside air schedule for OutsideAirControl '%s' 
                   will be ignored for compliance analysis.", Name )
    else UNCHANGED
    endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR.
        IfValidAnd( OASchMthd = "Scheduled" ) = 0 .OR. 
        LocalCompAssigned( OASchRef ) = 0 )
    then UNDEFINED
    else u:OASchRef
    endif
  ANNUAL
    z:OASchRef
ENDRULE


// ********** Start/End Outdoor Air Schedule Offsets  **************************
RULE OACtrl:StartUpDelay
  DESCRIPTION
    "If the OAScheduleMethod is 'FollowHVACAvailability', this positive integer 
     value indicates the number of hours that the system outside air flow is zero 
     during system start up."
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  UNITS
    hr
  MINIMUM
    0
  COMMONMAXIMUM
    2
  MAXIMUM
    12
  REPORTPRECISION
    0
  DEFAULT : T24N
    if( IfValidAnd( OASchMthd = "FollowHVACAvailability" ) )
    then 1
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IfValidAnd( OASchMthd = "FollowHVACAvailability" ) )
    then 0
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( IfValidAnd( OASchMthd = "FollowHVACAvailability" ) .AND. 
        IfValidAnd( StartUpDelay != 1 ) )
    then
      PostWarning("The user-defined OA start-up delay for OutsideAirControl '%s' 
                   will be reset to 1 hour.", Name )
    else UNCHANGED
    endif
  SIZING : T24N
    1
  SIZING : S901G ECBC
// TO DO: Add rules to set to be the same value as proposed?
    if( IfValidAnd( OASchMthd = "FollowHVACAvailability" ) )
    then ValidOr( StartUpDelay, 0 )
    else UNDEFINED
    endif
  ANNUAL
    z:StartUpDelay
ENDRULE


// ********** Maximum/Minimum Outside Air Fraction Schedules *******************
RULE NEW AirSys:VarFlowExhZnRef
  DATATYPE
    ThrmlZn
  LONGFORM
    VariableFlowExhaustZoneReference
  DESCRIPTION
    "The name of a zone that is ventilated by the current AirSystem system and
     has variable flow exhaust. Used to define the MaxOAFracSchRef property for
     systems that modulate OA flow with variable flow exhaust."
  INPUTCLASS 
    NotInput
  SIZING
    MaxRevRefComp( ThrmlZn:VentSysRef, ThrmlZn:IsVarFlowExh )
ENDRULE

// -----------------------------------------------------------------------------
RULE OACtrl:MaxOAFracSchRef
  DESCRIPTION
    "A schedule that defines the maximum outdoor air flow rate as a fraction of 
     the design supply air flow rate."
  HELP
    "This schedule is primarily used to limit the amount of outside air that is
     supplied by the ventiltion system when the exhaust system of the zone(s)
     is 'VariableFlow' and 'CoupledToSystem'.  This is typically the case for
     Laboratory and CommercialKitchen systems."
  INPUTCLASS 
    NotInput
  SIZING
    if( ParentCompAssigned( VarFlowExhZnRef ) .AND. IsNew )
    then ParentRef( VarFlowExhZnRef:ExhFlowSchRef )
    else UNCHANGED
    endif
  ANNUAL
    z:MaxOAFracSchRef
ENDRULE

// -----------------------------------------------------------------------------
RULE OACtrl:MinOAFracSchRef
  DESCRIPTION
    "A schedule that defines the minimum outdoor air flow rate as a fraction of 
     the design supply air flow rate."
  HELP
    "This property is not currently available for compliance.

     Specifying this is an alternate method for specifying the minimum outdoor air
     amount. The other method is using the OASchRef described above. If specified
     in the simulation, this schedule overrides the minimum OA schedule and
     minimum flow rate."
  INPUTCLASS 
    NotInput
ENDRULE


// ********** Economizer Availability Schedule *******************
RULE OACtrl:EconoAvailSchRef
  DESCRIPTION
    "A schedule that defines when the economizer is enabled/disabled."
  HELP
    "This property is not currently available for compliance.

     If an economizer is defined, it is always enabled unless locked out with
     heating or cooling, as specified by EconomizerIntegration."
  INPUTCLASS 
    NotInput
ENDRULE


// ********** Economizer Control Type ******************************************
RULE OACtrl:EconoCtrlMthd
  DESCRIPTION
    "The method used to control the air-side economizer."
  HELP
    "An air-side economizer increases outside air ventilation during periods when
     mechanical cooling loads can be reduced by increasing outside air flow. The 
     control types include:

     No economizer: Fixed outside are fraction at the system's design outside 
                    air flow when the system fan runs.

     Fixed dry-bulb: The system shifts to 100% outside air and shuts off the 
                    cooling when the temperature of the outside air is equal 
                    to or lower than the supply air temperature.

     Differential dry-bulb: The system shifts to 100% outside air when the 
                    temperature of the outside air is lower than the return air 
                    temperature but continues to operate the cooling system until 
                    the outside air temperature reaches the supply air temperature.

     Differential enthalpy: The system shifts to 100% outside air when the enthalpy 
                    of the outside air is lower than the return air enthalpy but 
                    continues to operate the cooling system until the outside air 
                    enthalpy reaches the supply air enthalpy.

     Differential enthalpy and dry-bulb: Utilizes combination of both the 
                    DifferentialDryBulb and DifferentialEnthalpy economizer 
                    control strategies."
  REFERENCE 
    NACM Section 5.7.4.2
  INPUTCLASS 
    Default
  OPTION
    NoEconomizer
    FixedDryBulb
    DifferentialDryBulb
    DifferentialEnthalpy
    DifferentialDryBulbAndEnthalpy
  DEFAULT
    "NoEconomizer"
  SIZING
    if( BaseSysNum > 0 )
    then // No economizer is assumed for sizing run
      "NoEconomizer"
    else u:EconoCtrlMthd
    endif
  ANNUAL : T24N
    if( BaseSysNum > 0 )
    then
      if( AirSys:BaseSysNum = 10 )
      then // CRAH system
        "DifferentialDryBulb" // No capacity threshold for CRAH units
      else if( AirSys:BaseSysNum = 12 )
      then // LAB system
        if( IfValidAnd( AirSys:PropSysOARat < 0.99 ) ) 
        then // Is a lab system and one of the proposed system serving the 
             // baseline lab zones(s) is less than 99% OA
          "DifferentialDryBulb"
        else "NoEconomizer"
        endif
;      else if( AirSys:BaseSysNum = 13 )
;      then // KITCH system
;        "None" // Commerical kitchen systems have no economizer.
      else if( AirSys:ClgCap > 54000 )
      then "DifferentialDryBulb"
      else "NoEconomizer"
      endif endif endif ; endif
    else z:EconoCtrlMthd
    endif
  ANNUAL : S901G ECBC
    if( BaseSysNum > 0 ) then
      if( AirSys:BaseSysNum = 1 .OR.
          AirSys:BaseSysNum = 2 .OR.
          AirSys:BaseSysNum = 9 .OR.
          AirSys:BaseSysNum = 10 ) then
        "NoEconomizer"
      else if( Proj:CliZnNum = 11 .OR.
               Proj:CliZnNum = 12 .OR.
               Proj:CliZnNum = 21 .OR.
               Proj:CliZnNum = 31 .OR.
               Proj:CliZnNum = 41 ) then
        "NoEconomizer"
      else if( Proj:OACleaningReqd = 1 .AND. Bldg:PropHasAirEcono = 0 ) then
        "NoEconomizer"
      else
        "FixedDryBulb"  // updated 8/26/2015 from DifferentialDryBulb. The PRM-RM has NoEconomizer for all other cases 
      endif endif endif
    else z:EconoCtrlMthd
    endif
ENDRULE


// ********** Economizer Control Type Report *************************************
RULE OACtrl:EconoCtrlMthdRpt
  RULESETS
    T24N
  DESCRIPTION
    "Translates EconoCtrlMthd to EconoCtrlMthdRpt."
  INPUTCLASS 
    NotInput
  OPTION
    No Economizer
    Fixed Dry Bulb
    Differential Dry Bulb
    Differential Enthalpy
    Differential Dry Bulb And Enthalpy
  ANNUAL_PROPOSED
    if( EconoCtrlMthd = "NoEconomizer" )
    then "No Economizer"
    else
    if( EconoCtrlMthd = "FixedDryBulb" )
    then "Fixed Dry Bulb"
    else
    if( EconoCtrlMthd = "DifferentialDryBulb" )
    then "Differential Dry Bulb"
    else
    if( EconoCtrlMthd = "DifferentialEnthalpy" )
    then "Differential Enthalpy"
    else
    if( EconoCtrlMthd = "DifferentialDryBulbAndEnthalpy" )
    then "Differential Dry Bulb And Enthalpy"
    else UNDEFINED
    endif endif endif endif endif
  ANNUAL_BASELINE
    UNDEFINED
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW AirSys:HasAirEcono
  DATATYPE
    Integer
  LONGFORM
    HasAirEconomizer
  DESCRIPTION
    "A flag that indicates whether the AirSystem has an air-side economizer."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SumChildrenIf( OACtrl:SysCnt, OACtrl:EconoCtrlMthd != "NoEconomizer" ) > 0 )
    then 1
    else 0
    endif
  ANNUAL
    if( SumChildrenIf( OACtrl:SysCnt, OACtrl:EconoCtrlMthd != "NoEconomizer" ) > 0 )
    then 1
    else 0
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW Bldg:PropHasAirEcono
  DATATYPE
    Integer
  LONGFORM
    ProposedHasAirEconomizer
  DESCRIPTION
    "Flag if proposed building has any economizers."
  HELP
    "Std. 62.1 sets requirements for air cleaning on outdoor air when outddor 
     air quality is poor.  In these cases, if the proposed building has no 
     economizers, then the baseline does not either.  "
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SumChildren( AirSys:HasAirEcono ) > 0 )
    then 1
    else 0
    endif
ENDRULE


// -----------------------------------------------------------------------------
// Adjust CRAC/CRAH fan power and capacity if an economizer is required
RULE AirSys:AirSeg:Fan:TotStaticPress
  ANNUAL : T24N
    if( BaseSysNum > 0 )
    then  
      if( AirSys:HasAirEcono > 0 .AND.
          ( AirSys:SubType = "CRAH" .OR.
            AirSys:SubType = "CRAC" ) )
      then // Increase static pressure by ratio of Econo / NoEcono fan power
        TotStaticPress * ( 0.49 / 0.39)    
      else UNCHANGED
      endif
    else UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
RULE AirSys:AirSeg:CoilClg:CapTotGrossRtd
  ANNUAL : T24N
    if( BaseSysNum > 0 )
    then 
      if( AirSys:HasAirEcono > 0 .AND.
          ( AirSys:SubType = "CRAH" .OR.
            AirSys:SubType = "CRAC" ) )
      then // Increase cooling capacity by extra fan heat
        CapTotGrossRtd + ( 0.1 * Parent2( SupFanCap ) / 3.412 ) // Extra 0.1 W/cfm
      else UNCHANGED
      endif
    else UNCHANGED
    endif   
ENDRULE

// -----------------------------------------------------------------------------
RULE AirSys:AirSeg:CoilClg:CapTotGrossRtdSim
  ANNUAL : T24N
    if( BaseSysNum > 0 )
    then 
      if( AirSys:HasAirEcono > 0 .AND.
          ( AirSys:SubType = "CRAH" .OR.
            AirSys:SubType = "CRAC" ) )
      then // Update CapTotGrossRtdSim
        CapTotGrossRtd * SysCnt
      else UNCHANGED
      endif
    else UNCHANGED
    endif 
ENDRULE


// ********** Economizer Integration Level *************************************
RULE OACtrl:EconoIntegration
  DESCRIPTION
    "Specifies whether or not the economizer is integrated with mechanical
     cooling."
  HELP
    "Options include:
     
     NonIntegrated: The system runs the economizer as the first stage
                    of cooling. When the economizer is unable to meet the load, 
                    the economizer returns the outside air damper to the minimum
                    position and the compressor turns on as the second stage of
                    cooling.
     Integrated:    The system can operate with the economizer fully open to outside 
                    air and mechanical cooling active (compressor running) 
                    simultaneously, even on the lowest cooling stage."
  REFERENCE 
    NACM Section 5.7.4.2
  INPUTCLASS 
    Default
  OPTION
    NonIntegrated
    Integrated
;   LockoutWithHeating
  DEFAULT
    if( EconoCtrlMthd != "NoEconomizer" )
    then "NonIntegrated"
    else UNDEFINED
    endif
  CHECKSIM
    if( EconoCtrlMthd != "NoEconomizer" .AND. 
        LocalStatus( EconoIntegration ) = 0 )
    then 
      PostError("Economizer integration for OutsideAirControl '%s' is a 
                 required input.", Name)
    else UNCHANGED
    endif
  SIZING
    if( EconoCtrlMthd = "NoEconomizer" )
    then UNDEFINED
    else if( BaseSysNum > 0 )
    then // No economizer is assumed for sizing run
      UNDEFINED
    else u:EconoIntegration
    endif endif
  ANNUAL
    if( BaseSysNum > 0 )
    then
      if( EconoCtrlMthd != "NoEconomizer" )
      then "Integrated"
      else UNDEFINED
      endif
    else z:EconoIntegration
    endif
ENDRULE


// ********** Fault Detection and Diagnostics **********************************
RULE AirSys:FDDEnabled
  RULESETS
    T24N
  DESCRIPTION
    "Indicates that the system has fault detection and diagnostics (FDD) capability."
  HELP
    "Controls and sensors permanently installed in a unitary DX expansion unit 
     to display system performance and detect faults."
  INPUTCLASS
    NotInput
  DEFAULT
    0
  SIZING
    UNDEFINED
  ANNUAL
    z:FDDEnabled
ENDRULE


// ********** Maximum Outside Air Ratio ****************************************
RULE OACtrl:MaxOARat
  DESCRIPTION
    "The maximum ratio of outside air that a system can provided, defined
     as a percentage of the design supply air."
  HELP
    "Applies to systems with modulating outside air dampers. Economizers for 
     smaller systems (<54,000 Btu/h) are assumed to have a restricted intake 
     capacity." 
  REFERENCE
    NACM Section 5.7.4.1 
  INPUTCLASS 
    Prescribed
  MINIMUM 
    0.0  
  MAXIMUM 
    1.0
  REPORTPRECISION
    2
  DEFAULT : T24N
    if( IfValidAnd( EconoCtrlMthd != "NoEconomizer" ) )
      then
      if( IfValidAnd( AirSys:ClgCap < 54000 ) .AND. 
          IfValidAnd( AirSys:FDDEnabled = 0 ) ) 
      then 0.9
      else 1.0
      endif
    else UNDEFINED
    endif
  DEFAULT : S901G ECBC
    if( IfValidAnd( EconoCtrlMthd != "NoEconomizer" ) )
      then 1.0
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( IfValidAnd( EconoCtrlMthd != "NoEconomizer" ) .AND.
        IfValidAnd( AirSys:ClgCap < 54000 ) .AND. 
        IfValidAnd( AirSys:FDDEnabled = 0 ) .AND.
        IfValidAnd( MaxOARat > 0.9 ) ) 
    then 
      PostWarning("The Maximum OA Ratio for OutsideAirControl '%s' will be reset
                   to 0.9 for compliance analyis.", Name )
    else UNCHANGED
    endif   
  SIZING : T24N
    if( BaseSysNum > 0 )
    then UNDEFINED
    else if( IfValidAnd( EconoCtrlMthd != "NoEconomizer" ) )
      then
      if( IfValidAnd( AirSys:ClgCap < 54000 ) .AND. 
          IfValidAnd( AirSys:FDDEnabled = 0 ) ) 
      then Min( 0.9, u:MaxOARat)
      else u:MaxOARat
      endif
    else UNDEFINED
    endif endif
  SIZING : S901G ECBC
    if( BaseSysNum > 0 )
    then UNDEFINED
    else if( IfValidAnd( EconoCtrlMthd != "NoEconomizer" ) )
      then u:MaxOARat
    else UNDEFINED
    endif endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. EconoCtrlMthd != "NoEconomizer" )
    then
      if( IfValidAnd( AirSys:ClgCap < 54000 ) )
      then 0.9
      else 1.0
      endif
    else z:MaxOARat
    endif  
  ANNUAL : S901G ECBC
    if( BaseSysNum > 0 .AND. EconoCtrlMthd != "NoEconomizer" )
    then 1.0
    else z:MaxOARat
    endif  
ENDRULE


// ********** Economizer High Temperature Lockout ******************************
RULE OACtrl:EconoHiTempLockout
  DESCRIPTION
    "The outside air drybulb temperature above which the economizer will return 
     to its minimum position."
  REFERENCE 
    NACM Section 5.7.4.2
  COMMONMINIMUM
    55
  COMMONMAXIMUM
    85  
  UNITS 
    °F  
  REPORTPRECISION
    0
  DEFAULT : S901G
    if( IfValidAnd( EconoCtrlMthd = "FixedDryBulb" ) )
    then
      if( Proj:CliZnNum = 22 .OR.
          Proj:CliZnNum = 32 .OR.
          Proj:CliZnNum = 33 .OR.
          Proj:CliZnNum = 42 .OR.
          Proj:CliZnNum = 43 .OR.
          Proj:CliZnNum = 52 .OR.
          Proj:CliZnNum = 53 .OR.
          Proj:CliZnNum = 62 .OR.
          Proj:CliZnNum = 70 .OR.
          Proj:CliZnNum = 80 )
      then 75
      else
        if( Proj:CliZnNum = 51 .OR.
            Proj:CliZnNum = 61 )
        then 70
        else 65
        endif
      endif
    else UNDEFINED
    endif
  DEFAULT
    if( IfValidAnd( EconoCtrlMthd = "FixedDryBulb" ) )
    then 70.0
    else UNDEFINED
    endif
  CHECKSIM : S901G
    if( IfValidAnd( EconoCtrlMthd = "FixedDryBulb" ) .AND. 
        LocalStatus( EconoHiTempLockout ) = 0 )
    then 
      PostError("OutsideAirControl '%s' dry bulb temperature high limit is 
                 required for FixedDryBulb control.", Name)
    else UNCHANGED
    endif
  CHECKSIM
    if( IfValidAnd( EconoCtrlMthd = "FixedDryBulb" ) .AND. 
        LocalStatus( EconoHiTempLockout ) = 0 )
    then 
      PostError("OutsideAirControl '%s' dry bulb temperature high limit is 
                 required for FixedDryBulb control.", Name)
    else UNCHANGED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then // No economizer is assumed for sizing run
      UNDEFINED
    else u:EconoHiTempLockout
    endif
  ANNUAL : S901G
    if( BaseSysNum > 0 )
    then
      if( EconoCtrlMthd = "FixedDryBulb" )
      then
        if( Proj:CliZnNum = 22 .OR.
            Proj:CliZnNum = 32 .OR.
            Proj:CliZnNum = 33 .OR.
            Proj:CliZnNum = 42 .OR.
            Proj:CliZnNum = 43 .OR.
            Proj:CliZnNum = 52 .OR.
            Proj:CliZnNum = 53 .OR.
            Proj:CliZnNum = 62 .OR.
            Proj:CliZnNum = 70 .OR.
            Proj:CliZnNum = 80 )
        then 75
        else
          if( Proj:CliZnNum = 51 .OR.
              Proj:CliZnNum = 61 )
          then 70
          else 65
          endif
        endif
      else UNDEFINED
      endif
    else z:EconoHiTempLockout
    endif
  ANNUAL
    if( BaseSysNum > 0 )
    then // Baseline is either no economizer or DifferentialDryBulb control
      UNDEFINED
    else z:EconoHiTempLockout
    endif
ENDRULE


// ********** Economizer Low Temperature Lockout *******************************
RULE OACtrl:EconoLowTempLockout
  DESCRIPTION
    "The outside air drybulb temperature below which the economizer will return 
     to its minimum position."
  REFERENCE 
    NACM Section 5.7.4.2
  INPUTCLASS 
    Default
  UNITS 
    °F
  SIZING
    if( BaseSysNum > 0 )
    then // No economizer is assumed for sizing run
      UNDEFINED
    else u:EconoLowTempLockout
    endif
  ANNUAL
    if( BaseSysNum > 0 )
    then // Baseline is either no economizer or DifferentialDryBulb control
      UNDEFINED
    else z:EconoLowTempLockout
    endif
ENDRULE


// ********** Economizer High Enthalpy Lockout *********************************
RULE OACtrl:EconoHiEnthLockout
  DESCRIPTION
    "The outside air drybulb temperature above which the economizer will return 
     to its minimum position."
  REFERENCE 
    NACM Section 5.7.4.2
  INPUTCLASS 
    Prescribed 
  UNITS 
    Btu/lb
  MINIMUM
    13
  REPORTPRECISION
    1
  DEFAULT
    if( IfValidAnd( EconoCtrlMthd = "DifferentialEnthalpy" ) .OR. 
        IfValidAnd( EconoCtrlMthd = "DifferentialDryBulbAndEnthalpy" ) )
    then 28.0
    else UNDEFINED
    endif
  CHECKSIM
    if( ( IfValidAnd( EconoCtrlMthd = "DifferentialEnthalpy" ) .OR. 
          IfValidAnd( EconoCtrlMthd = "DifferentialDryBulbAndEnthalpy" ) ) .AND.
        LocalStatus( EconoHiEnthLockout ) =  0 )
    then 
      PostError("OutsideAirControl '%s' enthalpy high limit is required for 
                 DifferentialEnthalpy or DifferentialDryBulbAndEnthalpy control
                 schemes.", Name)
    else UNCHANGED
    endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. 
        LocalStatus( EconoHiEnthLockout ) = 0 )
    then // No economizer is assumed for sizing run
      UNDEFINED
    else u:EconoHiEnthLockout + 2.0
    endif
  ANNUAL
    if( BaseSysNum > 0 )
    then // Baseline is either no economizer or DifferentialDryBulb control
      UNDEFINED
    else z:EconoHiEnthLockout
    endif
ENDRULE


// ********** Economizer Control Type *****************************************
RULE OACtrl:CtrlType
  DESCRIPTION
    "The method used to control the the economizer." 
  HELP
    "This property is not currently available for compliance. 


     ModulateFlow means the outdoor air fow rate will be increased to meet the 
     mixed air setpoint temperature, subject to the limits imposed via other 
     inputs for this object (e.g., EconomizerMaximum Limit Dry-Bulb Temperature,
     Maximum Outdoor Air Flow Rate, etc.).

     
     MinimumFlowWithBypass is used exclusively in conjunction with air-to-air
     heat recovery for providing free cooling operation in the absence 
     of a conventional air-side economizer (i.e., when outdoor air fow rate is 
     not increased during economizer mode). 
     The MinimumFlowWithBypass choice forces the outdoor air fow rate to always
     remain at the minimum. However, when high humidity control is used, the 
     outdoor air flowrate is set to the product of the maximum outdoor air flow
     rate multiplied by the high humidity outdoor air fow ratio. The heat
     exchanger uses the limit checking in the outdoor air controller to decide
     whether or not to bypass the outdoor air around the heat exchanger or
     turn of the wheel motor in the case of a rotary heat exchanger. Heat exchange
     is also suspended when highhumidity control is active. The ModulateFlow 
     option can also be used with HeatRecovery objects."
  INPUTCLASS 
    NotInput
  OPTION
    ModulateFlow
    MinimumFlowWithBypass
// OS Default is 'ModulateFlow'
  DEFAULT
    if( LocalCompAssigned( HtRcvryRef ) .AND. EconoCtrlMthd = "NoEconomizer" )
    then "MinimumFlowWithBypass"
    else "ModulateFlow"
    endif
  SIZING
    if( BaseSysNum > 0 )
    then "ModulateFlow"
    else u:CtrlType
    endif
  ANNUAL
    z:CtrlType
ENDRULE


// ********** Economizer Minimum Limit Type ***********************************
RULE OACtrl:MinLimitType
  DESCRIPTION
    "The method used to determine the minimum outdoor air flow rate."
  HELP
    "This property is not currently available for compliance. 
     
     Fixed means the minimum outdoor airfow rate is fixed no matter
     what the actual system flow rate is. Proportional means the minimum
     outdoor airfow rate varies in proportion to the total system air fow rate."
  INPUTCLASS 
    NotInput
  OPTION
    Fixed
    Proportional
// OS Default is 'Fixed'
ENDRULE


// ********** Heat Recovery Bypass Control Type *******************************
RULE OACtrl:HtRcvryBypassCtrlType
  DESCRIPTION
    ""
  HELP
    "This property is not currently available for compliance. 

     This choice feld determines if specialized control is used to optimize
     the use of heat recovery. If BypassWhenWithinEconomizerLimits is selected,
     heat recovery is disabled any time the controller determines that the
     economizer is active (i.e., all controls are within limits). 
     If BypassWhenOAFlowGreaterThanMinimum is selected, the model frst verifies
     that the economizer is active and then checks to see if the outdoor air 
     flow rate is greater than the minimum. If it is greater than minimum, 
     then heat recovery (if any) is set to bypass. When this option is used 
     with Time of Day Economizer Control or High Humidity Control, this option
     has priority."
  INPUTCLASS 
    NotInput
  OPTION
    BypassWhenWithinEconomizerLimits
    BypassWhenOAFlowGreaterThanMinimum
// OS Default is 'BypassWhenWithinEconomizerLimits'
  DEFAULT
    if ( LocalCompAssigned( HtRcvryRef ) .AND. 
         EconoCtrlMthd != "NoEconomizer" )
    then "BypassWhenOAFlowGreaterThanMinimum"
    else "BypassWhenWithinEconomizerLimits"
    endif
  SIZING
    if( BaseSysNum > 0 )
    then "BypassWhenWithinEconomizerLimits"
    else u:HtRcvryBypassCtrlType
    endif
  ANNUAL
    z:HtRcvryBypassCtrlType
ENDRULE


// -----------------------------------------------------------------------------
// Revise DCV control and VentPerPersonSim if system does not have an economizer
// See GC issue 433
RULE ThrmlZn:VentCtrlMthd
  ANNUAL
    if( BaseSysNum > 0 .AND. HasDCV .AND.
        LocalCompAssigned( VentSysRef ) = ComponentType( "AirSys" ) )
    then 
      if( VentSysRef:HasAirEcono = 0 )
      then "Fixed"
      else UNCHANGED
      endif
    else UNCHANGED
    endif
ENDRULE
RULE ThrmlZn:HasDCV
  ANNUAL
    if( VentCtrlMthd = "CO2Sensors" )
    then 1
    else 0
    endif
ENDRULE
RULE NEW Spc:HasDCV
  ANNUAL
    if( LocalCompAssigned( ThrmlZnRef ) )
    then ValidOr( ThrmlZnRef:HasDCV, 0 )
    else 0
    endif 
ENDRULE
RULE ThrmlZn:VentPerPersonSim
  ANNUAL
    if( BaseSysNum > 0 .AND. HasDCV = 0 )
    then 0
    else UNCHANGED
    endif
ENDRULE
RULE ThrmlZn:VentPerAreaSim
  ANNUAL
    if( BaseSysNum > 0 .AND. HasDCV = 0 )
    then zb:VentPerAreaSim
    else UNCHANGED
    endif
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW AirSys:AirEconoTypeCode
  DATATYPE
    Integer
  LONGFORM
    AirEconomizerTypeCode
  DESCRIPTION
    "A flag that indicates economizer types for the AirSystem.
     NoEconomizer  = 1,
     FixedDryBulb = 2,
     DifferentialDryBulb = 3,
     DifferentialEnthalpy = 4,
     DifferentialDryBulbAndEnthalpy = 5."
  INPUTCLASS 
    NotInput
  DEFAULT
    if( SumChildrenIf( OACtrl:SysCnt, OACtrl:EconoCtrlMthd = "NoEconomizer" ) > 0 ) 
      then 1
    else if( SumChildrenIf( OACtrl:SysCnt, OACtrl:EconoCtrlMthd = "FixedDryBulb" ) > 0 )
      then 2
    else if( SumChildrenIf( OACtrl:SysCnt, OACtrl:EconoCtrlMthd = "DifferentialDryBulb" ) > 0 )
      then 3
    else if( SumChildrenIf( OACtrl:SysCnt, OACtrl:EconoCtrlMthd = "DifferentialEnthalpy" ) > 0 )
      then 4
    else if( SumChildrenIf( OACtrl:SysCnt, OACtrl:EconoCtrlMthd = "DifferentialDryBulbAndEnthalpy" ) > 0 )
      then 5
    else 0
    endif endif endif endif endif
ENDRULE