// HVAC Secondary Systems - Chillers
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
//
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALifORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN if
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------

//  This rule file addresses the following building descriptors:
//  Section 5.8.2 - Chillers
//      Chiller Type
//      Number of Identical Chiller Units
//      Chiller Fuel
//      Chiller Rated Capacity
//      Chiller Rated Efficiency
//      Integrated Part-Load Value
//      Chiller Minimum Unloading Ratio
//      Chiller Cooling Capacity Adjustment Curve
//      Electric Chiller Cooling Efficiency Adjustment Curves
//      Fuel and Steam Chiller Cooling Efficiency Adjustment Curves
//      Chilled Water Supply Temperature
//      Chilled Water Return Temperature
//      Chilled Water Supply Temperature Reset
//      Condenser Type
//      Air-Cooled Condenser Power
//      ..


// ---------- Section 5.8.2 - Chillers -------------------------------


// ********** <Chiller Plant Capacity> *****************************************
RULE NEW FluidSys:ChWLoopLdNoPump
  DATATYPE
    FLOAT
  DESCRIPTION
    "The total peak load on chillers from sizing run."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  MINIMUM
    0
  SIZING
    UNDEFINED
  ANNUAL
    if( IsBaseSys .AND. Type = "ChilledWater" ) 
    then SumChildren(z:Chlr:CapRtd)
    else UNDEFINED
    endif
ENDRULE


// ********** <Pump Heat> ******************************************************
RULE NEW FluidSys:PumpHt
  DATATYPE
    FLOAT
  DESCRIPTION
    "Heat added by the pumps to the fluid loop."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr
  ANNUAL : S901G ECBC
    // Primary pump is 9 W/gpm and the secondary pump is 13 W/gpm
    // for a total of 22 W/gpm
    // FlowCap = ChWLoopLdNoPump/(DsgnSupWtrDelT*500.19)
    if( IsBaseSys ) then 
      ChWLoopLdNoPump/(DsgnSupWtrDelT*500.19) * 22 * 3.412
    else 0
    endif
  ANNUAL
    // Pump:Pwr(W) = BHP * 746/MtrEff
    // BHP = FlowCap * TotHd / 3960 / ImpellerEff
    // FlowCap = ChWLoopLdNoPump/(DsgnSupWtrDelT*500.19)
    // TotHd = Min (40 + (0.03 * ChWLoopLdNoPump/12000),64)  because 40 + 0.03 x 800 tons = 64   
    // ImpellerEff = 0.7
    // MtrEff = 0.855            Not included-Motor heat not added to fluid
    if( IsBaseSys .AND. Type = "ChilledWater" )
    then 
    ChWLoopLdNoPump/(DsgnSupWtrDelT*500.19) *
      Min(40 + (0.03*(ChWLoopLdNoPump/12000)),64) /
      3960 / 0.7 * 3.412
    else 0
    endif
ENDRULE


// ********** <Chiller Plant Capacity> *****************************************
RULE NEW FluidSys:ChWLoopLd
  DATATYPE
    FLOAT
  DESCRIPTION
    "The total peak load on chillers from sizing run including approx. pump heat."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  MINIMUM
    0
  SIZING
    UNDEFINED
  ANNUAL
    if( IsBaseSys .AND. Type = "ChilledWater" ) 
    then ChWLoopLdNoPump + PumpHt
    else UNDEFINED
    endif
ENDRULE


// ********** <Number of Chillers in Chilled Water System> *********************
RULE NEW FluidSys:ChlrCnt
  DATATYPE
    INTEGER
  DESCRIPTION
    "The number of chillers in a FluidSystem:type = ChilledWater."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  MINIMUM
    0
  COMMONMINIMUM
    1
  DEFAULT
    if( Type = "ChilledWater" )
      then ChildCount(Chlr)
    else UNDEFINED
    endif
  SIZING
    if( Type = "ChilledWater" ) then
      if( IsBaseSys ) then
        1
      else ChildCount(Chlr)
      endif
    else UNDEFINED
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( LocalStatus( ChWLoopLd ) < 1 ) then
        UNDEFINED
      else if( Type = "ChilledWater" .OR. 
               Type = "CondenserWater") then
        if( ChWLoopLd/12000 <= 300 ) then
          1
        else if( ChWLoopLd/12000 <= 1600 ) then
          2
        else
          INT((ChWLoopLd+9599999)/9600000)
        endif endif
      else UNDEFINED
  	  endif endif
  	else if( Type = "ChilledWater" ) then    // Type = CondenserWater?
      ChildCount(Chlr)
    else UNDEFINED
    endif endif
ENDRULE


// ********** <Number of Chillers in Project> **********************************
RULE Proj:ChlrCnt
  DESCRIPTION
    "The number of chillers in the project."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  REPORTPRECISION
    0
  DEFAULT
    SumChildren(FluidSys:ChlrCnt)
  SIZING
    u:ChlrCnt
  ANNUAL
    z:ChlrCnt
ENDRULE


// ********** <Create a second baseline chiller> *******************************
RULE NEW FluidSys:Chlr2Ref
  DATATYPE
    Chlr
  DESCRIPTION
    "Creates a second chiller for the baseline when the capacity requires it."
  HELP
    "If the chiller system cooling capacity is between 300 and 1600 tons,
    the project needs 2 baseline chillers.  The second chiller is created
    here."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL : T24N
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 1 ) )
    then 
      RuleLibrary(Chlr, "Base Chlr", 1, "BaseChWSystem")
    else UNDEFINED
    endif
  ANNUAL : S901G
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 1 ) )
    then 
      RuleLibrary(Chlr, "Base PriSec Chlr", 1, "BasePriSecChWSystem")
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a third baseline chiller> *******************************
RULE NEW FluidSys:Chlr3Ref
  DATATYPE
    Chlr
  DESCRIPTION
    "Creates a third chiller for the baseline when the capacity requires it."
  HELP
    "If the chiller system cooling capacity is between 1600 and 2400 tons,
    the project needs 3 baseline chillers.  The third chiller is created
    here."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL : T24N
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 2 ) )
    then 
      RuleLibrary(Chlr, "Base Chlr", 1, "BaseChWSystem")
    else UNDEFINED
    endif
  ANNUAL : S901G
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 2 ) )
    then 
      RuleLibrary(Chlr, "Base PriSec Chlr", 1, "BasePriSecChWSystem")
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a fourth baseline chiller> *******************************
RULE NEW FluidSys:Chlr4Ref
  DATATYPE
    Chlr
  DESCRIPTION
    "Creates a fourth chiller for the baseline when the capacity requires it."
  HELP
    "If the chiller system cooling capacity is between 2400 and 3200 tons,
    the project needs 4 baseline chillers.  The fourth chiller is created
    here."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL : T24N
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 3 ) )
    then 
      RuleLibrary(Chlr, "Base Chlr", 1, "BaseChWSystem")
    else UNDEFINED
    endif
  ANNUAL : S901G
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 3 ) )
    then 
      RuleLibrary(Chlr, "Base PriSec Chlr", 1, "BasePriSecChWSystem")
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a fifth baseline chiller> *******************************
RULE NEW FluidSys:Chlr5Ref
  DATATYPE
    Chlr
  DESCRIPTION
    "Creates a fifth chiller for the baseline when the capacity requires it."
  HELP
    "If the chiller system cooling capacity is between 3200 and 4000 tons,
    the project needs 5 baseline chillers.  The fifth chiller is created
    here."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL : T24N
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 4 ) )
    then 
      RuleLibrary(Chlr, "Base Chlr", 1, "BaseChWSystem")
    else UNDEFINED
    endif
  ANNUAL : S901G
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 4 ) )
    then 
      RuleLibrary(Chlr, "Base PriSec Chlr", 1, "BasePriSecChWSystem")
    else UNDEFINED
    endif
ENDRULE


// ********** <Create a sixth baseline chiller> *******************************
RULE NEW FluidSys:Chlr6Ref
  DATATYPE
    Chlr
  DESCRIPTION
    "Creates a sixth chiller for the baseline when the capacity requires it."
  HELP
    "If the chiller system cooling capacity is between 4000 and 4800 tons,
    the project needs 2 baseline chillers.  The sixth chiller is created
    here."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    NotInput
  SIZING
    UNDEFINED
  ANNUAL : T24N
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 5 ) )
    then 
      RuleLibrary(Chlr, "Base Chlr", 1, "BaseChWSystem")
    else UNDEFINED
    endif
  ANNUAL : S901G
    if( IsBaseSys .AND.
        Type = "ChilledWater" .AND. 
        IfValidAnd( ChlrCnt > 5 ) )
    then 
      RuleLibrary(Chlr, "Base PriSec Chlr", 1, "BasePriSecChWSystem")
    else UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
// Connect new chillers, if present, to chilled water / condenser water fluid segments
RULE Chlr:EvapFluidSegOutRef
  ANNUAL : S901G ECBC
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( EvapFluidSegOutRef ) = 0 .AND.
        CompExists( FluidSeg, "BasePriSecChWPriSupSeg" ) )
    then "BasePriSecChWPriSupSeg"
    else UNCHANGED
    endif
  ANNUAL 
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( EvapFluidSegOutRef ) = 0 .AND.
        CompExists( FluidSeg, "BaseChWPriSupSeg" ) )
    then "BaseChWPriSupSeg"
    else UNCHANGED
    endif
ENDRULE

RULE Chlr:EvapFluidSegInRef
  ANNUAL : S901G ECBC
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( EvapFluidSegInRef ) = 0 .AND.
        CompExists( FluidSeg, "BasePriSecChWPriRetSeg" ) )
    then "BasePriSecChWPriRetSeg"
    else UNCHANGED
    endif
  ANNUAL
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( EvapFluidSegInRef ) = 0 .AND.
        CompExists( FluidSeg, "BaseChWPriRetSeg" ) )
    then "BaseChWPriRetSeg"
    else UNCHANGED
    endif
ENDRULE

RULE Chlr:CndsrFluidSegOutRef
  ANNUAL
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( CndsrFluidSegOutRef ) = 0 .AND.
        CompExists( FluidSeg, "BaseCWPriRetSeg" ) )
    then "BaseCWPriRetSeg"
    else UNCHANGED
    endif
ENDRULE

RULE Chlr:CndsrFluidSegInRef
  ANNUAL
    if( FluidSys:IsBaseSys > 0 .AND. 
        LocalCompAssigned( CndsrFluidSegInRef ) = 0 .AND.
        CompExists( FluidSeg, "BaseCWPriSupSeg" ) )
    then "BaseCWPriSupSeg"
    else UNCHANGED
    endif
ENDRULE


// ********** Status *********************************************************** 
RULE Chlr:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  INPUTCLASS 
    Default
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New'
;   New
;   Existing 
  DEFAULT
// Status defined from top-down
    if( Parent( IsNew ) ) then "New" else "Existing" endif
  CHECKCODE
    if( ( Proj:IsNewMech .AND. Status != "New" ) .OR.
        ( Proj:IsAlt = 0 .AND. Status = "Altered" ) )
    then
      PostWarning("Chiller '%s' has a Status of '%s', but Compliance Type is '%s'.
                   The status of the system will be changed to 'New' for compliance
                   analysis.", Name, Status, Proj:CompType)
    else UNCHANGED
    endif
   SIZING
    if( IfValidAnd( FluidSys:IsBaseSys > 0 ) .OR. Proj:IsNewMech )
    then "New"
    else u:Status
    endif
  ANNUAL
    if( IfValidAnd( FluidSys:IsBaseSys > 0 ) .OR. Proj:IsNewMech )
    then "New"
    else z:Status
    endif
ENDRULE

RULE NEW Chlr:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  SIZING
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  ANNUAL
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
ENDRULE

RULE NEW Chlr:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsNew ) then 0 else 1 endif
  SIZING
    if( IsNew ) then 0 else 1 endif
  ANNUAL
    if( IsNew ) then 0 else 1 endif
ENDRULE


// ********** <Chiller Capacity> ***********************************************
RULE NEW Chlr:CapRtdNoPump
  DATATYPE
    FLOAT
  DESCRIPTION
    "Cooling output of the chiller at full load and rated conditions."
  HELP
    "The nominal full load output of the chiller at design operating conditions."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr
  ANNUAL
    if( IsBaseSys )
    then Parent(ChWLoopLdNoPump)/Parent(ChlrCnt)
    else 0
    endif
ENDRULE


// ********** <Pump Heat> ******************************************************
RULE NEW Chlr:PumpHt
  DATATYPE
    FLOAT
  DESCRIPTION
    "Heat added by the pump to the fluid loop."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr
  ANNUAL : S901G ECBC
    // Primary pump is 9 W/gpm and the secondary pump is 13 W/gpm
    // for a total of 22 W/gpm
    // FlowCap = Parent(CapRtd)/(Parent(DsgnSupWtrDelT)*500.19)
    if( IsBaseSys ) then 
      CapRtdNoPump/(Parent(DsgnSupWtrDelT)*500.19) * 22 * 3.412
    else 0
    endif
  ANNUAL
    // Pump:Pwr(W) = BHP * 746/MtrEff
    // BHP = FlowCap * TotHd / 3960 / ImpellerEff
    // FlowCap = Parent(CapRtd)/(Parent(DsgnSupWtrDelT)*500.19)
    // TotHd = Min (40 + (0.03 * Chlr:CapRtd/12000),100)
    // ImpellerEff = 0.7
    // MtrEff = 0.855            Not included-Motor heat not added to fluid
    if( IsBaseSys )
    then 
    CapRtdNoPump/(Parent(DsgnSupWtrDelT)*500.19) *
      Min(40 + (0.03*(CapRtdNoPump/12000)),100) /
      3960 / 0.7 * 3.412
    else 0
    endif
ENDRULE


// ********** <Chiller Rated Capacity> *****************************************
RULE Chlr:CapRtd
  DESCRIPTION
    "The cooling capacity of the chiller at rating conditions."
  HELP
    "The full load output of the chiller operating at rating temperatures and
     flows including adjustment for pump heat and the project sizing ratio."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Required
  MINIMUM
    0
  COMMONMINIMUM
    60000
  UNITS
    Btu/hr
  REPORTPRECISION
    -3
  DEFAULT
    if(Proj:AutoHardSize = 1
       .AND. LocalCompAssigned(EvapFluidSegInRef) ) then
      // For PROPOSED AutoHardSizing only
      EvapFluidSegInRef:CoilClgTotCap/Parent(ChlrCnt)
      ; Non-coincident total capacity of all cooling coils
    else UNDEFINED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED   // AutoSize
    else u:CapRtd
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( (CapRtdNoPump + PumpHt) > 0 ) then
        (CapRtdNoPump + PumpHt) * Proj:ClgSizingRat
      else 10
      endif
    else if( IsExisting )
    then zp:CapRtd
    else z:CapRtd
    endif endif
ENDRULE


// ********** <Chiller Condenser Type> ************************************
RULE Chlr:CndsrType
  DESCRIPTION
    "Method used to reject heat from the chiller."
  HELP
    "Air-cooled chillers use air to cool the condenser coils. Water-cooled
    chillers use cold water to cool the condenser and additionally need
    either a cooling tower or a local source of cold water. Evaporatively-
    cooled chillers are similar to air-cooled chillers, except they use a
    water mist to cool the condenser coil which makes them more efficient."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Required
; OPTION
; Defined in BEMEnums, shown here for reference
;   Air
;   Fluid
;   EvaporativePreCooled
  DEFAULT
    if( Type = "Centrifugal" .OR. Type = "AbsorptionSingleEffect" ) then
      "Fluid"
    else "Air"
    endif
  SIZING
    if( IsBaseSys ) then
      "Fluid"
    else u:CndsrType
    endif
  ANNUAL
    if( IsBaseSys ) then
      "Fluid"
    else z:CndsrType
    endif
ENDRULE


// ********** <Chiller Type> ************************************
RULE Chlr:Type
  DESCRIPTION
    "The type of chiller being used, based on the compressor type or
    absorption process."
  HELP
    "The type of chiller selected from a list: Centrifugal, Reciprocating,
       Scroll, Screw, Heat Recovery, Heat Pump, Single Effect Absorption,
       Double Effect Absorption, Triple Effect Absorption.  Note that the
       heat rejection process is not included in the Type descriptor.  See
       Condenser Type."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Required
; OPTION
; Defined in BEMEnums, shown here for reference
;   Centrifugal // Not a valid selection if CndsrType = 'Air'
;   Reciprocating
;   Scroll
;   Screw
;   AbsorptionSingleEffect // Not valid if CndsrType = 'Air'
;   AbsorptionDoubleEffect       // Not yet supported
;   GasEngineDriven              // Not yet supported
  CHECKSIM
    if( ( Type = "Centrifugal" .OR. Type = "AbsorptionSingleEffect" ) .AND. 
          CndsrType = "Air" ) then 
      PostError("For chiller '%s', the combination of chiller Type = '%s' and 
                 Condenser Type = '%s' is not supported.", Name, Type, CndsrType)
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      "Centrifugal"
    else u:Type
    endif
  ANNUAL : S901G ECBC
    if( IsBaseSys ) then
      if( CapRtd/12000 < 600 ) then
        "Screw"
      else "Centrifugal"
      endif
    else z:Type
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( CapRtd/12000 < 300 ) then
        "Screw"
      else "Centrifugal"
      endif
    else z:Type
    endif
ENDRULE


// ********** <Chiller Fuel> ************************************
RULE Chlr:FuelSrc
  DESCRIPTION
    "The form of the primary energy input to the chiller"
  HELP
    "All chillers will have a primary energy input along with electricity
    use for auxiliaries.  This input decribes the form of the primary energy.
    Options Electricity, Steam, Hot Water, Natural Gas, or Oil."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Required
  OPTION
    Electric
;    Steam
;    HotWater
;    Gas
;    Oil
    Indirect
  DEFAULT
    if( Type = "AbsorptionSingleEffect" ) then
      "Indirect"
    else "Electric"
    endif
  CHECKSIM
    if( ( Type = "AbsorptionSingleEffect" .AND. FuelSrc <> "Indirect" ) .OR. 
        ( Type <> "AbsorptionSingleEffect" .AND. FuelSrc = "Indirect" ) ) then
      PostError("For chiller '%s', the combination of chiller Type = '%s' and 
                 Fuel Source = '%s' is not supported.", Name, Type, FuelSrc)
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      "Electric"
    else u:FuelSrc
    endif
  ANNUAL
    if( IsBaseSys ) then
      "Electric"
    else z:FuelSrc
    endif
ENDRULE


// ********** <Chiller Integrated Part Load Value - kW/ton Units> **************
RULE Chlr:IPLVKwPerTon
  DESCRIPTION
    "The IPLV value for water cooled electric chillers in kW/ton."
  HELP
    "A measure of part load performance expressed in kW/ton units."
  REFERENCE
    ACM Section 5.8.2
    Table 110.2-D
  INPUTCLASS
    CondRequired
  MINIMUM
    0.0
  COMMONMINIMUM
    0.2
  COMMONMAXIMUM
    0.8
  MAXIMUM
    2.0
  UNITS
    kW per ton
  DEFAULT
    if( Proj:AutoEffInput = 1 ) then
      if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
        0.400
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE
    if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
      if( LocalStatus(IPLVKwPerTon) < 1 ) then
        PostError( "An IPLV value must be provided for chiller '%s'.",Name )
      else if( IPLVKwPerTon <= 0 ) then
        PostError( "A non-zero IPLV value must be provided for chiller '%s'.",Name )
      else
        UNCHANGED
      endif endif
    else
      UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED               // IPLV is used to determine Path A vs. Path B.  Baseline
                              // is always Path B so the value set here is not used.
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType = "Fluid" ) then
      u:IPLVKwPerTon
    else
      UNDEFINED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED
    else z:IPLVKwPerTon
    endif
ENDRULE


// ********** <Chiller Integrated Part Load Value - EER Units> *****************
RULE Chlr:IPLVEER
  DESCRIPTION
    "The IPLV value for air cooled electric chillers in EER units, Btuh/W."
  HELP
    "A measure of part load performance expressed in EER units, i.e., Btuh/W."
  REFERENCE
    ACM Section 5.8.2
    Table 110.2-D
  INPUTCLASS
    CondRequired
  MINIMUM
    0.0
  COMMONMINIMUM
    15.0
  COMMONMAXIMUM
    50.0
  MAXIMUM
    70.0
  UNITS
    Btu/hr/W
  DEFAULT
    if( Proj:AutoEffInput = 1 ) then
      if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
        12.5
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE
    if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
      if( LocalStatus(IPLVEER) < 1 ) then
        PostError( "An IPLV value must be provided for chiller '%s'.",Name )
      else if( IPLVEER <= 0 ) then
        PostError( "A non-zero IPLV value must be provided for chiller '%s'.",Name )
      else
        UNCHANGED
      endif endif
    else
      UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED               // Baseline is always water cooled
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType != "Fluid" ) then
      u:IPLVEER
    else
      UNDEFINED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED
    else z:IPLVEER
    endif
ENDRULE


// ********** <Chiller Integrated Part Load Value - COP Units> *****************
RULE Chlr:IPLVCOP
  DESCRIPTION
    "The IPLV value for fuel or heat driven chillers in COP units, Btu/Btu."
  HELP
    "A measure of part load performance expressed in COP units, i.e., Btu/Btu."
  REFERENCE
    ACM Section 5.8.2
    Table 110.2-D
  INPUTCLASS
    CondRequired
  MINIMUM
    0.0
  COMMONMINIMUM
    3.0
  COMMONMAXIMUM
    15.0
  MAXIMUM
    30.0
  UNITS
    Btuh/Btuh
  DEFAULT
    if( Proj:AutoEffInput = 1 ) then
      if( FuelSrc != "Electric" ) then
        1.0
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE
    if( FuelSrc != "Electric" ) then
      if( LocalStatus(IPLVCOP) < 1 ) then
        PostWarning( "An IPLV value has not been provided for chiller '%s'.",Name )
      else if( IPLVCOP <= 0 ) then
        PostWarning( "An IPLV value of zero has been provided for chiller '%s'.",Name )
      else
        UNCHANGED
      endif endif
    else
      UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED               // Baseline is always water cooled
    else if( u:FuelSrc != "Electric" ) then
      u:IPLVCOP
    else
      UNDEFINED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      UNDEFINED
    else z:IPLVCOP
    endif
ENDRULE


// ********** <Chiller kW/ton> ************************************
RULE Chlr:KwPerTon
  DESCRIPTION
    "The efficiency of water cooled electric chillers in kW/ton."
  HELP
    "The electric energy required by the chiller per ton of cooling produced
    at rating conditions."
  REFERENCE
    ACM Section 5.8.2
    Table 110.2-D
  INPUTCLASS
    CondRequired
  MINIMUM
    0.0
  COMMONMINIMUM
    0.35
  COMMONMAXIMUM
    0.8
  MAXIMUM
    2.0
  UNITS
    kW per ton
  DEFAULT
    if( Proj:AutoEffInput = 1 ) then
      if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
        0.590
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE : T24N_2013 S901G ECBC
    // mandatory requirements on chiller efficiency, so a maximum
    // values for efficiency and IPLV are required
    if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
      if( LocalStatus(KwPerTon) < 1 ) then
        PostError( "A kW/ton value must be provided for chiller '%s'.",Name )
      else if( KwPerTon = 0 ) then
        PostError( "A non-zero kW/ton value must be provided for chiller '%s'.",Name )
      else if( IfValidAnd(IPLVKwPerTon > 0) ) then   ; missing or invalid IPLVKwPerTon reported elsewhere
        if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd < (12000 * 75) ) ) then
          if( KwPerTon <= 0.800 .AND. IPLVKwPerTon <= 0.600 )
            then UNCHANGED
          else if( KwPerTon <= 0.780 .AND. IPLVKwPerTon <= 0.630 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.800/0.600 or 0.780/0.630 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd < (12000 * 150) ) ) then
          if( KwPerTon <= 0.790 .AND. IPLVKwPerTon <= 0.586 )
            then UNCHANGED
          else if( KwPerTon <= 0.775 .AND. IPLVKwPerTon <= 0.615 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.790/0.586 or 0.775/0.615 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd < (12000 * 300) ) ) then
          if( KwPerTon <= 0.718 .AND. IPLVKwPerTon <= 0.540 )
            then UNCHANGED
          else if( KwPerTon <= 0.680 .AND. IPLVKwPerTon <= 0.580 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.718/0.540 or 0.680/0.580 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd >= (12000 * 300) ) ) then
          if( KwPerTon <= 0.639 .AND. IPLVKwPerTon <= 0.490 )
            then UNCHANGED
          else if( KwPerTon <= 0.620 .AND. IPLVKwPerTon <= 0.540 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.639/0.490 or 0.620/0.540 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd < (12000 * 150) ) ) then
          if( KwPerTon <= 0.639 .AND. IPLVKwPerTon <= 0.450 )
            then UNCHANGED
          else if( KwPerTon <= 0.634 .AND. IPLVKwPerTon <= 0.596 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.639/0.450 or 0.634/0.596 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd < (12000 * 300) ) ) then
          if( KwPerTon <= 0.639 .AND. IPLVKwPerTon <= 0.450 )
            then UNCHANGED
          else if( KwPerTon <= 0.634 .AND. IPLVKwPerTon <= 0.596 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.639/0.450 or 0.634/0.596 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd < (12000 * 600) ) ) then
          if( KwPerTon <= 0.600 .AND. IPLVKwPerTon <= 0.400 )
            then UNCHANGED
          else if( KwPerTon <= 0.576 .AND. IPLVKwPerTon <= 0.549 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.600/0.400 or 0.576/0.549 kW/ton & IPLV",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd >= (12000 * 600) ) ) then
          if( KwPerTon <= 0.590 .AND. IPLVKwPerTon <= 0.400 )
            then UNCHANGED
          else if( KwPerTon <= 0.570 .AND. IPLVKwPerTon <= 0.539 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.590/0.400 or 0.570/0.539 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else UNCHANGED
        endif endif endif endif
        endif endif endif endif
      else UNCHANGED   ; if IPLVKwPerTon is invalid
      endif endif endif
    else UNCHANGED
    endif
  CHECKCODE : T24N_2016
    // mandatory requirements on chiller efficiency, so a maximum
    // values for efficiency and IPLV are required
    if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
      if( LocalStatus(KwPerTon) < 1 ) then
        PostError( "A kW/ton value must be provided for chiller '%s'.",Name )
      else if( KwPerTon = 0 ) then
        PostError( "A non-zero kW/ton value must be provided for chiller '%s'.",Name )
      else if( IfValidAnd(IPLVKwPerTon > 0) ) then   ; missing or invalid IPLVKwPerTon reported elsewhere
        if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd < (12000 * 75) ) ) then
          if( KwPerTon <= 0.780 .AND. IPLVKwPerTon <= 0.500 )
            then UNCHANGED
          else if( KwPerTon <= 0.750 .AND. IPLVKwPerTon <= 0.600 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.780/0.500 or 0.750/0.600 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd < (12000 * 150) ) ) then
          if( KwPerTon <= 0.750 .AND. IPLVKwPerTon <= 0.490 )
            then UNCHANGED
          else if( KwPerTon <= 0.720 .AND. IPLVKwPerTon <= 0.560 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.750/0.490 or 0.720/0.560 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd < (12000 * 300) ) ) then
          if( KwPerTon <= 0.680 .AND. IPLVKwPerTon <= 0.440 )
            then UNCHANGED
          else if( KwPerTon <= 0.660 .AND. IPLVKwPerTon <= 0.540 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.680/0.440 or 0.660/0.540 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd < (12000 * 600) ) ) then
          if( KwPerTon <= 0.625 .AND. IPLVKwPerTon <= 0.410 )
            then UNCHANGED
          else if( KwPerTon <= 0.610 .AND. IPLVKwPerTon <= 0.520 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.625/0.410 or 0.610/0.520 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( (Type = "Reciprocating" .OR. Type = "Scroll" .OR.
                 Type = "Screw") .AND. IfValidAnd( CapRtd >= (12000 * 600) ) ) then
          if( KwPerTon <= 0.625585 .AND. IPLVKwPerTon <= 0.380 )
            then UNCHANGED
          else if( KwPerTon <= 0.560 .AND. IPLVKwPerTon <= 0.500 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.585/0.380 or 0.560/0.500 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd < (12000 * 150) ) ) then
          if( KwPerTon <= 0.695 .AND. IPLVKwPerTon <= 0.440 )
            then UNCHANGED
          else if( KwPerTon <= 0.610 .AND. IPLVKwPerTon <= 0.550 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.695/0.440 or 0.610/0.550 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd < (12000 * 300) ) ) then
          if( KwPerTon <= 0.635 .AND. IPLVKwPerTon <= 0.400 )
            then UNCHANGED
          else if( KwPerTon <= 0.610 .AND. IPLVKwPerTon <= 0.550 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.635/0.400 or 0.610/0.550 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd < (12000 * 400) ) ) then
          if( KwPerTon <= 0.595 .AND. IPLVKwPerTon <= 0.390 )
            then UNCHANGED
          else if( KwPerTon <= 0.560 .AND. IPLVKwPerTon <= 0.520 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.595/0.390 or 0.560/0.520 kW/ton & IPLV",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd < (12000 * 600) ) ) then
          if( KwPerTon <= 0.585 .AND. IPLVKwPerTon <= 0.380 )
            then UNCHANGED
          else if( KwPerTon <= 0.560 .AND. IPLVKwPerTon <= 0.500 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.585/0.380 or 0.560/0.500 kW/ton & IPLV",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd >= (12000 * 600) ) ) then
          if( KwPerTon <= 0.585 .AND. IPLVKwPerTon <= 0.380 )
            then UNCHANGED
          else if( KwPerTon <= 0.560 .AND. IPLVKwPerTon <= 0.500 )
            then UNCHANGED
          else PostError( "%.3f kW/ton or %.3f IPLV exceed the code maximums
                          of 0.585/0.390 or 0.560/0.500 kW/ton & IPLV for
                          chiller '%s'.",
                          KwPerTon,IPLVKwPerTon,Name )
          endif endif
        else UNCHANGED
        endif endif endif endif endif
        endif endif endif endif endif
      else UNCHANGED   ; if IPLVKwPerTon is invalid
      endif endif endif
    else UNCHANGED
    endif
  SIZING : T24N_2013 S901G ECBC
    if( IsBaseSys ) then
      0.639
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType = "Fluid" ) then
      u:KwPerTon
    else
      UNCHANGED
    endif endif
  SIZING : T24N_2016
    if( IsBaseSys ) then
      0.695
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType = "Fluid" ) then
      u:KwPerTon
    else
      UNCHANGED
    endif endif
  ANNUAL : T24N_2013 S901G ECBC
    if( IsBaseSys ) then
      if( Type = "Screw" .AND. CapRtd/12000 < 75 ) then 0.800
      else if( Type = "Screw" .AND. CapRtd/12000 < 150 ) then 0.790
      else if( Type = "Screw" .AND. CapRtd/12000 < 300 ) then 0.718
      else if( Type = "Screw" .AND. CapRtd/12000 >= 300 ) then 0.639
      else 0.590    // centrifugal if > 600 tons
      endif endif endif endif
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType = "Fluid" ) then
      z:KwPerTon
    else
      UNCHANGED
    endif endif
  ANNUAL : T24N_2016
    if( IsBaseSys ) then
      if( Type = "Screw" .AND. CapRtd/12000 < 75 ) then 0.780
      else if( Type = "Screw" .AND. CapRtd/12000 < 150 ) then 0.750
      else if( Type = "Screw" .AND. CapRtd/12000 < 300 ) then 0.680
      else if( Type = "Screw" .AND. CapRtd/12000 >= 300 ) then 0.625
      else 0.585    // centrifugal if > 600 tons
      endif endif endif endif
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType = "Fluid" ) then
      z:KwPerTon
    else
      UNCHANGED
    endif endif
ENDRULE


// ********** <Chiller EER> ************************************
RULE Chlr:EER
  DESCRIPTION
    "The efficiency of air cooled electric chillers using EER."
  HELP
    "The cooling provided by an air cooled electric chiller in Btu/hr
    divided by the electricity consumption in Watts at rating conditions."
  REFERENCE
    ACM Section 5.8.2
    Table 110.2-D
  INPUTCLASS
    CondRequired
  MINIMUM
    0.0
  COMMONMINIMUM
    15.0
  COMMONMAXIMUM
    35.0
  MAXIMUM
    50.0
  UNITS
    Btu/hr/W
  DEFAULT
    if( Proj:AutoEffInput = 1 ) then
      if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
        9.5
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE : T24N_2013 S901G ECBC
    // mandatory requirements on chiller efficiency, so minimum
    // values for efficiency and IPLV are required
    if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
      if( LocalStatus(EER) < 1 ) then
        PostError( "An EER value must be provided for chiller '%s'.",Name )
      else if( EER = 0 ) then
        PostError( "A non-zero EER value must be provided for chiller '%s'.",Name )
      else
        if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" .AND.
            IfValidAnd( CapRtd < (12000*150) ) ) then    // CapRtd/12000 < 150 ) then
          if( EER >= 9.562 .AND. IPLVEER >= 12.500 )
            then UNCHANGED
          else PostError( "%.3f EER or %.3f IPLV are less than the code minimums
                          of 9.562 EER and 12.500 IPLV for chiller '%s'.",
                          EER,IPLVEER,Name )
          endif
        else if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" .AND.
                 IfValidAnd( CapRtd >= (12000*150) ) ) then    // CapRtd/12000 >= 150 ) then
          if( EER >= 9.562 .AND. IPLVEER >= 12.750 )
            then UNCHANGED
          else PostError( "%.3f EER or %.3f IPLV are less than the code minimums
                          of 9.562 EER and 12.750 IPLV for chiller '%s'.",
                          EER,IPLVEER,Name )
          endif
        else
          UNCHANGED
        endif endif
      endif endif
    else UNCHANGED
    endif
  CHECKCODE : T24N_2016
    // mandatory requirements on chiller efficiency, so minimum
    // values for efficiency and IPLV are required
    if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
      if( LocalStatus(EER) < 1 ) then
        PostError( "An EER value must be provided for chiller '%s'.",Name )
      else if( EER = 0 ) then
        PostError( "A non-zero EER value must be provided for chiller '%s'.",Name )
      else if( IfValidAnd(IPLVEER > 0) ) then   ; missing or invalid IPLVEER reported elsewhere
        if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" .AND.
            IfValidAnd( CapRtd < (12000*150) ) ) then    // CapRtd/12000 < 150 ) then
          if( EER >= 10.100 .AND. IPLVEER >= 13.700 )
            then UNCHANGED
          else if( EER >= 9.700 .AND. IPLVEER >= 15.800 )
            then UNCHANGED
          else PostError( "%.3f EER or %.3f IPLV are less than the code minimums
                          of 10.1/13.7 or 9.7/15.8 EER & IPLV for
                          chiller '%s'.",
                          EER,IPLVEER,Name )
          endif endif
        else if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" .AND.
                 IfValidAnd( CapRtd >= (12000*150) ) ) then    // CapRtd/12000 >= 150 ) then
          if( EER >= 10.100 .AND. IPLVEER >= 14.000 )
            then UNCHANGED
          else if( EER >= 9.700 .AND. IPLVEER >= 16.100 )
            then UNCHANGED
          else PostError( "%.3f EER or %.3f IPLV are less than the code minimums
                          of 10.1/14.0 or 9.7/16.1 EER & IPLV for
                          chiller '%s'.",
                          EER,IPLVEER,Name )
          endif endif
        else
          UNCHANGED
        endif endif
      else UNCHANGED
      endif endif endif
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      UNCHANGED
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType != "Fluid") then
      u:EER
    else
      UNCHANGED
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      UNCHANGED
    else z:EER
    endif
ENDRULE


// ********** <Chiller COP> ************************************
RULE Chlr:COP
  DESCRIPTION
    "The efficiency of heat driven and air cooled electric chillers
    expressed as COP."
  HELP
    "The efficiency of heat driven and air cooled electric chillers
    expressed as the ratio of the output cooling energy to the energy
    consumed by the chiller at rating conditions, using common units."
  REFERENCE
    ACM Section 5.8.2
    Table 110.2-D
  INPUTCLASS
    CondRequired
  MINIMUM
    0.0
  COMMONMINIMUM
    0.5
  COMMONMAXIMUM
    10.0
  MAXIMUM
    20.0
  UNITS
    Btuh/Btuh
  REPORTPRECISION
    2
  DEFAULT
  DEFAULT
    if( Proj:AutoEffInput = 1 ) then
      if( FuelSrc != "Electric" ) then
        0.700
      else UNDEFINED
      endif
    else if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" .AND.
             IfValidAnd( KwPerTon > 0 ) ) then
      12/KwPerTon/3.412
    else if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" .AND.
             IfValidAnd( EER > 0 ) ) then
      EER/3.412
    else UNDEFINED
    endif endif endif
  CHECKCODE
    // mandatory requirements on chiller efficiency, so a minimum
    // value is required
    if( FuelSrc != "Electric" ) then
      if( LocalStatus(COP) < 1 ) then
        PostError( "A COP value must be provided for chiller '%s'.",Name )
      else if( COP = 0 ) then
        PostError( "A non-zero COP value must be provided for chiller '%s'.",Name )
      else
        if( Type = "AbsorptionSingleEffect" .AND. CndsrType = "Fluid" )
          then
          if( COP >= 0.700 - 0.001 ) then
            UNCHANGED
          else
            PostError( "COP of %.3f is less than the code minimum of 0.700 for
                       chiller '%s'.",COP,Name )
          endif
//        else if( Type = "AbsorptionSingleEffect" .AND. CndsrType = "Air" ) then
//          if( COP >= 0.600 - 0.001 ) then
//            UNCHANGED
//          else
//            PostError( "COP of %.3f is less than the code minimum of 0.600 for
//                       chiller '%s'.",COP,Name )
//          endif
//        else if( Type = "AbsorptionDoubleEffect" ) then
//          if( COP >= 1.000 .AND. IPLVCOP >= 1.000 ) then
//            UNCHANGED
//          else
//            PostError( "%.3f COP or %.3f IPLV are less than the code minimums
//                       of 1.000 COP and 1.000 IPLV for chiller '%s'.",
//                       COP,IPLVCOP,Name )
//          endif
//        else if( Type = "IndirectFiredDoubleEffectAbsorption" ) then
//          if( COP >= 1.000 .AND. IPLVCOP >= 1.050 ) then
//            UNCHANGED
//          else
//            PostError( "%.3f COP or %.3f IPLV are less than the code minimums
//                       of 1.000 COP and 1.050 IPLV for chiller '%s'.",
//                       COP,IPLVCOP,Name )
//          endif
//        else if( Type = "GasEngineDriven" ) then
//          if( COP >= 1.200 .AND. IPLVCOP >= 2.000 ) then
//            UNCHANGED
//          else
//            PostError( "%.3f COP or %.3f IPLV are less than the code minimums
//                       of 1.200 COP and 2.000 IPLV for chiller '%s'.",
//                       COP,IPLVCOP,Name  )
//          endif
        else UNCHANGED
//        endif endif endif endif
        endif 
      endif endif
    else                             // FuelType = Electric
      if( LocalStatus(COP) = 7 ) then
        if( COP = 0 ) then
          PostError( "Electric chiller '%s' has a COP of 0.  Please remove the 
                      COP input from the input file.",Name )
        else UNCHANGED
        endif
      else UNCHANGED
      endif
    endif
  SIZING
    if( IsBaseSys ) then
      12/KwPerTon/3.412
    else if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
      12/KwPerTon/3.412
    else if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
      EER/3.412
    else u:COP
    endif endif endif
  ANNUAL
    if( IsBaseSys ) then
      12/KwPerTon/3.412
    else if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
      12/KwPerTon/3.412
    else if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
      EER/3.412
    else u:COP
    endif endif endif
ENDRULE


// ********** <Chiller Path A or Path B> ************************************
RULE NEW Chlr:EffPath
  DATATYPE
    String
  DESCRIPTION
    "Based on the chiller efficiency and IPLV, the chiller is classed as
    Path A or Path B.  The Path determines which set of performance curves
    are assigned."
  HELP
    "Based on the chiller efficiency and IPLV, the chiller is classed as
    Path A or Path B.  The Path determines which set of performance curves
    are assigned.  Only applies to water-cooled electric chillers, other
    types are always Path A."
  REFERENCE
    ACM Section 5.8.2
  LONGFORM
    EfficiencyPath
  INPUTCLASS
    NotInput
  SIZING : T24N_2016
    if( IsBaseSys ) then
      "B"
    // mandatory requirements on chiller efficiency, so a maximum value is required
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType = "Fluid" ) then
      if( LocalStatus(u:IPLVKwPerTon) < 1 )
        then PostError( "Can't set Chlr:EffPath when IPLVKwPerTon not defined" )
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
           u:Type = "Screw") .AND. IfValidAnd( u:CapRtd < (12000 * 75) ) ) then
        if( u:IPLVKwPerTon <= 0.500 ) then "B"
        else if( u:IPLVKwPerTon <= 0.600 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
                u:Type = "Screw") .AND. IfValidAnd( u:CapRtd < (12000 * 150) ) ) then
        if( u:IPLVKwPerTon <= 0.490 ) then "B"
        else if( u:IPLVKwPerTon <= 0.560 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
                u:Type = "Screw") .AND. IfValidAnd( u:CapRtd < (12000 * 300) ) ) then
        if( u:IPLVKwPerTon <= 0.440 ) then "B"
        else if( u:IPLVKwPerTon <= 0.540 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
                u:Type = "Screw") .AND. IfValidAnd( u:CapRtd < (12000 * 600) ) ) then
        if( u:IPLVKwPerTon <= 0.410 ) then "B"
        else if( u:IPLVKwPerTon <= 0.520 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
                u:Type = "Screw") .AND. IfValidAnd( u:CapRtd >= (12000 * 600) ) ) then
        if( u:IPLVKwPerTon <= 0.380 ) then "B"
        else if( u:IPLVKwPerTon <= 0.500 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd < (12000 * 150) ) ) then
        if( u:IPLVKwPerTon <= 0.440 ) then "B"
        else if( u:IPLVKwPerTon <= 0.550 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd < (12000 * 300) ) ) then
        if( u:IPLVKwPerTon <= 0.400 ) then "B"
        else if( u:IPLVKwPerTon <= 0.550 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd < (12000 * 400) ) ) then
        if( u:IPLVKwPerTon <= 0.390 ) then "B"
        else if( u:IPLVKwPerTon <= 0.520 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd < (12000 * 600) ) ) then
        if( u:IPLVKwPerTon <= 0.380 ) then "B"
        else if( u:IPLVKwPerTon <= 0.500 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd >= (12000 * 600) ) ) then
        if( u:IPLVKwPerTon <= 0.380 ) then "B"
        else if( u:IPLVKwPerTon <= 0.500 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else
        "A"
      endif endif endif endif endif
      endif endif endif endif endif endif
    else "A"
    endif endif
  SIZING
    if( IsBaseSys ) then
      "B"
    // mandatory requirements on chiller efficiency, so a maximum value is required
    else if( u:FuelSrc = "Electric" .AND. u:CndsrType = "Fluid" ) then
      if( LocalStatus(u:IPLVKwPerTon) < 1 )
        then PostError( "Can't set Chlr:EffPath when IPLVKwPerTon not defined" )
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
           u:Type = "Screw") .AND. IfValidAnd( u:CapRtd < (12000 * 75) ) ) then
        if( u:IPLVKwPerTon <= 0.600 ) then "B"
        else if( u:IPLVKwPerTon <= 0.630 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
                u:Type = "Screw") .AND. IfValidAnd( u:CapRtd < (12000 * 150) ) ) then
        if( u:IPLVKwPerTon <= 0.586 ) then "B"
        else if( u:IPLVKwPerTon <= 0.615 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
                u:Type = "Screw") .AND. IfValidAnd( u:CapRtd < (12000 * 300) ) ) then
        if( u:IPLVKwPerTon <= 0.540 ) then "B"
        else if( u:IPLVKwPerTon <= 0.580 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( (u:Type = "Reciprocating" .OR. u:Type = "Scroll" .OR.
                u:Type = "Screw") .AND. IfValidAnd( u:CapRtd >= (12000 * 300) ) ) then
        if( u:IPLVKwPerTon <= 0.490 ) then "B"
        else if( u:IPLVKwPerTon <= 0.540 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd < (12000 * 150) ) ) then
        if( u:IPLVKwPerTon <= 0.450 ) then "B"
        else if( u:IPLVKwPerTon <= 0.596 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd < (12000 * 300) ) ) then
        if( u:IPLVKwPerTon <= 0.450 ) then "B"
        else if( u:IPLVKwPerTon <= 0.596 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd < (12000 * 600) ) ) then
        if( u:IPLVKwPerTon <= 0.400 ) then "B"
        else if( u:IPLVKwPerTon <= 0.549 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd >= (12000 * 600) ) ) then
        if( u:IPLVKwPerTon <= 0.400 ) then "B"
        else if( u:IPLVKwPerTon <= 0.539 ) then "A"
        else PostError( "Illegal IPLV Value for Chiller '%s'",Name )
        endif endif
      else
        "A"
      endif endif endif endif
      endif endif endif endif endif
    else "A"
    endif endif
  ANNUAL
    if( IsBaseSys ) then
      "B"
    else z:EffPath
    endif
ENDRULE


// -----------------------------------------------------------------------------
RULE Chlr:EffRpt
  DESCRIPTION
    "A text string describing chiller efficiency for compliance reporting."
  INPUTCLASS
    NotInput
  ANNUAL_PROPOSED
    if( FuelSrc = "Electric" .AND. CndsrType = "Fluid" ) then
      Format( "kW/ton: %s", FltToStr(KwPerTon,3,0,5) )
    else if( FuelSrc = "Electric" .AND. CndsrType != "Fluid" ) then
      Format( "EER: %s", FltToStr(EER,1,0,4) )
    else
      Format( "COP: %s", FltToStr(COP,2,0,4) )
    endif endif
  ANNUAL_BASELINE
    UNDEFINED
ENDRULE


// ********** <Chiller Minimum Unloading Ratio> ************************************
RULE Chlr:UnldRatMin
//      Chiller Minimum Unloading Ratio
  DESCRIPTION
    "The lowest load at which the chiller can run continuously."
  HELP
    "The lowest load, as a fraction of design capacity, at which the chiller
    can operate continously.  At loads below this ratio, the chiller must
    cycle."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Default
  REPORTPRECISION
    2
  DEFAULT : S901G ECBC
    if( Type = "Centrifugal" )
      then 0.10
    else if( Type = "Reciprocating" )
      then 0.25
    else if( Type = "Scroll" )
      then 0.25
    else if( Type = "Screw" )
      then 0.15
    else 0.10
    endif endif endif endif
  DEFAULT
    if( Type = "Centrifugal" )
      then 0.10
    else 0.15
    endif
  SIZING
    if( IsBaseSys ) then
      0.15
    else u:UnldRatMin
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( Type = "Centrifugal" )
        then 0.10
      else 0.15
      endif
    else z:UnldRatMin
    endif
ENDRULE


// ********** <Chiller Minimum Unloading Ratio> ************************************
RULE Chlr:PLRMin
//      Chiller Minimum Unloading Ratio
  DESCRIPTION
    "The lowest load at which the chiller can run continuously."
  HELP
    "The lowest load, as a fraction of design capacity, at which the chiller
    can operate continously.  At loads below this ratio, the chiller must
    cycle."
  REFERENCE
    ACM Section 5.8.2
  PREVIOUSNAMES
    PartLdRatMin
  INPUTCLASS
    Default
  REPORTPRECISION
    2
  DEFAULT
    if( Type = "Centrifugal" )
      then 0.10
    else 0.15
    endif
  CHECKSIM
    if( IfValidAnd( PLRMin > UnldRatMin ) ) then
      PostError( "The minimum part load ratio '%.3f' for chiller '%s' exceeds
                  the minimum unloading ratio '%.3f'.  This is not allowed.", 
                  PLRMin, Name, UnldRatMin )
    else UNCHANGED
    endif
  SIZING
    if( IsBaseSys ) then
      0.15
    else u:PLRMin
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( Type = "Centrifugal" )
        then 0.10
      else 0.15
      endif
    else z:PLRMin
    endif
ENDRULE


// ********** <Chiller Water Supply Temperature> *******************************
RULE Chlr:LvgTempDsgn
//      Chilled Water Supply Temperature
  DESCRIPTION
    "The chilled water supply temperature at design conditions."
  HELP
    "The chilled water supply temperature at design conditions.  This
    temperature is used to size the chilled water components of the
    system. "
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Default
  MINIMUM
    35
  COMMONMINIMUM
    42
  COMMONMAXIMUM
    50
  MAXIMUM
    70
  UNITS
    F
  REPORTPRECISION
    0
  DEFAULT
    44
  SIZING
    if( IsBaseSys ) then
      44
    else u:LvgTempDsgn
    endif
  ANNUAL
    if( IsBaseSys ) then
      44
    else z:LvgTempDsgn
    endif
ENDRULE


// ********** <Chiller Water Return Temperature> *******************************
RULE Chlr:EntTempDsgn
//      Chilled Water Return Temperature
  DESCRIPTION
    "The chilled water return temperature at design conditions."
  HELP
    "The chilled water return temperature at design conditions.  This
    temperature is used to size the chilled water components of the
    system. "
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Default
  MINIMUM
    35
  COMMONMINIMUM
    45
  COMMONMAXIMUM
    70
  MAXIMUM
    85
  UNITS
    F
  REPORTPRECISION
    0
  DEFAULT
    64
  SIZING : S901G ECBC
    if( IsBaseSys ) then
      56
    else u:EntTempDsgn
    endif
  SIZING
    if( IsBaseSys ) then
      64
    else u:EntTempDsgn
    endif
  ANNUAL : S901G ECBC
    if( IsBaseSys ) then
      56
    else z:EntTempDsgn
    endif
  ANNUAL
    if( IsBaseSys ) then
      64
    else z:EntTempDsgn
    endif
ENDRULE

// -------------- Chiller Chilled Water Flow -----------------------------------
RULE Chlr:WtrFlowCap
  DESCRIPTION
    "The chilled water flow rate."
  HELP
    ""
  INPUTCLASS
    NotInput
  MINIMUM
    0
  UNITS
    gpm
  REPORTPRECISION
    1
  DEFAULT
    if( IfValidAnd( CapRtd > 0 ) .AND.
        IfValidAnd( ParentValid( DsgnSupWtrDelT ) > 0 ) ) then
    // gpm = Btuh / 8.3365 (lb/gal) / 60 (min/hr) / Design loop dT
      CapRtd/500.19/FluidSys:DsgnSupWtrDelT
    else 0
    endif
  SIZING
    if( IsBaseSys ) then
      UNDEFINED // AutoSize
    else u:WtrFlowCap
    endif
  ANNUAL
    if( IsBaseSys ) then
      CapRtd/500.19/FluidSys:DsgnSupWtrDelT
    else z:WtrFlowCap
    endif
ENDRULE

// -------------- Absorption Chiller Auxiliary Power ---------------------------
RULE Chlr:AuxPwr
  DESCRIPTION
    "The electric power requirement for absorption chillers."
  HELP
    ""
  INPUTCLASS
    Default
  MINIMUM
    0
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "AbsorptionSingleEffect" ) then
      0.01 * CapRtd/12000*1000    // 0.01 kW/ton
    else
      UNDEFINED
    endif
  CHECKCODE
    if( Type = "AbsorptionSingleEffect" ) then
      if( AuxPwr < 0.001 * CapRtd/12000*1000 ) then
        PostError( "The auxiliary power input for chiller '%s' is too low.
                    The input power must be increased.",Name )
      else if( AuxPwr > 0.05 * CapRtd/12000*1000 ) then
        PostWarning( "The auxiliary power input for chiller '%s' is higher 
                      than expected.  Check that your inputs are correct.",Name )
      else
        UNCHANGED
      endif endif  
    else
      UNCHANGED
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Amount of heat rejected from the chiller
RULE NEW Chlr:HtRejLd
  DATATYPE
    Float
  LONGFORM
    HeatRejectionLoad
  DESCRIPTION
    "The total amount of heat that needs to be dissipated by the chiller 
     condenser."
  INPUTCLASS
    NotInput
  UNITS
    Btu/h
  DEFAULT
    if(LocalStatus( CapRtd ) = 0 ) 
    then UNDEFINED
    else if( IfValidAnd( COP > 0 ) = 0 ) 
    then
      if( IfValidAnd ( EER > 0 ) = 0 ) 
      then
        if( IfValidAnd(KwPerTon > 0) = 0 )
        then -1 // Flag that indicates efficiency has not been defined
        else CapRtd + (CapRtd/12000*KwPerTon*3412)
        endif
      else CapRtd + (CapRtd/EER*3412)
      endif
    else CapRtd + (CapRtd/COP)
    endif endif
  SIZING
    if( IsBaseSys ) then
      if( IfValidAnd( CapRtd > 0 ) .AND. IfValidAnd( COP > 0 ) ) then
        CapRtd + (CapRtd/COP)
      else UNDEFINED
      endif
    else u:HtRejLd
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( IfValidAnd( CapRtd > 0 ) .AND. IfValidAnd( COP > 0 ) ) then
        CapRtd + (CapRtd/COP)
      else UNDEFINED
      endif
    else z:HtRejLd
    endif
ENDRULE

// -----------------------------------------------------------------------------
// Calculate total heat rejection load from all chillers attached to the condenser return
RULE NEW FluidSys:FluidSeg:TotHtRejFromChlr
  DATATYPE
    Float
  LONGFORM
    TotalHeatRejectionFromChiller
  DESCRIPTION
    "The total amount of chiller heat that needs to be dissapated by the heat
     rejection equipment."
  INPUTCLASS
    NotInput
  UNITS
    Btu/h
  DEFAULT
    if( Type = "PrimarySupply" .AND. FluidSys:Type = "CondenserWater" ) then
      SumRevRef(Chlr:CndsrFluidSegInRef, Chlr:HtRejLd)
    else 0
    endif
  CHECKSIM
    if( FluidSys:Type = "CondenserWater" .AND.
        Type = "PrimarySupply" .AND. 
        TotHtRejFromChlr = 0 .AND.
        CoilClgTotCap = 0 ) 
      then PostWarning("FluidSystem '%s' is Type = 'CondenserWater' but has no
                        chillers or water-source coils referenced by primary
                        segment '%s' (or its secondary segments).", 
                        FluidSys:Name, Name)
    else UNCHANGED
    endif
  SIZING
    if( FluidSys:IsBaseSys ) then
      if( Type = "PrimarySupply" .AND. FluidSys:Type = "CondenserWater" ) then
        SumRevRef(Chlr:CndsrFluidSegInRef, Chlr:HtRejLd)
      else UNDEFINED
      endif
    else u:TotHtRejFromChlr
    endif
  ANNUAL
    if( z:FluidSys:IsBaseSys ) then
      if( Type = "PrimarySupply" .AND. FluidSys:Type = "CondenserWater" ) then
        SumRevRef(Chlr:CndsrFluidSegInRef, Chlr:HtRejLd)
      else UNDEFINED
      endif
    else z:TotHtRejFromChlr
    endif
ENDRULE


// -------------- Chiller Chilled Water Flow -----------------------------------
RULE Chlr:CndsrFlowCap
  DESCRIPTION
    "The condenser water flow rate."
  HELP
    ""
  INPUTCLASS
    NotInput
  MINIMUM
    0
  UNITS
    gpm
  REPORTPRECISION
    1
  DEFAULT
    if( CndsrType = "Fluid" ) then
      if( LocalStatus(CndsrFlowCap) < 5 .AND. Proj:AutoHardSize = 1 ) then
    // For PROPOSED AutoHardSizing only
    // gpm = Btuh / 8.3365 (lb/gal) / 60 (min/hr) / Design loop dT
        HtRejLd/500.19/CndsrFluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  SIZING
    if( CndsrType = "Fluid" ) then
      if( IsBaseSys ) then
        UNDEFINED // AutoSize
      else
        HtRejLd/500.19/CndsrFluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT
      endif
    else UNDEFINED
    endif
  ANNUAL
    if( CndsrType = "Fluid" ) then
      if( IsBaseSys ) then
        HtRejLd/500.19/CndsrFluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT
      else z:CndsrFlowCap
      endif
    else UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
//
RULE NEW Chlr:TotHtInput
  DATATYPE
    Float
  LONGFORM
    TotalHeatInput
  DESCRIPTION
    "The total amount of heat needed by an absorption chiller from the hot 
     water fluid system."
  INPUTCLASS
    NotInput
  UNITS
    Btu/h
  DEFAULT
    if( Type = "AbsorptionSingleEffect" ) then
      if( IfValidAnd( CapRtd > 0 ) .AND. IfValidAnd( COP > 0 ) ) then
        CapRtd / COP - (AuxPwr * 3.412)
      else UNDEFINED
      endif
    else UNDEFINED
    endif
ENDRULE

// -----------------------------------------------------------------------------
//      <Absorption Chiller Generator Fluid Entering Temperature> 
RULE Chlr:EntGenTempDsgn
  DESCRIPTION
    "The generator fluid entering temperature at design conditions."
  HELP
    "The temperature of the fluid used to provide the heat input to an 
     absorption chiller."
  INPUTCLASS
    Default
  MINIMUM
    140
  COMMONMINIMUM
    180
  COMMONMAXIMUM
    210
  MAXIMUM
    300
  UNITS
    F
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "AbsorptionSingleEffect" ) then
      if( LocalCompAssigned( GenFluidSegInRef ) = 0 ) then
        190
      else
        GenFluidSegInRef:ParentFluidSysRef:DsgnSupWtrTemp
      endif
    else
      UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
//      <Absorption Chiller Generator Fluid Temperature Difference> 
RULE Chlr:DsgnGenFluidDelT
  DESCRIPTION
    "The generator fluid temperature difference across the chiller at design 
     conditions."
  HELP
    "The delta T across the chiller of the fluid used to provide the heat 
     input to an absorption chiller."
  INPUTCLASS
    Default
  MINIMUM
    1
  COMMONMINIMUM
    10
  COMMONMAXIMUM
    60
  MAXIMUM
    100
  UNITS
    F
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "AbsorptionSingleEffect" ) then
      if( LocalCompAssigned( GenFluidSegInRef ) = 0 ) then
        40
      else
        GenFluidSegInRef:ParentFluidSysRef:DsgnSupWtrDelT
      endif
    else
      UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
//      <Absorption Chiller Generator Fluid Flow Rate> 
RULE Chlr:GenFluidFlowCap
  DESCRIPTION
    "The generator fluid flow rate at design conditions."
  HELP
    ""
  INPUTCLASS
    Default
  MINIMUM
    1
  UNITS
    gpm
  REPORTPRECISION
    0
  DEFAULT
    if( Type = "AbsorptionSingleEffect" .AND.
        IfValidAnd( DsgnGenFluidDelT > 0 ) .AND. 
        IfValidAnd( TotHtInput > 0 ) ) 
    then
      TotHtInput/8.0/DsgnGenFluidDelT/60    //  density of water = 8.0 lb/gal at 212F 
    else 
      UNDEFINED
    endif
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW Chlr:CapRtdNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( CapRtd > 0 ) ) then CapRtd * IsNew else 0 endif
  SIZING
    UNDEFINED
  ANNUAL
    if( IfValidAnd( CapRtd > 0 ) ) then CapRtd * IsNew else 0 endif
ENDRULE


// -----------------------------------------------------------------------------
RULE NEW Chlr:CapRtdExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IfValidAnd( CapRtd > 0 ) ) then CapRtd * IsExisting else 0 endif
  SIZING
    UNDEFINED
  ANNUAL
    if( IfValidAnd( CapRtd > 0 ) ) then CapRtd * IsExisting else 0 endif
ENDRULE


// ********** <Chiller Capacity Curve Name> ************************************
RULE Chlr:Cap_fTempCrvRef
//      Chiller Cooling Capacity Adjustment Curve
  DESCRIPTION
    "The name of the performance curve which describes the chiller cooling
    capacity as a function of operating temperatures."
  HELP
    "The name of the performance curve which describes the chiller cooling
    capacity as a function of operating temperatures.  For water cooled
    chillers the temperatures are the leaving chilled water and entering
    condenser water temperature.  For air cooled chillers the temperatures
    are the leaving chilled water temperature and the outdoor air drybulb. "
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
// TO DO: Move PostError checks to CHECKSIM
  SIZING
    if( IsBaseSys ) then
      RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql600tonQRatio_fTchwsTcwsSI")
    //   FUEL and HEAT Driven
    else if( Type = "AbsorptionSingleEffect" ) then
      UNDEFINED
    //    AIR COOLED ELECTRIC
    else if( u:CndsrType = "Air" ) then
      if( u:Type = "Scroll" ) then
        RuleLibrary(CrvDblQuad,"ChlrAirScrollQRatio_fTchwsToadbSI")
      else if( u:Type = "Reciprocating" ) then
        RuleLibrary(CrvDblQuad,"ChlrAirRecipQRatio_fTchwsToadbSI")
      else if( u:Type = "Screw" ) then
        RuleLibrary(CrvDblQuad,"ChlrAirScrewQRatio_fTchwsToadbSI")
// CndsrType = "Air" and Type = "Centrifugal" is not currently a valid combination
      else if( u:Type = "Centrifugal" ) then
;        RuleLibrary(CrvDblQuad,"ChlrAirCentQRatio_fTchwsToadbSI")
        PostError( "Centrifugal Air Chiller '%s' not currently supported.", Name )
      else
        PostError( "Air Chiller '%s' not Scroll, Recip, or Screw.", Name )
      endif endif endif endif
    //    WATER COOLED ELECTRIC
    else if( u:CndsrType = "Fluid" ) then
      //  PATH A
      if( Chlr:EffPath = "A" ) then
        if( u:Type = "Scroll" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathAAllQRatio_fTchwsTcwsSI")
        else if( u:Type = "Reciprocating" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathAAllQRatio_fTchwsTcwsSI")
        else if( u:Type = "Screw" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathAAllQRatio_fTchwsTcwsSI")
        else if( u:Type = "Centrifugal" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrCentPathAAllQRatio_fTchwsTcwsSI")
        else
          PostError( "Path A Chlr '%s' not Scroll, Recip, Screw or Centrifugal.",
                      Name )
        endif endif endif endif
      //  PATH B
      else if( Chlr:EffPath = "B" ) then
        //  POSITIVE DISPLACEMENT
        if( u:Type = "Scroll" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllQRatio_fTchwsTcwsSI")
        else if( u:Type = "Reciprocating" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllQRatio_fTchwsTcwsSI")
        else if( u:Type = "Screw" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllQRatio_fTchwsTcwsSI")
        //  CENTRIFUGAL
        else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd <  (12000 * 150) ) ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBLt300tonQRatio_fTchwsTcwsSI")
        else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd <  (12000 * 300) ) ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBLt300tonQRatio_fTchwsTcwsSI")
        else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd <  (12000 * 600) ) ) then
          RuleLibrary(CrvDblQuad,
                    "ChlrWtrCentPathBGtEql300Lt600tonQRatio_fTchwsTcwsSI")
        else if( u:Type = "Centrifugal" .AND. IfValidAnd( u:CapRtd >= (12000 * 600) ) ) then
          RuleLibrary(CrvDblQuad,
                    "ChlrWtrCentPathBGtEql600tonQRatio_fTchwsTcwsSI")
        else
          PostError( "Path B chiller '%s' not Scroll, Recip, Screw or Centrifugal.",
                      Name )
        endif endif endif endif endif endif endif
      else
        PostError( "Water chiller '%s' not Path A or B.",Name )
      endif endif
    else
      PostError( "Chiller '%s' doesn't have Type (Path A or B).",Name )
    endif endif endif endif
  ANNUAL : S901G ECBC
    if( IsBaseSys ) then
      if( CapRtd/12000 < 600 ) then
        RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllQRatio_fTchwsTcwsSI")
      else
        RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql600tonQRatio_fTchwsTcwsSI")
      endif
    else z:Cap_fTempCrvRef
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( CapRtd/12000 < 300 ) then
        RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllQRatio_fTchwsTcwsSI")
      else if( CapRtd/12000 < 600 ) then
        RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql300Lt600tonQRatio_fTchwsTcwsSI")
      else
        RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql600tonQRatio_fTchwsTcwsSI")
      endif endif
    else z:Cap_fTempCrvRef
    endif
ENDRULE


// ********** <Chiller Capacity Curve Name> ************************************
RULE Chlr:Cap_fGenTempCrvRef
//      Chiller Capacity Adjustment Curve
  DESCRIPTION
    "The name of the performance curve which describes the chiller capacity
     as a function of entering generator fluid temperature."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "AbsorptionSingleEffect" ) then
      RuleLibrary(CrvCubic,"ChlrAbsorbSglStgIndirQRatio_fTgenSI")
    else
      UNDEFINED
    endif
ENDRULE


// ********** <Chiller Capacity Curve Name> ************************************
RULE Chlr:Cap_fCndTempCrvRef
//      Chiller Cooling Capacity Adjustment Curve
  DESCRIPTION
    "The name of the performance curve which describes the chiller cooling
     capacity as a function of entering condenser water temperature."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "AbsorptionSingleEffect" ) then
      RuleLibrary(CrvQuad,"ChlrAbsorbSglStgIndirQRatio_fTcwsSI")
    else
      UNDEFINED
    endif
ENDRULE


// ********** <Chiller Capacity Curve Name> ************************************
RULE Chlr:Cap_fEvapTempCrvRef
//      Chiller Cooling Capacity Adjustment Curve
  DESCRIPTION
    "The name of the performance curve which describes the chiller cooling
     capacity as a function of entering chilled water temperature."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "AbsorptionSingleEffect" ) then
      RuleLibrary(CrvQuad,"ChlrAbsorbSglStgIndirQRatio_fTchwsSI")
    else
      UNDEFINED
    endif
ENDRULE


// ********** <Chiller Efficiency as a function of temperature curve name> *****
RULE Chlr:EIR_fTempCrvRef
//      Electric Chiller Cooling Efficiency fTemperature Curves
  DESCRIPTION
    "The name of the performance curve which describes the chiller efficiency
    as a function of operating temperatures."
  HELP
    "The name of the performance curve which describes the chiller efficiency
    as a function of operating temperatures.  For water cooled chillers
    the temperatures are the leaving chilled water and entering condenser
    water temperature.  For air cooled chillers the temperatures are the
    leaving chilled water temperature and the outdoor air drybulb. "
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
// TO DO: Move PostError checks to CHECKSIM
  SIZING
    if( IsBaseSys ) then
      RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql600tonEIRRatio_fTchwsTcwsSI")
    //   FUEL and HEAT Driven
    else if( Type = "AbsorptionSingleEffect" ) then
      UNDEFINED
    //    AIR COOLED ELECTRIC
    else if( CndsrType = "Air" ) then
      if( Type = "Scroll" ) then
        RuleLibrary(CrvDblQuad,"ChlrAirScrollEIRRatio_fTchwsToadbSI")
      else if( Type = "Reciprocating" ) then
        RuleLibrary(CrvDblQuad,"ChlrAirRecipEIRRatio_fTchwsToadbSI")
      else if( Type = "Screw" ) then
        RuleLibrary(CrvDblQuad,"ChlrAirScrewEIRRatio_fTchwsToadbSI")
// CndsrType = "Air" and Type = "Centrifugal" is not currently a valid combination
      else if( Type = "Centrifugal" ) then
;        RuleLibrary(CrvDblQuad,"ChlrAirCentEIRRatio_fTchwsToadbSI")
        PostError( "Centrifugal Air Chiller '%s' not currently supported.", Name )
      else UNCHANGED
      endif endif endif endif
    //    WATER COOLED ELECTRIC
    else if( CndsrType = "Fluid" ) then
      //  PATH A
      if( Chlr:EffPath = "A" ) then
        //  POSITIVE DISPLACEMENT
        if( Type = "Scroll" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathAAllEIRRatio_fTchwsTcwsSI")
        else if( Type = "Reciprocating" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathAAllEIRRatio_fTchwsTcwsSI")
        else if( Type = "Screw" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathAAllEIRRatio_fTchwsTcwsSI")
        //  CENTRIFUGAL
        else if( Type = "Centrifugal" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrCentPathAAllEIRRatio_fTchwsTcwsSI")
        else UNCHANGED
        endif endif endif endif
      //  PATH B
      else if( Chlr:EffPath = "B" ) then
        //  POSITIVE DISPLACEMENT
        if( Type = "Scroll" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllEIRRatio_fTchwsTcwsSI")
        else if( Type = "Reciprocating" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllEIRRatio_fTchwsTcwsSI")
        else if( Type = "Screw" ) then
          RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllEIRRatio_fTchwsTcwsSI")
        //  CENTRIFUGAL
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd <  (12000 * 150) ) ) then
          RuleLibrary(CrvDblQuad,
                    "ChlrWtrCentPathBLt300tonEIRRatio_fTchwsTcwsSI")
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd <  (12000 * 300) ) ) then
          RuleLibrary(CrvDblQuad,
                    "ChlrWtrCentPathBLt300tonEIRRatio_fTchwsTcwsSI")
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd <  (12000 * 600) ) ) then
          RuleLibrary(CrvDblQuad,
                    "ChlrWtrCentPathBGtEql300Lt600tonEIRRatio_fTchwsTcwsSI")
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd >= (12000 * 600) ) ) then
          RuleLibrary(CrvDblQuad,
                    "ChlrWtrCentPathBGtEql600tonEIRRatio_fTchwsTcwsSI")
        else UNCHANGED
        endif endif endif endif endif endif endif
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif endif endif endif
  ANNUAL : S901G ECBC
    if( IsBaseSys ) then
      if( CapRtd/12000 < 600 ) then
        RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllEIRRatio_fTchwsTcwsSI")
      else
        RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql600tonEIRRatio_fTchwsTcwsSI")
      endif
    else z:EIR_fTempCrvRef
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( CapRtd/12000 < 300 ) then
        RuleLibrary(CrvDblQuad,"ChlrWtrPosDispPathBAllEIRRatio_fTchwsTcwsSI")
      else if( CapRtd/12000 < 600 ) then
        RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql300Lt600tonEIRRatio_fTchwsTcwsSI")
      else
        RuleLibrary(CrvDblQuad,"ChlrWtrCentPathBGtEql600tonEIRRatio_fTchwsTcwsSI")
      endif endif
    else z:EIR_fTempCrvRef
    endif
ENDRULE


// ********** <Chiller Efficiency as a function of temperature curve name> *****
RULE Chlr:HIR_fCndTempCrvRef
//      Absorption Chiller Cooling Efficiency fCondenser Temperature Curve
  DESCRIPTION
    "The name of the performance curve which describes the chiller efficiency
    as a function of entering condenser water temperature."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "AbsorptionSingleEffect" ) then
      RuleLibrary(CrvCubic,"ChlrAbsorbSglStgIndirHIRRatio_fTcwsSI")
    else
      UNDEFINED
    endif
ENDRULE


// ********** <Chiller Efficiency as a function of temperature curve name> *****
RULE Chlr:HIR_fEvapTempCrvRef
//      Absorption Chiller Cooling Efficiency fEvaporator Temperature Curve
  DESCRIPTION
    "The name of the performance curve which describes the chiller efficiency
    as a function of entering chilled water temperature."
  HELP
    ""
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "AbsorptionSingleEffect" ) then
      RuleLibrary(CrvCubic,"ChlrAbsorbSglStgIndirHIRRatio_fTchwsSI")
    else
      UNDEFINED
    endif
ENDRULE


// ********** <Chiller Efficiency as a function of PLR curve name> *************
RULE Chlr:EIR_fPLRCrvRef
//      Electric Chiller Cooling Efficiency fPLR Curves
  DESCRIPTION
    "The name of the performance curve which describes the chiller efficiency
    as a function of part load ratio."
  HELP
    "The name of the performance curve which describes the chiller efficiency
    as a function of part load ratio.  The part load ration is the actual
    cooling load divided by the chillers available cooling capacity.  The
    curve output should decrease from 1 towards 0 as part-load ratio
    decreases from 1 to 0."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
// TO DO: Move PostError checks to CHECKSIM
  SIZING
    if( IsBaseSys ) then
    RuleLibrary(CrvQuad,"ChlrWtrCentPathBGtEql600tonEIRRatio_fQRatio")
    //   FUEL and HEAT Driven
    else if( Type = "AbsorptionSingleEffect" ) then
      RuleLibrary(CrvLin,"ChlrAbsorbSglStgIndirEIRRatio_fQRatioSI")
    //    AIR COOLED ELECTRIC
    else if( CndsrType = "Air" ) then
      if( Type = "Scroll" ) then
        RuleLibrary(CrvQuad,"ChlrAirScrollEIRRatio_fQRatio")
      else if( Type = "Reciprocating" ) then
        RuleLibrary(CrvQuad,"ChlrAirRecipEIRRatio_fQRatio")
      else if( Type = "Screw" ) then
        RuleLibrary(CrvQuad,"ChlrAirScrewEIRRatio_fQRatio")
      else if( Type = "Centrifugal" ) then
        RuleLibrary(CrvQuad,"ChlrAirCentEIRRatio_fQRatio")
      else UNCHANGED
      endif endif endif endif
    //    WATER COOLED ELECTRIC
    else if( CndsrType = "Fluid" ) then
      //  PATH A
      if( Chlr:EffPath = "A" ) then
        //  POSITIVE DISPLACEMENT
        if( Type = "Scroll" ) then
          RuleLibrary(CrvQuad,"ChlrWtrPosDispPathAAllEIRRatio_fQRatio")
        else if( Type = "Reciprocating" ) then
          RuleLibrary(CrvQuad,"ChlrWtrPosDispPathAAllEIRRatio_fQRatio")
        else if( Type = "Screw" ) then
          RuleLibrary(CrvQuad,"ChlrWtrPosDispPathAAllEIRRatio_fQRatio")
        //  CENTRIFUGAL
        else if( Type = "Centrifugal" ) then
          RuleLibrary(CrvQuad,"ChlrWtrCentPathAAllEIRRatio_fQRatio")
        else UNCHANGED
        endif endif endif endif
      //  PATH B
      else if( Chlr:EffPath = "B" ) then
        //  POSITIVE DISPLACEMENT
        if( Type = "Scroll" ) then
          RuleLibrary(CrvQuad,"ChlrWtrPosDispPathBAllEIRRatio_fQRatio")
        else if( Type = "Reciprocating" ) then
          RuleLibrary(CrvQuad,"ChlrWtrPosDispPathBAllEIRRatio_fQRatio")
        else if( Type = "Screw" ) then
          RuleLibrary(CrvQuad,"ChlrWtrPosDispPathBAllEIRRatio_fQRatio")
        //  CENTRIFUGAL
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd <  (12000 * 150) ) ) then
          RuleLibrary(CrvQuad,"ChlrWtrCentPathBLt300tonEIRRatio_fQRatio")
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd <  (12000 * 300) ) ) then
          RuleLibrary(CrvQuad,"ChlrWtrCentPathBLt300tonEIRRatio_fQRatio")
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd <  (12000 * 600) ) ) then
          RuleLibrary(CrvQuad,
                    "ChlrWtrCentPathBGtEql300Lt600tonEIRRatio_fQRatio")
        else if( Type = "Centrifugal" .AND. IfValidAnd( CapRtd >= (12000 * 600) ) ) then
          RuleLibrary(CrvQuad,"ChlrWtrCentPathBGtEql600tonEIRRatio_fQRatio")
        else UNCHANGED
        endif endif endif endif endif endif endif
      else UNCHANGED
      endif endif
    else UNCHANGED
    endif endif endif endif
  ANNUAL : S901G ECBC
    if( IsBaseSys ) then
      if( CapRtd/12000 < 600 ) then
        RuleLibrary(CrvQuad,"ChlrWtrPosDispPathBAllEIRRatio_fQRatio")
      else
        RuleLibrary(CrvQuad,"ChlrWtrCentPathBGtEql600tonEIRRatio_fQRatio")
      endif
    else z:EIR_fPLRCrvRef
    endif
  ANNUAL
    if( IsBaseSys ) then
      if( CapRtd/12000 < 300 ) then
        RuleLibrary(CrvQuad,"ChlrWtrPosDispPathBAllEIRRatio_fQRatio")
      else if( CapRtd/12000 < 600 ) then
        RuleLibrary(CrvQuad,"ChlrWtrCentPathBGtEql300Lt600tonEIRRatio_fQRatio")
      else
        RuleLibrary(CrvQuad,"ChlrWtrCentPathBGtEql600tonEIRRatio_fQRatio")
      endif endif
    else z:EIR_fPLRCrvRef
    endif
ENDRULE


// ********** <Chiller Efficiency as a function of PLR curve name> *************
RULE Chlr:HIR_fPLRCrvRef
//      Absorption Chiller Cooling Efficiency fPLR Curves
  DESCRIPTION
    "The name of the performance curve which describes the chiller efficiency
    as a function of part load ratio."
  HELP
    "The name of the performance curve which describes the chiller efficiency
    as a function of part load ratio.  The part load ration is the actual
    cooling load divided by the chillers available cooling capacity.  The
    curve output should decrease from 1 towards 0 as part-load ratio
    decreases from 1 to 0."
  REFERENCE
    ACM Section 5.8.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "AbsorptionSingleEffect" ) then
      RuleLibrary(CrvCubic,"ChlrAbsorbSglStgIndirHIRRatio_fQRatioSI")
    else
      UNDEFINED
    endif
ENDRULE



