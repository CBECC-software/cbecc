// HVAC Secondary Systems - Cooling Coils - Capacity
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------


//  This rule file addresses the following building descriptors:
//  Section 5.7.5.1 - General
//      Cooling Source
//      Total Cooling Capacity
//      Sensible Cooling Capacity
//  Section 5.7.5.2 - Direct Expansion
//      Condenser Type



// ---------- Section 5.7.5.2 - Direct Expansion -------------------------------


// ********** Seasonal Energy Efficiency Ratio *********************************
RULE NEW CoilClg:CodeMinSEER
  DATATYPE
    Float
  LONGFORM
    CodeMinimumSEER
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CndsrType = "Air" ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
      	if(AirSys:Type = "SPVAC" .OR. AirSys:Type ="SPVHP")
        then // efficiency for SPVAC and SPVHP are described in terms of EER 
          UNDEFINED
        else 
        if( CapTotNetRtd < 65000 .AND. 
            ValidOr( Parent2( SubType ), "undef" ) != "undef" )  // work-around for not having Parent2Status
        then
          if( Proj:StdsVersion != "Compliance2014" .AND. 
              ( AirSys:SubType = "Packaged1Phase" .OR. AirSys:SubType = "Split1Phase" ) )
          then 14.0
          else 13.0
          endif
        else UNDEFINED
        endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" )
      then // Part of a ZoneSystem
        if( ( ZnSys:Type = "SZAC" .OR. ZnSys:Type = "SZHP" ) .AND.
            CapTotNetRtd < 65000 .AND. 
            ValidOr( Parent( SubType ), "undef" ) != "undef" )
        then
          if( Proj:StdsVersion != "Compliance2014" .AND. 
              ( ZnSys:SubType = "Packaged1Phase" .OR. ZnSys:SubType = "Split1Phase" ) )
          then 14.0
          else 13.0
          endif
        else UNDEFINED
        endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else u:CodeMinSEER
    endif 
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED
    else z:CodeMinSEER
    endif 
ENDRULE
// -----------------------------------------------------------------------------
RULE CoilClg:DXSEER
  DESCRIPTION
    "The Seasonal Energy Efficiency Ratio (SEER) is a term used to describe the 
     seasonal performance of a DX cooling system. It is determined in accordance with 
     AHRI standards." 
  HELP
    "This is a required input for air-source DX cooling coils in packaged/split
     units where EER is not entered and the net capacity is <65  MBH."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
   CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    20
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Proj:AutoEffInput = 1 .AND. LocalStatus( CodeMinSEER ) > 0 )
    then // For PROPOSED AutoEffInput only 
      CodeMinSEER
    else UNDEFINED
    endif
  CHECKCODE
    if( LocalStatus( CodeMinSEER ) > 0 .AND. 
        IfValidAnd( DXSEER > 0 ) = 0 )
    then PostError("DX coil '%s' must be specified with AHRI rated SEER", Name)
    else 
    if( BypassMinEffCheck = 0 .AND.
        LocalStatus( CodeMinSEER ) > 0 .AND. 
        IfValidAnd( DXSEER < CodeMinSEER ) .AND.
        IsNew )
    then PostError("SEER of coil '%s' is less than the code minimum required efficiency 
                    of SEER-%.1f. Select the bypass of efficiency check at the coil
                    if this is not applicable.", Name, CodeMinSEER)
    else UNCHANGED 
    endif endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "DirectExpansion" )
    then // Not used for non-DX coils and efficiency is unknown for sizing baseline systems
      UNDEFINED
    else if( LocalStatus( CodeMinSEER ) > 0 )  
    then // User-equipment is SEER-rated
      u:DXSEER
    else UNDEFINED
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then // Is baseline system
      if( IfValidAnd( CapTotNetRtd < 65000 ) )
      then 13.0 // Baseline is assumed 3Phase
      else UNDEFINED // Not used if net capacity >= 65 MBH
      endif
    else z:DXSEER
    endif
ENDRULE


// ********** Direct Expansion Cooling Efficiency ******************************
// Determine code minimum EER for systems with DX cooling coils
RULE NEW CoilClg:CodeMinEER
  DATATYPE
    Float
  LONGFORM
    CodeMinimumEER
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        if( AirSys:Type = "SPVAC" .OR. AirSys:Type ="SPVHP" )
        then // efficiency for SPVAC and SPVHP are described in terms of EER 
          if( CapTotNetRtd >= 240000)
          then UNDEFINED
          else SPVACClgEfficiencyT24N:EER("SizeCategory", CapTotNetRtd)
          endif
        else
        if( LocalStatus( CndsrType ) > 0 .AND.
            AirSys:UnitaryCat != "NA" .AND.
            Proj:StdsVersion != "Compliance2014" .AND.
            AirSys:SubType != "Packaged3Phase" .AND.
            CapTotNetRtd < 65000 )
        then // <65MBH and >=2015 efficiency requirements
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", CndsrType,
            "SizeCategory", CapTotNetRtd )
        else        
        if( IfValidAnd( CndsrType = "Air" ) .AND.
            AirSys:UnitaryCat != "NA" .AND.
            CapTotNetRtd >= 65000 )
        then // Efficiency requirements for air-source AC/HP
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", CndsrType,
            "SizeCategory", CapTotNetRtd ) - AirSys:EERDeduction
        else
        if( IfValidAnd( CndsrType = "WaterSource" ) .AND.
            AirSys:UnitaryCat != "NA" .AND.
            CapTotNetRtd >= 65000 .AND.
            CapTotNetRtd < 760000 )
        then // Efficiency requirements for water-source AC/HP
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", AirSys:SubType,
            "CondenserType", CndsrType,
            "SizeCategory", CapTotNetRtd ) - AirSys:EERDeduction
        else UNDEFINED
        endif endif endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" ) 
      then // Part of an ZoneSystem
        if( ( ZnSys:Type = "SZAC" .OR. ZnSys:Type = "SZHP" ) .AND.
            CapTotNetRtd < 65000 .AND.
            Proj:StdsVersion != "Compliance2014" .AND.
            ZnSys:UnitaryCat != "NA" .AND.
            ZnSys:SubType != "Packaged3Phase" )
        then // <65MBH and >=2015 efficiency requirements
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", ZnSys:UnitaryCat,
            "SubCategory", ZnSys:SubType,
            "CondenserType", "Air",
            "SizeCategory", CapTotNetRtd )
        else
        if( ( ZnSys:Type = "SZAC" .OR. ZnSys:Type = "SZHP" ) .AND.
            CapTotNetRtd >= 65000 .AND.
            IfValidAnd( CndsrType = "Air" ) .AND.
            ZnSys:UnitaryCat != "NA" )
        then // Efficiency requirements for air-source AC/HP
          UnitaryACHPMinEffReq:EER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", ZnSys:UnitaryCat,
            "SubCategory", ZnSys:SubType,
            "CondenserType", "Air",
            "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
        // Rules currently only cover PTAC and PTHP in new constrution or newly 
        // conditioned buildings or additions.  Rules for PTAC/PTHP replacements 
        // not currently supported.
        else
        if( (ZnSys:Type = "SPVAC" .OR. ZnSys:Type = "SPVHP" ))
        then // <240MBH >=2015 Appliance Efficiency Standards
          if( CapTotNetRtd >= 240000)
          then UNDEFINED
          else SPVACClgEfficiencyT24N:EER("SizeCategory", CapTotNetRtd)
          endif
        else 
        if( ZnSys:Type = "PTAC" )
        then
        // This is a cooling coil in a Znsys PTAC, and subject the efficiency 
        // requirements of Table 110.2-E.
          if( Proj:StdsVersion != "Compliance2016" )
          then   
            13.8 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
          else 
            14.0 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
          endif
        else
        if( ZnSys:Type = "PTHP" )
        then 
        // This is a cooling coil in a Znsys PTHP, and subject the efficiency 
        // requirements of Table 110.2-E.  
          14.0 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
        else
        if( ZnSys:Type = "WSHP" .AND. 
            IfValidAnd( CndsrType = "WaterSource" ) .AND.
            ZnSys:UnitaryCat != "NA" )
        then
          if( CapTotNetRtd < 65000 )
          then
            UnitaryACHPMinEffReq:EER(
              "StandardsVersion", Proj:StdsVersion,
              "Category", ZnSys:UnitaryCat,
              "SubCategory", ZnSys:SubType,
              "CondenserType", "WaterSource",
              "SizeCategory", CapTotNetRtd )
          else
          if( CapTotNetRtd < 760000 )
          then
            UnitaryACHPMinEffReq:EER(
              "StandardsVersion", Proj:StdsVersion,
              "Category", ZnSys:UnitaryCat,
              "SubCategory", ZnSys:SubType,
              "CondenserType", "WaterSource",
              "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
          else UNDEFINED
          endif endif
        else UNDEFINED
        endif endif endif endif endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else u:CodeMinEER
    endif 
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED
    else z:CodeMinEER
    endif 
ENDRULE
// Determine code minimum IEER for systems with DX cooling coils
RULE NEW CoilClg:CodeMinIEER
  DATATYPE
    Float
  LONGFORM
    CodeMinimumIEER
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) )
    then
      if( ParentComp = "AirSys" )
      then // Part of an AirSystem
        // Cooling coil is in an AirSys it is subject to unitary equipment 
        // efficiency requirements of T24 Table 110.2-A and 110.2-B
        if(AirSys:Type = "SPVAC" .OR. AirSys:Type ="SPVHP")
        then // efficiency for SPVAC and SPVHP are described in terms of EER 
          UNDEFINED
        else     
        if( LocalStatus( CndsrType ) > 0 .AND.
            AirSys:UnitaryCat != "NA" .AND.
            CapTotNetRtd >= 65000 )
        then // Table look up of minimum IEER
          UnitaryACHPMinEffReq:IEER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", AirSys:UnitaryCat,
            "SubCategory", "*",
            "CondenserType", CndsrType,
            "SizeCategory", CapTotNetRtd ) - AirSys:EERDeduction
        else UNDEFINED
        endif endif
      else // ------------------------------------------------------------------
      if( ParentComp = "ZnSys" ) 
      then // Part of an ZoneSystem
        if( ( ZnSys:Type = "SZAC" .OR. ZnSys:Type = "SZHP" ) .AND.
            ZnSys:UnitaryCat != "NA" .AND.
            CapTotNetRtd >= 65000 )
        then // Table look up of minimum IEER
          UnitaryACHPMinEffReq:IEER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", ZnSys:UnitaryCat,
            "SubCategory", "*",
            "CondenserType", "Air",
            "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
        else
        if( ZnSys:Type = "WSHP" .AND. 
            ZnSys:UnitaryCat = "AC" .AND.
            CapTotNetRtd >= 65000 )
        then // Table look up of minimum IEER
          UnitaryACHPMinEffReq:IEER(
            "StandardsVersion", Proj:StdsVersion,
            "Category", "AC",
            "SubCategory", "*",
            "CondenserType", "WaterSource",
            "SizeCategory", CapTotNetRtd ) - ZnSys:EERDeduction
        else UNDEFINED
        endif endif
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else u:CodeMinIEER
    endif 
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED
    else z:CodeMinIEER
    endif 
ENDRULE
// -----------------------------------------------------------------------------
// Determine code minimum EER of systems with separate condensing units (split systems only) 
RULE NEW CoilClg:CodeMinEERCndsr
  DATATYPE
    Float
  LONGFORM
    CodeMinEERCondenser
  DESCRIPTION
    "The efficiency of the direct expansion (DX) condensing unit 
     (compressor + condenser fan only) at AHRI rated conditions."
  INPUTCLASS
    NotInput
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" .AND. 
        ParentComp = "AirSys" .AND. 
        IfValidAnd( CapTotNetRtd > 0 ) )
      // If the cooling coil is in an AirSys with a remote condensing unit (split),
      // it is subject to also subject to the Condensing Unit efficiency 
      // requirements of T24 Table 110.2-A
    then
      if( AirSys:UnitaryCat = "AC" .AND. 
          AirSys:SubType = "Split3Phase" .AND.
          CapTotNetRtd >= 135000 )
      then
        // Condensing unit minimum EER applies
        switch ( CndsrType )
          case "Air"                 : 10.5
          case "EvaporativelyCooled" : 13.5
          case "WaterSource"         : 13.5
          default : UNCHANGED
        endswitch
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  SIZING
    if( BaseSysNum > 0 )
    then UNDEFINED
    else u:CodeMinEERCndsr
    endif 
  ANNUAL
    if( BaseSysNum > 0 )
    then UNDEFINED
    else z:CodeMinEERCndsr
    endif 
ENDRULE
// -----------------------------------------------------------------------------
// DX EER 
RULE CoilClg:DXEER
  DESCRIPTION
    "The efficiency of a direct expansion (DX) cooling system at AHRI rated 
     conditions."
  HELP
    "This is a required input for DX cooling coils in PTAC/PTHP, WSHP, and other
     packaged/split units >= 65  MBH cooling capacity."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    18  
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" )
    then
      if( Proj:AutoEffInput = 1 .AND. IfValidAnd( CodeMinEER > 0 ) )
      then // For PROPOSED AutoEffInput only
        CodeMinEER
      else if( IfValidAnd( DXSEER > 0 ) )
      then // Calulate EER from SEER; shown for reference
        Min( 13, ( -0.0194 * DXSEER**2 ) + ( 1.0864 * DXSEER ) )
      else UNDEFINED
      endif endif
    else UNDEFINED
    endif
  CHECKCODE
      if( ( LocalStatus( CodeMinEER ) > 0 .OR. LocalStatus( CodeMinEERCndsr ) > 0 )
          .AND. IfValidAnd( DXEER > 0 ) = 0 )
      then 
         PostError("DX coil '%s' must be specified with AHRI rated EER", Name)
      else
      if( BypassMinEffCheck = 0 .AND. 
          LocalStatus( CodeMinEER ) > 0 .AND. 
          IfValidAnd( DXEER < CodeMinEER ) .AND.
          IsNew )
      then 
        PostError("Air conditioner efficiency (EER) of coil '%s' is less than the code 
                   minimum required efficiency of EER-%.1f. Select the bypass of
                   efficiency check at the coil if this is not applicable.", 
                   Name, CodeMinEER)
      else UNCHANGED 
      endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
      if( LocalStatus( CodeMinEER ) > 0 .AND. 
          IfValidAnd( DXEER > 0 ) = 0 )
      then 
         PostError("DX coil ''%s' must be specified with AHRI rated EER", Name)
      else UNCHANGED 
      endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "DirectExpansion" )
    then
      // Not used for non-DX coils or ZnSys DX coils, and 
      // AHRI rated EER unknown for baseline systems 
      UNDEFINED
    else if( IfValidAnd( u:DXSEER > 0 ) .AND. LocalStatus( u:DXEER ) = 0 )
    then // User has provided SEER but not EER, use default equation to calculate EER 
      Min( 13, ( -0.0194 * u:DXSEER**2 ) + ( 1.0864 * u:DXSEER ) )
    else // Use user-specified EER
      u:DXEER
    endif endif
  ANNUAL : T24N
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion")
    then
      if( ParentComp = "ZnSys" )
      then
        if( ZnSys:BaseSysNum = 1 )
        then // Is PTAC
          if( Proj:StdsVersion != "Compliance2016" )
          then   
            13.8 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
          else 
            14.0 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
          endif 
        else UNDEFINED
        endif
      else if( IfValidAnd( CapTotNetRtd < 65000 ) ) 
      then // Use SEER -> EER equation
        ( -0.0194 * DXSEER**2 ) + ( 1.0864 * DXSEER )
      else // Look-up minimum efficiency based on baseline rated net capacity 
        StdDXClgEfficiencyT24N:EER("SizeCategory", CapTotNetRtd)
      endif endif
    else z:DXEER
    endif
  ANNUAL : S901G ECBC
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion")
    then
      if( ParentComp = "ZnSys" )
      then // Is PTHP/PTAC, look-up minimum efficiency from 6.8.1D
        if( ZnSys:BaseSysNum = 1 )
        then // Is PTAC
          13.8 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 )
        else // Is PTHP
          14.0 - ( 0.300 * ( Min( 15000, Max( 7000, CapTotNetRtd ) ) ) / 1000 ) 
        endif
      else if( IfValidAnd( CapTotNetRtd < 65000 ) ) 
      then // Use SEER -> EER equation
        ( -0.0194 * DXSEER**2 ) + ( 1.0864 * DXSEER )
      else if( AirSys:BaseSysNum = 4 )
      then // Is SZHP, look-up minimum efficiency from 6.8.1B
        StdHPClgEfficiencyS901G:EER("SizeCategory", CapTotNetRtd)
      else if( AirSys:BaseSysNum = 3 )
      then // Is SZAC, look-up minimum efficiency from 6.8.1A, include 0.2 EER deduction
        StdDXClgEfficiencyS901G:EER("SizeCategory", CapTotNetRtd) - 0.2
      else // Look-up minimum efficiency from 6.8.1A, assuming no heating source
        StdDXClgEfficiencyS901G:EER("SizeCategory", CapTotNetRtd)
      endif endif endif endif
    else z:DXEER
    endif
ENDRULE
// DX IEER 
RULE CoilClg:DXIEER
  DESCRIPTION
    "The integrated part-load efficiency of a direct expansion (DX) cooling system at AHRI rated 
     conditions."
  HELP
    "This is a required input for air- and water-source DX cooling coils in systems with >= 65MBH
     cooling capacity."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    CondRequired
  COMMONMINIMUM
    7
  COMMONMAXIMUM
    18  
  UNITS 
    Btu/h-W
  REPORTPRECISION
    1
  DEFAULT
    if( Type = "DirectExpansion" )
    then
      if( Proj:AutoEffInput = 1 .AND. IfValidAnd( CodeMinIEER > 0 ) )
      then // For PROPOSED AutoEffInput only
        CodeMinIEER
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE
      if( LocalStatus( CodeMinIEER ) > 0 .AND. IfValidAnd( DXIEER > 0 ) = 0 )
      then 
         PostError("DX coil '%s' must be specified with AHRI rated IEER", Name)
      else
      if( BypassMinEffCheck = 0 .AND. 
          LocalStatus( CodeMinIEER ) > 0 .AND. 
          IfValidAnd( DXIEER < CodeMinIEER ) .AND.
          IsNew )
      then 
        PostError("Air conditioner integrated efficiency (IEER) of coil '%s' is less than the code 
                   minimum required efficiency of IEER-%.1f. Select the bypass of
                   efficiency check at the coil if this is not applicable.", 
                   Name, CodeMinIEER)
      else UNCHANGED 
      endif endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
      if( LocalStatus( CodeMinIEER ) > 0 .AND. IfValidAnd( DXIEER > 0 ) = 0 )
      then 
         PostError("DX coil ''%s' must be specified with AHRI rated IEER", Name)
      else UNCHANGED 
      endif
  SIZING
    if( BaseSysNum > 0 .OR. Type != "DirectExpansion" )
    then
      // Not used for non-DX coils or ZnSys DX coils, and 
      // AHRI rated EER unknown for baseline systems 
      UNDEFINED
    else // Use user-specified EER
      u:DXIEER
    endif
  ANNUAL
    z:DXIEER
ENDRULE

// -----------------------------------------------------------------------------
// T24N DX cooling efficiency
TABLE StdDXClgEfficiencyT24N
// All EER values included deduction indicated in Table 110.2-A footnote b: for
// units with a heating section other than electric resistance heat.
    SizeCategory    EER    
//  <65000          Defined in DXEER rule
    <135000         11.0   
    <240000         10.8   
    <760000         9.8   
    >=760000        9.5   
ENDTABLE

// -----------------------------------------------------------------------------
// S901G DX cooling efficiency
// Table 6.8.1A
TABLE StdDXClgEfficiencyS901G
// EERs DO NOT reflect 0.2 deduction from required EERs for units with a heating
    SizeCategory    EER    
//  <65000          Defined in DXEER rule
    <135000         11.2   
    <240000         11.0   
    <760000         10.0 
    >=760000        9.7  
ENDTABLE

// S901G HP cooling efficiency
// Table 6.8.1B
TABLE StdHPClgEfficiencyS901G
// EERs reflect 0.2 deduction from required EERs for units with a heating section 
// other than electric resistance heat.
    SizeCategory    EER    
//  <65000          Defined in DXEER rule
    <135000         10.8 
    <240000         10.4   
    >=240000        9.3  
ENDTABLE  


// T24 Single Packaged Vertical Air Conditioner cooling efficiency
// Appl Std Table C-5
TABLE SPVACClgEfficiencyT24N
    SizeCategory    EER    
    <65000          9.0
    <135000         8.9 
    >=135000        8.6  
ENDTABLE  


// -----------------------------------------------------------------------------
// Adjust rated EER to remove fan heat at AHRI rating condtion
RULE CoilClg:DXEERAdj
  DESCRIPTION
    "The cooling efficiency of a direct expansion (DX) cooling system at AHRI 
     rated conditions, adjusted to remove fan energy and pump energy.


      DXEERAdj = ( Net capacity (Btu/hr) + Fan heat (Btu/hr) ) /
                 ( Rated power (W) - fan power (W) - pump power (W) )"
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/h-W
  DEFAULT
    // Calculated in DEFAULT rules for CHECKCODE rule 
    if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
        IfValidAnd( DXEER > 0 ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then
      CapTotGrossRtd /  
      ( ( CapTotNetRtd / DXEER ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
    else UNDEFINED
    endif
  CHECKCODE
    // Check Table 110.2-A minimum efficiency for condensing units 
    // Condensing unit EER = EERAdj
    if( LocalStatus( CodeMinEERCndsr ) > 0 )
      then
      if( BypassMinEffCheck = 0 .AND.
          IfValidAnd( DXEERAdj < CodeMinEERCndsr ) .AND. 
          IsNew )
      then 
        PostError("Condensing unit efficiency of coil '%s' is less than 
                   the code minimum required EER-%.1f. Select the bypass of
                   efficiency check at the coil if this is not applicable.", 
                   Name, CodeMinEERCndsr)
      else UNCHANGED 
      endif 
    else UNCHANGED
    endif
  CHECKCODE : S901G ECBC
    // 90.1G does not include mandatory minimum efficiency
    // Condensing unit EER = EERAdj
    if( LocalStatus( CodeMinEERCndsr ) > 0 .AND. 
        ParentComponentType() != "ZnSys" )
      then
      if( LocalStatus( DXEERAdj ) = 0 )
      then
        PostError("DX coil '%s' must be specified with AHRI rated EER", Name)
      else UNCHANGED 
      endif   
    else UNCHANGED
    endif
  SIZING
    if( ( BaseSysNum > 0 ) .OR. Type != "DirectExpansion" )
      // Not used for non-DX coils and 
      // AHRI rated EER unknown for baseline systems 
    then UNDEFINED
    else
    if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
        IfValidAnd( DXEER > 0 ) .AND.
        IfValidAnd( CapTotNetRtd > 0 ) )
    then
      // DXEERAdj = ( Net capacity (Btu/hr) + Fan heat (Btu/hr) ) /
      //            ( Rated power (W) - fan power (W) - pump power (W) )
      // where the fan heat/power is the power associated with overcoming the
      // internal static and coil pressure drop ( for water-source equipment )
      // ( CapTotNetRtd + FanHtRtd ) /
      // ( CapTotNetRtd / DXEER ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
      // CapTotGrossRtd = CapTotNetRtd + FanHtRtd
      CapTotGrossRtd /  
      ( ( CapTotNetRtd / DXEER ) - ( FanHtRtd / 3.412 ) - PumpPwrRtd )
    else 0
    endif endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion")
    then
      if( IfValidAnd( CapTotGrossRtd > 0 ) .AND.
          IfValidAnd( DXEER > 0 ) .AND.
          IfValidAnd( CapTotNetRtd > 0 ) )
      then
        CapTotGrossRtd / ; CapTotGrossRtd = CapTotNetRtd + FanHtRtd
        ( ( CapTotNetRtd / DXEER ) - ( FanHtRtd / 3.412 ) )
      else 0
      endif
    else z:DXEERAdj
    endif  
ENDRULE
 
// -----------------------------------------------------------------------------
// Convert EERAdj to EIR for simulation purposes
RULE CoilClg:DXEIR
  DESCRIPTION
    "The cooling efficiency of a direct expansion (DX) cooling system, described
     for simulation purposes.  Enery Input Ratio (EIR) is the inverse of the COP."
  REFERENCE 
    NACM Section 5.7.5.2 
  INPUTCLASS 
    NotInput
  SIZING
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then 1 / 3.0  // Default cooling efficiency assumed for baseline sizing run
    else if( IfValidAnd( DXEERAdj > 0 ) )
    then 1 / ( DXEERAdj / 3.412 )
//    then
//      if( ParentComp = "AirSys" )
//      then 
//        if( IfValidAnd( Parent2Valid( DuctLeakage ) > 0 ) )
//       then // For now, increase EIR by 6.5% if DuctLeakage > 0
//          1 / ( DXEERAdj / 3.412 ) * 1.065       
//        else // No adjustment
//          1 / ( DXEERAdj / 3.412 )
//        endif
//      else if( ParentComp = "ZnSys" )
//      then 
//        if( IfValidAnd( ParentValid( DuctLeakage ) > 0 ) )
//       then // For now, increase EIR by 6.5% if DuctLeakage > 0
//          1 / ( DXEERAdj / 3.412 ) * 1.065       
//        else // No adjustment
//          1 / ( DXEERAdj / 3.412 )
//        endif
//      else 1 / ( DXEERAdj / 3.412 )
//      endif endif
    else UNDEFINED
    endif endif
  ANNUAL 
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( IfValidAnd( DXEERAdj > 0 ) )
      then 1 / ( DXEERAdj / 3.412 )
      else UNDEFINED
      endif 
    else z:DXEIR
    endif
ENDRULE

// -----------------------------------------------------------------------------
// ********** Direct Expansion Cooling Efficiency Temperature Adjustment Curve *
RULE CoilClg:DXEIR_fTempCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     indoor coil and condenser conditions."
  REFERENCE 
    NACM Section 5.7.5.2
  INPUTCLASS
    Prescribed  
  SIZING
    if( Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) ) 
    then
      if( CndsrType = "WaterSource" )
      then UNDEFINED
      // Currently the translator is using hard-coded coefficients in the WLHP objects
      //RuleLibrary(QuadplLin, "CoilClgWSDXEIRRatio_fTwbRatioTwtRatioCFMRatioGPMRatioSI")
      else if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI")
      else if( CapTotNetRtd < 65000 )
      then // Use SEER rated curves based on user input or calculated EER and SEER
        if( IfValidAnd( DXEER >= 13 ) )
        then // EER >= 13, use EER13 set of curves
          if( IfValidAnd( DXSEER >= 20 ) )
          then // SEER >= 20
            RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER20EIRRatio_fTwbToadbSI")
          else if( IfValidAnd( DXSEER >= 18 ) )
          then // SEER >= 18
            RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER18EIRRatio_fTwbToadbSI")
          else // SEER < 18
            RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER16EIRRatio_fTwbToadbSI")
          endif endif
        // EER < 13, use EER11 set of curves
        else if( IfValidAnd( DXSEER >= 17 ) )
        then // SEER >= 17
          RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER17EIRRatio_fTwbToadbSI")
        else if( IfValidAnd( DXSEER >= 15 ) )
        then // SEER >= 15
          RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER15EIRRatio_fTwbToadbSI")
        else // SEER < 15
          RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER13EIRRatio_fTwbToadbSI")
        endif endif endif
      else // Default curve for sizing DX non-PTAC/PTHP equipment 
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbSI") 
      endif endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" )
    then
      if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI")
      else if( CapTotNetRtd < 65000 )
      then // Use SEER rated curves based on user input or calculated EER and SEER
        if( IfValidAnd( DXEER >= 13 ) )
        then // EER >= 13, use EER13 set of curves
          if( IfValidAnd( DXSEER >= 20 ) )
          then // SEER >= 20
            RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER20EIRRatio_fTwbToadbSI")
          else if( IfValidAnd( DXSEER >= 18 ) )
          then // SEER >= 18
            RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER18EIRRatio_fTwbToadbSI")
          else // SEER < 18
            RuleLibrary(CrvDblQuad, "CoilClgDXEER13SEER16EIRRatio_fTwbToadbSI")
          endif endif
        // EER < 13, use EER11 set of curves
        else if( IfValidAnd( DXSEER >= 17 ) )
        then // SEER >= 17
          RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER17EIRRatio_fTwbToadbSI")
        else if( IfValidAnd( DXSEER >= 15 ) )
        then // SEER >= 15
          RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER15EIRRatio_fTwbToadbSI")
        else // SEER < 15
          RuleLibrary(CrvDblQuad, "CoilClgDXEER11SEER13EIRRatio_fTwbToadbSI")
        endif endif endif
      else // Default curve for sizing DX non-PTAC/PTHP equipment 
        RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fTwbToadbSI") 
      endif endif
    else if( Type = "DirectExpansion" )
    then
      if( CndsrType = "WaterSource" )
      then UNDEFINED
      // Currently the translator is using hard-coded coefficients in the WLHP objects
      //RuleLibrary(QuadplLin, "CoilClgWSDXEIRRatio_fTwbRatioTwtRatioCFMRatioGPMRatioSI")
      else z:DXEIR_fTempCrvRef
      endif
    else UNDEFINED
    endif endif
ENDRULE
// Reset DXEIR_fTempCrvRef if system is SPVAC/HP without Economizer
RULE CoilClg:DXEIR_fTempCrvRef
  SIZING
    if( Type = "DirectExpansion" .AND. ParentComp = "AirSys" )
    then
      if( ( SysType = "SPVAC" .OR. SysType = "SPVHP" ) .AND.
          AirSys:HasAirEcono = 0 )
      then // AirSys SPV does not have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI" )
      else UNCHANGED
      endif
    else if( Type = "DirectExpansion" .AND. ParentComp = "ZnSys" )
    then
      if( SysType = "SPVAC" .OR. SysType = "SPVHP" )
      then // ZnSys SPV cannot have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fTwbToadbSI" )
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif
  ANNUAL
    UNCHANGED // Baseline does not use SPVAC/HP
ENDRULE


// -----------------------------------------------------------------------------
// ********** Direct Expansion Part-Load Efficiency Adjustment Curve ***********
RULE CoilClg:DXEIR_fPLFCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     part-load factor.  This curve type is specific to EnergyPlus."
  REFERENCE 
    NACM Section 5.7.5.2
  INPUTCLASS
    Prescribed
  SIZING
    if( Type = "DirectExpansion" .AND. IfValidAnd( CapTotNetRtd > 0 ) ) 
    then
      if( CndsrType = "WaterSource" ) then
        UNDEFINED 
      else if( SysType = "PTAC" .OR. SysType = "PTHP" )
      then
        RuleLibrary(CrvCubic, "CoilClgPTACEIRRatio_fQFrac")
      else if( IfValidAnd( LocalValid( CapTotNetRtd ) < 65000 ) ) then
        RuleLibrary(CrvCubic, "CoilClgDXSEEREIRRatio_fQFrac") 
      else RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fQFrac") 
      endif endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" ) then
      if( SysType = "PTAC" .OR. SysType = "PTHP" ) then
        RuleLibrary(CrvCubic, "CoilClgPTACEIRRatio_fQFrac")
      else if( IfValidAnd( LocalValid( CapTotNetRtd ) < 65000 ) ) then
        RuleLibrary(CrvCubic, "CoilClgDXSEEREIRRatio_fQFrac") 
      else RuleLibrary(CrvDblQuad, "CoilClgDXEIRRatio_fQFrac") 
      endif endif
    else if( Type = "DirectExpansion" .AND. CndsrType = "WaterSource" ) then
      UNDEFINED 
    else z:DXEIR_fPLFCrvRef
    endif endif
ENDRULE
// Reset DXEIR_fPLFCrvRef if system is Single Packaged Unit without Economizer
RULE CoilClg:DXEIR_fPLFCrvRef
  SIZING
    if( Type = "DirectExpansion" .AND. ParentComp = "AirSys" )
    then
      if( ( SysType = "SPVAC" .OR. SysType = "SPVHP" ) .AND.
          AirSys:HasAirEcono = 0 )
      then // AirSys SPV does not have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fQFrac" )
      else UNCHANGED
      endif
    else if( Type = "DirectExpansion" .AND. ParentComp = "ZnSys" )
    then
      if( SysType = "SPVAC" .OR. SysType = "SPVHP" )
      then // ZnSys SPV cannot have economizer, use PTAC curves
        RuleLibrary( CrvDblQuad, "CoilClgPTACEIRRatio_fQFrac" )
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif
  ANNUAL
    UNCHANGED // Baseline does not use SPVAC/HP
ENDRULE


// -----------------------------------------------------------------------------
// Cooling efficiency as a function of indoor coil coil flow.  Applicable to EnergyPlus
RULE CoilClg:DXEIR_fFlowCrvRef
  DESCRIPTION
    "Normalized curve that varies full-load efficiency (EIR) as a function of 
     indoor coil flow. This curve type is specific to EnergyPlus."
  INPUTCLASS
    Prescribed  
  SIZING
    if( Type = "DirectExpansion" ) then
      if( IfValidAnd( CndsrType = "WaterSource" ) ) then
        UNDEFINED 
      else if( IfValidAnd( NumClgStages > 1 ) ) then
        RuleLibrary(CrvCubic, "CoilClgDXDblEIRRatio_fCFMRatio")
      else RuleLibrary(CrvQuad, "CoilClgDXSnglEIRRatio_fCFMRatio")
      endif endif
    else UNDEFINED
    endif
  ANNUAL
    if( BaseSysNum > 0 .AND. Type = "DirectExpansion" ) then
      if( IfValidAnd( NumClgStages > 1 ) ) then
        RuleLibrary(CrvCubic, "CoilClgDXDblEIRRatio_fCFMRatio")
      else RuleLibrary(CrvQuad, "CoilClgDXSnglEIRRatio_fCFMRatio")
      endif
    else if( Type = "DirectExpansion" ) then
      if( IfValidAnd( CndsrType = "WaterSource" ) ) then
        UNDEFINED 
      else z:DXEIR_fFlowCrvRef
      endif
    else UNDEFINED
    endif endif
ENDRULE


// ********** Crankcase Heater Capacity ****************************************
RULE CoilClg:DXCrankcaseHtrCap
  DESCRIPTION
    "The capacity of the electric resistance crankcase heater."
  INPUTCLASS 
    Optional
  UNITS 
    Btu/hr
  SIZING_PROPOSED
    if( Type = "DirectExpansion" .AND. LocalStatus( u:DXCrankcaseHtrCap ) > 0 )
    then u:DXCrankcaseHtrCap
    else UNDEFINED
    endif
  SIZING_BASELINE
    UNDEFINED // Heat pumps are not used in baseline design
  ANNUAL
    z:DXCrankcaseHtrCap
ENDRULE

// -----------------------------------------------------------------------------
RULE CoilClg:DXCrankcaseHtrCapSim
  DESCRIPTION
    "The capacity of the electric resistance crankcase heater, for simulation."
  INPUTCLASS 
    NotInput
  UNITS 
    Btu/hr
  SIZING_PROPOSED
    if( LocalStatus( DXCrankcaseHtrCap ) > 0 )
    then DXCrankcaseHtrCap * SysCnt
    else UNDEFINED
    endif
  SIZING_BASELINE
    UNDEFINED // Heat pumps are not used in baseline design
  ANNUAL
    z:DXCrankcaseHtrCapSim
ENDRULE


// ********** Heat Rejection for Water-cooled Condenser ************************
RULE NEW CoilClg:HtRejLdSim
  DATATYPE
    Float
  LONGFORM
    HeatRejectionLoadSimulated
  DESCRIPTION
    "The total amount of heat that needs to be dissapated the cooling coil
     condenser."
  INPUTCLASS
    NotInput
  UNITS
    Btu/h
  DEFAULT
    if( IfValidAnd( CndsrType = "WaterSource" ) .AND. CapTotGrossRtdSim > 0 )
    then CapTotGrossRtdSim * ( 1 + ( 3.412 / ValidOr( DXEERAdj, 12 ) ) )
    else 0
    endif
ENDRULE


// added simply to handle backward compatibility for now
RULE CoilClg:AuxPwr
//  DESCRIPTION
//    "The ..."
//  REFERENCE 
//    NACM Section ...
  PREVIOUSNAMES
    AuxilliaryPower
ENDRULE
