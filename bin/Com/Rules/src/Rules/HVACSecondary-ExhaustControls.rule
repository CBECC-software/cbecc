// HVAC Secondary Systems - Exhaust Controls
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012-2014, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  

//  This rule file addresses the following building descriptors:



// ********** Exhaust System Type *********************************************
// =========================== AirSystem ======================================
RULE AirSys:ExhSysType
  DESCRIPTION
    "The type of exhaust system."  
  HELP
    "This input is used to default assumptions for system controls and power. 
     Laboratory, CommercialKitchen, and ParkingGarage exhaust systems can only
     be assigned to ThermalZones comprised of the applicable specific SpaceFunction 
     categories."
  REFERENCE 
    NACM Section 5.7.3.4  
  INPUTCLASS
    Default
  OPTION
    General
    Laboratory
    CommercialKitchen
    ParkingGarage
  DEFAULT
    if( IsExhSys )
    then "General"
    else UNDEFINED
    endif
  SIZING
    if( IsExhSys = 0 )
    then UNDEFINED
    else UNCHANGED
    endif 
ENDRULE
// =========================== ZoneSystem ======================================
RULE ZnSys:ExhSysType
  DESCRIPTION
    "The type of exhaust system."  
  HELP
    "This input is used to default assumptions for system controls and power. 
     Laboratory, CommercialKitchen, and ParkingGarage exhaust systems can only
     be assigned to ThermalZones comprised of the applicable specific SpaceFunction 
     categories."
  INPUTCLASS
    Default
  OPTION
    General
    Laboratory
    CommercialKitchen
    ParkingGarage
  DEFAULT
    if( IsExhSys )
    then "General"
    else UNDEFINED
    endif 
ENDRULE



// ********** Exhaust Operation Mode *******************************************
// =========================== AirSystem ======================================
RULE AirSys:ExhOperMode
  DESCRIPTION
    "Describes whether exahust system availabilty control is interlocked with
     the HVAC system availability, including when the system night cycles."
  INPUTCLASS 
    Default
  OPTION
    DecoupledFromSystem
    CoupledToSystem
  DEFAULT
    if( IsExhSys )
    then
      if( ExhSysType = "Laboratory" )
      then "CoupledToSystem"
      else "DecoupledFromSystem"
      endif
    else UNDEFINED
    endif
ENDRULE
// =========================== ZoneSystem ======================================
RULE ZnSys:ExhOperMode
  DESCRIPTION
    "Describes whether exahust system availabilty control is interlocked with
     the HVAC system availability, including when the system night cycles."
  INPUTCLASS 
    Default
  OPTION
    DecoupledFromSystem
    CoupledToSystem
  DEFAULT
    if( IsExhSys )
    then
      if( ExhSysType = "Laboratory" )
      then "CoupledToSystem"
      else "DecoupledFromSystem"
      endif
    else UNDEFINED
    endif
ENDRULE


// ********** Exhaust Control Type *********************************************
// =========================== AirSystem ======================================
RULE AirSys:ExhCtrlMthd
  DESCRIPTION
    "The type of exhaust control."
  HELP
    "The control is defaulted based on the specification of ExhaustSystemType.
     This property should be coordinated with the specification of Fan:ControlMethod.
     When applicable, it is used in rules to specify the exhaust flow schedule
     or overall, average system power."
  INPUTCLASS 
    Default
//OPTION
//  ConstantFlowConstantSpeedFan
//  VariableFlowConstantSpeedFan
//  VariableFlowVariableSpeedFan
//  NoCOControl
//  COControl   
ENDRULE
// =========================== ZoneSystem ======================================
RULE ZnSys:ExhCtrlMthd
  DESCRIPTION
    "The type of exhaust control."
  HELP
    "The control is defaulted based on the specification of ExhaustSystemType.
     This property should be coordinated with the specification of Fan:ControlMethod.
     When applicable, it is used in rules to specify the exhaust flow schedule
     or overall, average system power."
  INPUTCLASS 
    Default
//OPTION
//  ConstantFlowConstantSpeedFan
//  VariableFlowConstantSpeedFan
//  VariableFlowVariableSpeedFan
//  NoCOControl
//  COControl  
ENDRULE


// ********** Exhaust Flow Schedule Reference **********************************
// =========================== AirSystem ======================================
RULE AirSys:ExhFlowSchRef
  DESCRIPTION
    "The schedule that defines the hourly multiplier on the exhasut flow."
  HELP
    "This schedule is prescibed for Type = CommercialKitchen and Laboratory exhaust
     systems. For General exhaust, the schedule is user defined, but will default 
     to match the HVAC system availabilty schedule for the ThermalZones that the 
     exhaust system serves. For ParkingGarage systems, the schedule is prescibed
     to be constant, and the fan power is adjusted to account for variable speed
     fan control."
  INPUTCLASS
    Prescribed
ENDRULE
// =========================== ZoneSystem ======================================
RULE ZnSys:ExhFlowSchRef
  DESCRIPTION
    "The schedule that defines the hourly multiplier on the exhasut flow."
  HELP
    "This schedule is prescibed for Type = CommercialKitchen and Laboratory exhaust
     systems. For General exhaust, the schedule is user defined, but will default 
     to match the HVAC system availabilty schedule for the ThermalZones that the 
     exhaust system serves. For ParkingGarage systems, the schedule is prescibed
     to be constant, and the fan power is adjusted to account for variable speed
     fan control."
  INPUTCLASS
    Prescribed
ENDRULE



// ---------- Exhaust fan controls at the ThermalZone --------------------------
// These are the actual control specifications actually used for simulation

RULE ThrmlZn:ExhCtrlMthd
  DESCRIPTION
    "Echo of the control method defined at exhaust system referenced by the
     ThermalZone"
  INPUTCLASS
    NotInput
  OPTION
    ConstantFlowConstantSpeedFan
    VariableFlowConstantSpeedFan
    VariableFlowVariableSpeedFan
    NoCOControl
    COControl       
  DEFAULT
    if( ExhFlow > 0 )
    then 
      if( LocalCompAssigned( ExhSysRef ) )
      then
        if( IfValidAnd( ExhSysRef:ExhCtrlMthd != "" ) )
        then ExhSysRef:ExhCtrlMthd
        else "ConstantFlowConstantSpeedFan"
        endif
      else UNDEFINED
      endif
    else UNDEFINED
    endif
  CHECKCODE : T24N
    if( LocalStatus( ExhCtrlMthd ) > 0 .AND. 
        LabArea > 0 .AND.
        DsgnExhACH > 0 )
    then 
      if( ( ExhCtrlMthd = "VariableFlowConstantSpeedFan" .OR. 
            ExhCtrlMthd = "VariableFlowVariableSpeedFan" ) .AND. 
          DsgnExhACH < 10  )
      then 
        PostWarning("The vent/exhaust flow rate for Labortory ThermalZone '%s'
                     must be at least 10 ACH to use variable flow exahust.
                     ConstantFlow exhaust will be simulated.", Name )
      else UNCHANGED
      endif
    else UNCHANGED
    endif  
  SIZING : T24N
    if( BaseSysNum > 0 .AND. IsNewExh != 0 .AND. ExhFlow > 0 )
    then
      if( LabArea > 0 )
      then
        if( Bldg:LabExhFlow > 2000 .AND. DsgnExhACH >= 10  )
        then // Zone design exhaust flow is >= 10 ACH, use variable flow
          "VariableFlowVariableSpeedFan"
        else // Use constant flow/speed exhaust
          "ConstantFlowConstantSpeedFan"
        endif
      else if( CommKitArea > 0 )
      then 
        if( Bldg:TotKitExhHoodFlow > 5000 )
        then // Building kitchen exhaust hood flow is > 5000, 
             // use variable flow/speed exhaust
          "VariableFlowVariableSpeedFan"
        else // Use constant flow/speed exhaust
          "ConstantFlowConstantSpeedFan" 
        endif
      else if( PrkgGarArea > 0 )
        then
        if( Bldg:PrkgGarExhFlow > 10000 )
        then // Building parking garage flow is > 10000, use CO control
          "COControl"
        else // No CO control
          "NoCOControl"
        endif
      else "ConstantFlowConstantSpeedFan" // Default assumption
      endif endif endif   
    else if( LabArea > 0 .AND. DsgnExhACH < 10  )
    then "ConstantFlowConstantSpeedFan" // Use constant for labs < 10ACH
    else u:ExhCtrlMthd // Control is same as proposed
    endif endif
  SIZING : S901G ECBC
    u:ExhCtrlMthd
  ANNUAL
    z:ExhCtrlMthd
ENDRULE

RULE ThrmlZn:ExhOperMode
  DESCRIPTION
    "Echo of the operation mode defined at exhaust system referenced by the
     ThermalZone"
  INPUTCLASS 
    NotInput
  OPTION
    DecoupledFromSystem
    CoupledToSystem
  DEFAULT
    if( ExhFlow > 0 )
    then 
      if( LocalCompAssigned( ExhSysRef ) )
      then ExhSysRef:ExhOperMode
      else "DecoupledFromSystem"
      endif
    else UNDEFINED
    endif
  SIZING : T24N
    if( BaseSysNum > 0 .AND. IsNewExh != 0 .AND. ExhFlow > 0 )
    then // Use Baseline HVAC rules
      if( ( CommKitArea > 0 .OR. LabArea > 0 ) .AND. 
          ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
      then "CoupledToSystem"
      else "DecoupledFromSystem"
      endif
    else if( ExhFlow > 0 )
    then 
      if( ( CommKitArea > 0 .OR. LabArea > 0 ) .AND.
          ExhCtrlMthd = "VariableFlowVariableSpeedFan" .AND. 
          VentSysRef:IsVAVSys )
      then // Under these conditions, the user can specify any operation mode
        u:ExhOperMode
      else // Prescribed to be decoupled
        "DecoupledFromSystem"
      endif
    else UNDEFINED
    endif endif
  SIZING : S901G ECBC
    u:ExhOperMode
  ANNUAL
    z:ExhOperMode
ENDRULE

RULE ThrmlZn:ExhFlowSchRef
  DESCRIPTION
    "Echo of the flow schedule defined at exhaust system referenced by the
     ThermalZone"
  HELP
    "If a schedule is defined, the flow rate that the fan operates will be this
     fraction times the maximum flow rate. This allows a variable speed exhaust
     fan to be modeled according to a schedule.
     
     For compliance analysis, the exhaust flow schedule is prescribed for process
     spaces, and otherwise assumed to run continuous when according to the HVAC
     system availabilty schedule."
  INPUTCLASS 
    Prescribed
  DEFAULT
    if( LocalCompAssigned( ExhSysRef ) )
    then ExhSysRef:ExhFlowSchRef
    else UNDEFINED
    endif
  SIZING_PROPOSED
    if( ExhFlow > 0 )
    then
      if( LabArea > 0 )
      then // Is commercial kitchen, define schedule based on control method
        if( ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
        then RuleLibrary( Schedule, "LabExhaustHoodVAV" )
        else RuleLibrary( Schedule, "LabExhaustHoodCAV" )
        endif
      else if ( CommKitArea > 0 )
      then // Is commercial kitchen, define schedule based on control method
        if( ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
        then RuleLibrary( Schedule, "RestaurantExhaustHoodGreaterThan5000cfm" )
        else RuleLibrary( Schedule, "RestaurantExhaustHoodEqualOrLessThan5000cfm" )
        endif
      else if( PrkgGarArea > 0 )
      then // Is parking garage, fan power is prescribed and it is based on 24/7 operation
        RuleLibrary( Schedule, "On" )
      else if( Proj:IsNoMech .OR. ( Proj:IsNoAddMech .AND. IfValidAnd( IsExistingHVAC <= 0 ) ) )
      then // Is Partial compliance w/ no mechanical (complete or addition)
        if( VentSysIsExh = 2 )
        then // Is Res and baseline ventilation system is an exhaust fan, prescribe 24/7/100% flow
          RuleLibrary( Schedule, "On" )
        else // Exhaust is prescribed for Labs and CommKit (see above), but otherwise is not simulated
          UNDEFINED 
        endif
  // TO DO: Consider addding this check to make sure the proposed schedule for exhaust-based vent
  // system is reasonable.
  ;   else if( VentSysIsExh > 0 .AND. 
  ;            ( ExhFlow < VentFlow * Proj:ExhVentMultLim ) .AND. 
  ;            ( ExhFlow < VentFlow / Proj:ExhVentMultLim ) )
  ;   then // Ventilation system is an exhaust fan, prescribe 24/7/100% flow if withing 20% 
  ;        // of ventilation requirement.
  ;     RuleLibrary( Schedule, "On" )
      else // Use user defined exhaust schedule
        u:ExhFlowSchRef
      endif endif endif endif ;endif
    else UNDEFINED
    endif
  SIZING_BASELINE
    if( ExhFlow > 0 )
    then
      if( LabArea > 0 )
      then // Is commercial kitchen, define schedule based on control method
        if( ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
        then RuleLibrary( Schedule, "LabExhaustHoodVAV" )
        else RuleLibrary( Schedule, "LabExhaustHoodCAV" )
        endif
      else if ( CommKitArea > 0 )
      then // Is commercial kitchen, define schedule based on control method
        if( ExhCtrlMthd = "VariableFlowVariableSpeedFan" )
        then RuleLibrary( Schedule, "RestaurantExhaustHoodGreaterThan5000cfm" )
        else RuleLibrary( Schedule, "RestaurantExhaustHoodEqualOrLessThan5000cfm" )
        endif
      else if( PrkgGarArea > 0 )
      then // Is parking garage, fan power is prescribed and it is based on 24/7 operation
        RuleLibrary( Schedule, "On" )
      else if( VentSysIsExh = 2 )
      then // Is Res and baseline ventilation system is an exhaust fan, prescribe 24/7/100% flow
        RuleLibrary( Schedule, "On" )
      else // Baseline exhaust schedule same as proposed
        UNCHANGED
      endif endif endif endif
    else UNDEFINED
    endif
  ANNUAL
    z:ExhFlowSchRef
ENDRULE

RULE ThrmlZn:ExhBalancedSchRef
  DESCRIPTION
    "A fractional schedule that defines how much of the exhaust air flow is
     balanced by the simple air flows, such as infiltration, ventilation, or 
     transfer air provided to the zone."
  HELP
    "If not specified or set to 0, then all the exhaust air flow is assumed to be
     not provided by (unbalanced) simple airflows. Unbalanced exhaust is then
     modeled as being provided by the central air system. Balanced exhaust 
     assumes make-up air is provided by simple airflows and does not impact the
     central air system return air or outdoor air flow rates."
  INPUTCLASS 
    Prescribed
  SIZING
    if( ExhFlow > 0 )
    then
      if( ExhOperMode = "CoupledToSystem" ) 
      then // Based on user specification of ExhOperMode
        RuleLibrary( Schedule, "Off" )
      else // Unbalanced for all other systems
        RuleLibrary( Schedule, "On" )
      endif
    else UNDEFINED
    endif
  ANNUAL
    z:ExhBalancedSchRef
ENDRULE

// This property is not currently used
RULE ThrmlZn:ExhMinZnTempSchRef 
  DESCRIPTION
    "A temperature schedule that defines when the exhaust fan will turn on."
  HELP
    "The fan control will be based on a comparison between the current zone 
     air temperature and the schedule values. If the zone is warmer than the 
     scheduled limit, then the fan will operate.

     This property is not currently used, but is supported by the SDD->OS->E+
     translator"
  INPUTCLASS 
    NotInput
ENDRULE

RULE NEW ThrmlZn:IsVarFlowExh
  DATATYPE
    Integer
  LONGFORM
    IsVariableFlowExhaust
  INPUTCLASS
    NotInput
  SIZING
    if( ExhFlow > 0 .AND. 
        ExhOperMode = "CoupledToSystem" .AND.
        ( IfValidAnd( ExhCtrlMthd = "VariableFlowConstantSpeedFan" ) .OR.
          IfValidAnd( ExhCtrlMthd = "VariableFlowVariableSpeedFan" ) ) )
    then 1
    else UNDEFINED
    endif
ENDRULE


