// Opaque Envelope - Floors
//
// -------------------------------------------------------------------------
//  Copyright (c) 2013, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
//
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//

// According to 5.5.5 ACM May 30, 2013, items:
//     Floor Name
//     Floor Type
//     Floor Geometry
//     Floor Construction
//
// The main objects in the SDD are:
//     Proj:Bldg:Story:Spc:ExtFlr
//     Proj:Bldg:Story:Spc:ExtFlr:ConsAssmRef
//     Proj:Bldg:Story:Spc:ExtFlr:ExtRoughness
//     Proj:Bldg:Story:Spc:ExtFlr:ExtSolAbs
//     Proj:Bldg:Story:Spc:ExtFlr:ExtThrmlAbs
//     Proj:Bldg:Story:Spc:ExtFlr:ExtVisAbs
//     Proj:Bldg:Story:Spc:ExtFlr:IntSolAbs
//     Proj:Bldg:Story:Spc:ExtFlr:IntThrmlAbs
//     Proj:Bldg:Story:Spc:ExtFlr:IntVisAbs
//     Proj:Bldg:Story:Spc:ExtFlr:Area
//     Proj:Bldg:Story:Spc:ExtFlr:Az
//     Proj:Bldg:Story:Spc:ExtFlr:Tilt
//     Proj:Bldg:Story:Spc:ExtFlr:Status
//


// -------------- Floor Name --------------------
// Input restriction summary:
//      None
// Baseline rule summary:
//      If provided, same for all transformations.
//
RULE ExtFlr:Name
  DESCRIPTION
    "A unique name that identifies the wall."
  HELP
    "A unique name or code that relates the exterior wall to the design documents. This is
    an optional input since there are other acceptable ways to key surfaces to the construction
    documents."
  REFERENCE
    ACM 5.5.5
  INPUTCLASS
    Required
  // No transformations are needed since already copied initially.
ENDRULE


//*********************************************************************************
//****************** Temporary ****************************************************
//*********************************************************************************
// To Do Remove this rule.  It fixes Status as New for ExtWall
// There does not seem to be a status selection in CBECC at the Space:Surface level
//*********************************************************************************
// -------------- Exterior Floor Status -----------------------------
RULE ExtFlr:Status
  DESCRIPTION
    "This sets the Exterior Floor construction status to New.  There will be an option
     to select New, ExistingAlteration, ExistingUnchanged, Removed."
  HELP
    ""
  INPUTCLASS
    Required
  OPTION
    New
    Existing
    Altered
  DEFAULT
    Parent(EnvStatus)
  CHECKCODE
    if( Proj:IsNewEnv ) then
      if( Status != "New" ) then
        PostWarning( "Floor '%s':  Status '%s' is incompatible with 
                      Compliance Type '%s'.  Status will be treated as
                      'New' for simulation.", Name, Status, Proj:CompType )
      else UNCHANGED
      endif
    else if( Proj:IsExistingEnv) then
      if( Status != "Existing" ) then
        PostWarning( "Floor '%s':  Status '%s' is incompatible with 
                      Compliance Type '%s'.  Status will be treated as
                      'Existing' for simulation.", Name, Status, Proj:CompType )
      else UNCHANGED
      endif
    else if( Proj:IsAdd ) then
      if( Status = "Altered" ) then
        PostWarning( "Floor '%s':  Status '%s' is incompatible with 
                      Compliance Type '%s'.  Status will be treated as
                      'New' for simulation.", Name, Status, Proj:CompType )
      else UNCHANGED
      endif
    else if( Proj:IsAltOnly ) then
      if( Status = "New" ) then
        PostWarning( "Floor '%s':  Status '%s' is incompatible with 
                      Compliance Type '%s'.  Status will be treated as
                      'Altered' for simulation.", Name, Status, Proj:CompType )
      else UNCHANGED
      endif
    else UNCHANGED
    endif endif endif endif
  SIZING
    if( Proj:IsNewEnv ) then "New"
    else if( Proj:IsExistingEnv ) then "Existing"
    else if( Proj:IsAdd .AND. u:Status = "Altered" ) then "New"
    else if( Proj:IsAltOnly .AND. u:Status = "New" ) then "Altered"
    else u:Status
    endif endif endif endif
  ANNUAL
    z:Status
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:IsNew
  DATATYPE 
    Integer
  LONGFORM
    IsNew
  DESCRIPTION
    "Flag to identify whether the status of the surface is New"  
  INPUTCLASS
    NotInput
  DEFAULT 
    if( Proj:IsNewEnv ) then 1
    else if( Proj:IsExistingEnv ) then 0
    else if( Proj:IsAdd .AND. u:Status = "Altered" ) then 1
    else if( Proj:CompType = "ExistingAlteration" ) then 0
    else if( u:Status = "New") then 1
    else 0
    endif endif endif endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:IsAltered
  DATATYPE 
    Integer
  LONGFORM
    IsAltered
  DESCRIPTION
    "Flag to identify whether the status of the surface is Altered"  
  INPUTCLASS
    NotInput
  DEFAULT 
    if( Proj:IsNewEnv ) then 0
    else if( Proj:IsExistingEnv ) then 0
    else if( Proj:IsAdd ) then 0
    else if( Proj:CompType = "ExistingAlteration" .AND. u:Status = "New" ) then 1
    else if( u:Status = "Altered" ) then 1
    else 0
    endif endif endif endif endif
ENDRULE

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:IsExisting
  DATATYPE 
    Integer
  LONGFORM
    IsExisting
  DESCRIPTION
    "Flag to identify whether the status of the surface is Existing"  
  INPUTCLASS
    NotInput
  DEFAULT 
    if( Proj:IsNewEnv ) then 0
    else if( Proj:IsExistingEnv ) then 1
    else if( u:Status = "Existing" ) then 1
    else 0
    endif endif endif
ENDRULE

// -------------- Determine the name of the exterior floor construction ---------
RULE ExtFlr:ConsAssmRef
  DESCRIPTION
    "Construction assembly reference (construction name) for an exterior floor."
  HELP
    "A reference to a construction assembly. "
  INPUTCLASS
    Required
  REFERENCE
    ACM 5.5.5
    App5.5C-Table5.5.1
    JA4-Table4.2.7
  DEFAULT
    ConsAssmRef
  CHECKSIM
    if( LocalCompAssigned(ConsAssmRef) = 0 ) then
      PostError("A valid construction assembly must be assigned to surface '%s'
                 in space '%s'.",Name,Spc:Name)
    else UNCHANGED
    endif
  SIZING_PROPOSED
    u:ConsAssmRef
  SIZING_BASELINE
//  See below for baselinesizing
  ANNUAL_PROPOSED
    u:ConsAssmRef
  ANNUAL_BASELINE
//  See below for baseline
ENDRULE



// -------------- Floor Heat Capacity -------------------------------------------
RULE NEW ExtFlr:HtCapCat
  RULESETS
    T24N
  DATATYPE
    ENUMERATION
  DESCRIPTION
    "The category for the heat capacity of the floor: Mass Floor or Other
    Floor. Based on threshold of 5 Btu/ft²-F derived from 3in of lightweight
    concrete with density of 100 lb/ft³ and specific heat of 0.2 Btu/lb-F."
  HELP
    ""
  INPUTCLASS
    NotInput
  OPTION
    Mass
    Other
    // Another floor type is a Heated Slab Floor that is not being included yet
  DEFAULT
    // HC>=5  is Mass
    // 5>HC   is Other
    if( ConsAssmRef:SumMatHeatCap >= 5 ) then
      "Mass"
    else
      "Other"
    endif
  SIZING
    "Other"
  ANNUAL_PROPOSED
    u:HtCapCat
  ANNUAL_BASELINE
    z:HtCapCat
ENDRULE


// -------------- Floor Construction Type ---------------------------------------
RULE NEW ExtFlr:ConsType
  RULESETS
    T24N
  DATATYPE
    Enumeration
  DESCRIPTION
    "Defines the floor construction type. "
  HELP
    "One of three categories of exposed floor assemblies used to determine
    minimum insulation requirements. The floor type categories are: Mass
    Floors, Other Floors and Heated Slab Floors."
  REFERENCE
    T24 Sect. 120.7(c)
    ACM 5.5.5
  LONGFORM
    ConstructType
  OPTION
    MassFloor
    OtherFloor
//    HeatedSlabFloor  *** This Option is located in OpaqueEnvelope-UndergroundSurfaces.rule ***
  INPUTCLASS
    NotInput
  DEFAULT
    // HC>=5 is mass
    // 5>HC  is other
    if( LocalCompAssigned(ConsAssmRef) = 0 ) then UNCHANGED
    else if( HtCapCat = "Mass" ) then
      "MassFloor"
    else
      "OtherFloor"
    endif endif
  SIZING_PROPOSED
    u:ConsType
  SIZING_BASELINE
    if( Proj:IsExistingEnv ) then
      u:ConsType
    else if( u:Proj:CompType = "ExistingAlteration" .OR.
             u:Proj:CompType = "ExistingAdditionAndAlteration" ) then
      if( Status = "Existing" .OR. 
          Status = "Altered" ) then
        u:ConsType
      else "OtherFloor"
      endif
    else "OtherFloor"
    endif endif
  ANNUAL
    z:ConsType
ENDRULE


// -------------- Floor Construction --------------------
// Input restriction summary:
//      The construction assembly, defined by a series of layers, must be equal to or more
//      efficient than the mandatory U-factor requirements of Section 120.7 of the Standards
//      for new construction, and Section 141.0 of the Standards for alterations.  Each layer
//      specified must be listed in the materials database in the ACM Appendix 5.5A.
//      Appropriate R-values for insulation can be calculated using the formula below.
//
//         Rinsulation = (1/U-factor) – Rlayer(1) – Rlayer(2) – Rlayer(3) – Rlayer(n)
//         Rinsulation = Rins.continuous + Rins.framing
//
// Baseline rule summary:
//      For new construction, the standard design wall type is metal-framed with continuous
//      insulation as described in Table 4.3.3 of Reference Appendix JA4.  The U-factor required
//      for wall construction of the standard design building is defined in Table 140.3-B, C or
//      D of the Standards.

// -------------- Building Type - Res, Relocatable School, NonRes --------------
RULE NEW ExtFlr:BldgTypeForExtFlr
  DATATYPE
    Enumeration
  DESCRIPTION
    "Space type used when determining the exterior wall u-factor"
  HELP
    ""
  OPTION : S901G ECBC
    Residential
    Semiheated
    NonResidential
  OPTION
    Residential
    RelocClass
    NonResidential
  REFERENCE
    ACM 5.5.5
  INPUTCLASS
    NotInput
  DEFAULT : S901G ECBC
    if( Spc:Semiheated = 1 )
    then "Semiheated"
    else if( Spc:OccClass = "Residential" )
//            Spc:SpcFunc = "Courthouse/Police Station/Penitentiary - Confinement Cells"
//       .OR. Spc:SpcFunc = "Dormitory - Living Quarters"
//       .OR. Spc:SpcFunc = "Fire Stations - Sleeping Quarters"
//       .OR. Spc:SpcFunc = "Hospital - Nursery"
//       .OR. Spc:SpcFunc = "Hospital - Patient Room" 
//       .OR. Spc:SpcFunc = "Hotel/Highway Lodging - Hotel Guest Rooms"
//       .OR. Spc:SpcFunc = "Hotel/Highway Lodging - Highway Lodging Guest Rooms")
    then "Residential"
    else "NonResidential"
    endif endif
  DEFAULT
    if( Bldg:RelocPubSchoolBldg = 1 ) then
      "RelocClass"
    else if( Spc:SpcFunc = "Unoccupied-Include in Gross Floor Area" .OR.
             Spc:SpcFunc = "Unoccupied-Exclude from Gross Floor Area" ) then
      if( Spc:OccClass = "Residential" ) then
        "Residential"
      else "NonResidential"
      endif
    else if( Spc:SpcFunc = "High-Rise Residential Living Spaces" .OR.
             Spc:SpcFunc = "Hotel/Motel Guest Room" ) then
      "Residential"
    else "NonResidential"
    endif endif endif
  SIZING
    u:BldgTypeForExtFlr
  ANNUAL
    z:BldgTypeForExtFlr
ENDRULE


// -------------- Calculate Floor U-Factor -------------------------------------
RULE NEW ExtFlr:OverallUFactor
  DATATYPE
    Float
  DESCRIPTION
    "The overall computed u-factor"
  INPUTCLASS
    NotInput
  DEFAULT
    ConsAssmRef:UFactor
  SIZING
    ConsAssmRef:UFactor
  ANNUAL
    z:ConsAssmRef:UFactor
ENDRULE


// -------------- Check for Mandatory Floor U-Factor Compliance ----------------
RULE NEW ExtFlr:NonCompIns
  RULESETS
    T24N
  DATATYPE
    Float
  LONGFORM
    NonCompliantInsulation
  INPUTCLASS
    NotInput
  DESCRIPTION
    "Flags floors that do not comply with the requirements of Section 120.7,
    Mandatory Insulation Requirements."
  HELP
    ""
  REFERENCE
    T24-Sec120.7
  CHECKCODE
    if( LocalCompAssigned(ConsAssmRef) = 0 ) then 
      UNCHANGED
    else if( LocalStatus(ConsType) < 1 ) then
      PostError("A valid construction assembly must be assigned to surface '%s'
                 in space '%s'.",Name,Spc:Name)
    else
      switch (Status)
        case "New":
          switch (ConsType)
            case "MassFloor":
              if( OverallUFactor > Proj:MassFlrMaxUFac[1] + 0.001 ) then 1
              else 0
              endif
            case "OtherFloor":
              if( OverallUFactor > Proj:OtherFlrMaxUFac[1] + 0.001 ) then 1
              else 0
              endif
            default:
              PostError("Unknown floor construction type for floor '%s'",Name)
          endswitch
        case "ExistingAlteration":
          switch (ConsType)
            case "MassFloor":
              if( BldgTypeForExtFlr = "Residential" .AND.
                  OverallUFactor > Proj:AltMassFlrMaxUFac[1] + 0.001 ) then 1
              else 0
              endif
            case "OtherFloor":
              if( OverallUFactor > Proj:OtherFlrMaxUFac[2] + 0.001 ) then 1
              else 0
              endif
            default:
              0
          endswitch
        default:
          0
      endswitch
    endif endif
ENDRULE


// -------------- Determine Baseline Floor U-Factor ----------------------------
RULE NEW ExtFlr:MaxUFactor
  DATATYPE
    Float
  DESCRIPTION
    "Baseline exposed floor U-Factor set using prescriptive tables 140.3-B,C,D"
  HELP
    ""
  REFERENCE
    ACM 5.5.5
    Table 140.3-B
    Table 140.3-C
    Table 140.3-D
  UNITS
    Btu/h-ft2-F
  INPUTCLASS
    NotInput
  SIZING : S901G ECBC
    ExteriorFloorUFactor_S901G:UFactor("BldgType",BldgTypeForExtFlr, "ClimateZone",u:Project:CliZn)
  SIZING
    ExteriorFloorUFactor:UFactor("BldgType",BldgTypeForExtFlr, "TypeOfCons",ConsType, "ClimateZone",u:Project:CliZnNum)
  ANNUAL_PROPOSED
    UNCHANGED
  ANNUAL_BASELINE
    z:MaxUFactor
ENDRULE


// ============================================================================================================================================================================
TABLE ExteriorFloorUFactor_S901G
    BldgType                  UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor
    ClimateZone=              1A      1B      2A      2B      3A      3B      3C      4A      4B      4C      5A      5B      5C      6A      6B      7       8
    NonResidential            0.35    0.35    0.052   0.052   0.052   0.052   0.052   0.038   0.038   0.038   0.038   0.038   0.038   0.038   0.038   0.038   0.032
    Residential               0.35    0.35    0.052   0.052   0.052   0.052   0.052   0.038   0.038   0.038   0.038   0.038   0.038   0.032   0.032   0.032   0.032
    Semiheated                0.35    0.35    0.069   0.069   0.069   0.069   0.069   0.069   0.069   0.069   0.052   0.052   0.052   0.052   0.052   0.052   0.052
ENDTABLE
// ============================================================================================================================================================================

// ============================================================================================================================================================================
// Table 140.3-B Prescriptive Envelope Criteria For Nonresidential Buildings
// Table 140.3-C Prescriptive Envelope Criteria For High-Rise Residential Buildings And Guest Rooms Of Hotel/Motel Buildings
// Table 140.3-D Prescriptive Envelope Criteria For Relocatable Public School Buildings Where Manufacturer Certifies for Use In All Climate Zones
//   Baseline for new construction is always OtherFloor, but for alterations the baseline can be any type
TABLE ExteriorFloorUFactor
    BldgType         TypeOfCons         UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor UFactor
    ClimateZone=                        1       2       3       4       5       6       7       8       9       10      11      12      13      14      15      16
    NonResidential   MassFloor          0.092   0.092   0.269   0.269   0.269   0.269   0.269   0.269   0.269   0.269   0.092   0.092   0.092   0.092   0.092   0.058
    NonResidential   OtherFloor         0.048   0.039   0.071   0.071   0.071   0.071   0.071   0.071   0.071   0.071   0.039   0.071   0.071   0.039   0.039   0.039
    Residential      MassFloor          0.045   0.045   0.058   0.058   0.058   0.069   0.092   0.092   0.092   0.069   0.058   0.058   0.058   0.045   0.058   0.037
    Residential      OtherFloor         0.034   0.034   0.039   0.039   0.039   0.039   0.071   0.039   0.039   0.039   0.039   0.039   0.039   0.034   0.039   0.034
    RelocClass       MassFloor          0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048
    RelocClass       OtherFloor         0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048   0.048
ENDTABLE
// ============================================================================================================================================================================



// -------------- Determine the name of the exterior floor construction ---------
RULE ExtFlr:ConsAssmRef
  DESCRIPTION
    "Construction assembly reference (construction name) for an exterior floor."
  HELP
    "A reference to a construction assembly. "
  REFERENCE
    ACM 5.5.5
    App5.5C-Table5.5.1
    JA4-Table4.2.7
  DEFAULT
//   See above for Default
  CHECKSIM
//   See above for Checksim
  SIZING_PROPOSED
//   See above for Proposed
  SIZING_BASELINE : S901G ECBC
    switch (Status)
      case "New":
        if( Spc:NotEnclosed = 1 )
        then zp:ConsAssmRef
        else
          if( Spc:CondgType = "Unconditioned" ) then
            RuleLibrary(ConsAssm,"FloorSteelJoistUnconditioned")
          else
            switch (MaxUFactor)
              case 0.35:
                RuleLibrary(ConsAssm,"FloorSteelJoistU0.35")
              case 0.069:
                RuleLibrary(ConsAssm,"FloorSteelJoistU0.069")
              case 0.052:
                RuleLibrary(ConsAssm,"FloorSteelJoistU0.052")
              case 0.038:
                RuleLibrary(ConsAssm,"FloorSteelJoistU0.038")
              case 0.032:
                RuleLibrary(ConsAssm,"FloorSteelJoistU0.032")
              default:
                RuleLibrary(ConsAssm,"FloorSteelJoistU0.032")
            endswitch
          endif
	endif
      default:
        RuleLibrary(ConsAssm,"FloorSteelJoistUnconditioned")
    endswitch
  SIZING_BASELINE
    if( Spc:CondgType = "Unconditioned" ) then
      RuleLibrary(ConsAssm,"OtherFloorUnconditioned")
    else
      switch (Status)
        case "New":
//          RuleLibrary(ConsAssm, MetalFrameWallConstruct:AssemCons("UFact",MaxUFactor))
          switch (MaxUFactor)
            case 0.048:
              RuleLibrary(ConsAssm,"OtherFloorU048")
            case 0.039:
              RuleLibrary(ConsAssm,"OtherFloorU039")
            case 0.071:
              RuleLibrary(ConsAssm,"OtherFloorU071")
            default:
              RuleLibrary(ConsAssm,"OtherFloorU071")
          endswitch
        case "Altered":
          switch (u:ConsType)
            case "MassFloor":
              RuleLibrary(ConsAssm,
                          MassFloorConstruct:AssemCons("UFact",MaxUFactor))
            case "MetalFrameWall":
              RuleLibrary(ConsAssm,
                          OtherFloorConstruct:AssemCons("UFact",MaxUFactor))
            default:
              RuleLibrary(ConsAssm,
                          OtherFloorConstruct:AssemCons("UFact",MaxUFactor))
          endswitch
        case "Existing":
           u:ConsAssmRef
        default:
          RuleLibrary(ConsAssm,
                          OtherFloorConstruct:AssemCons("UFact",MaxUFactor))
      endswitch
    endif
  ANNUAL_PROPOSED
//   See above for Proposed
  ANNUAL_BASELINE
    z:ConsAssmRef
ENDRULE


// ===============================================
TABLE OtherFloorConstruct
   UFact    AssemCons
   <0.0341  "OtherFloorU034"
   <0.0391  "OtherFloorU039"
   <0.0481  "OtherFloorU048"
   <0.0711  "OtherFloorU071"
ENDTABLE

// ===============================================
TABLE MassFloorConstruct
   UFact    AssemCons
   <0.0371  "MassFloorU037"
   <0.0451  "MassFloorU045"
   <0.0581  "MassFloorU058"
   <0.0691  "MassFloorU069"
   <0.0921  "MassFloorU092"
   <0.2691  "MassFloorU269"
ENDTABLE
// ===============================================


// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
// ------- Calculate area by Status - ExtFlr
RULE NEW ExtFlr:AreaNew
  DESCRIPTION
    "The surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:AreaAltered
  DESCRIPTION
    "The surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Altered" ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:AreaExisting
  DESCRIPTION
    "The surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Existing" ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
// -----------------------------------------------------------------------------
// ------- Calculate area by Status and BldgType - ExtFlr
RULE NEW ExtFlr:AreaNewNonResCond
  DESCRIPTION
    "The NonResidential surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .AND. BldgTypeForExtFlr = "NonResidential" .AND.
      ( Parent(CondgType) = "DirectlyConditioned" .OR. 
        Parent(CondgType) = "IndirectlyConditioned" .OR. 
        Parent(CondgType) = "Plenum" ) ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:AreaNewResCond
  DESCRIPTION
    "The Residential surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .AND. BldgTypeForExtFlr = "Residential" .AND.
      ( Parent(CondgType) = "Conditioned" .OR. 
        Parent(CondgType) = "IndirectlyConditioned" .OR. 
        Parent(CondgType) = "Plenum" ) ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:AreaNewRelocCond
  DESCRIPTION
    "The Relocatable Classroom surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .AND. BldgTypeForExtFlr = "RelocClass" .AND.
      ( Parent(CondgType) = "Conditioned" .OR. 
        Parent(CondgType) = "IndirectlyConditioned" .OR. 
        Parent(CondgType) = "Plenum" ) ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
// ------- Calculate area by Status and BldgType - ExtFlr
RULE NEW ExtFlr:AreaAltNonResCond
  DESCRIPTION
    "The NonResidential surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Altered" .AND. BldgTypeForExtFlr = "NonResidential" .AND.
      ( Parent(CondgType) = "Conditioned" .OR. 
        Parent(CondgType) = "IndirectlyConditioned" .OR. 
        Parent(CondgType) = "Plenum" ) ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:AreaAltResCond
  DESCRIPTION
    "The Residential surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Altered" .AND. BldgTypeForExtFlr = "Residential" .AND.
      ( Parent(CondgType) = "Conditioned" .OR. 
        Parent(CondgType) = "IndirectlyConditioned" .OR. 
        Parent(CondgType) = "Plenum" ) ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// -----------------------------------------------------------------------------
RULE NEW ExtFlr:AreaAltRelocCond
  DESCRIPTION
    "The Relocatable Classroom surface area by status."
  UNITS
    ft2
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "Altered" .AND. BldgTypeForExtFlr = "RelocClass" .AND.
      ( Parent(CondgType) = "Conditioned" .OR. 
        Parent(CondgType) = "IndirectlyConditioned" .OR. 
        Parent(CondgType) = "Plenum" ) ) then
      Area
    else
      UNCHANGED
    endif
ENDRULE    

// ----------------- Exposed Floor Exterior Solar Absorptance ------------------
RULE ExtFlr:ExtSolAbs
  DESCRIPTION
    "The fraction of incident solar radiation on the floor which is absorbed."
  HELP
    "The fraction of incident solar radiation on the floor which is absorbed.
    Solar radiation includes the visible spectrum as well as infrared and
    ultraviolet wavelengths."
  REFERENCE
    ACM-5.5.1 (possibly being moved to 5.5.3)
  INPUTCLASS
    NotInput
  MINIMUM
    0
  MAXIMUM
    1
  DEFAULT
    0.7         // 1 - 0.3
  SIZING
    0.7
  ANNUAL
    z:ExtSolAbs
ENDRULE


// ----------------- Exposed Floor Exterior Thermal Absorptance ----------------
RULE ExtFlr:ExtThrmlAbs
  DESCRIPTION
    "The fraction of incident long wavelength radiation on the floor which is
    absorbed."
  HELP
    "The fraction of incident long wavelength radiation on the floor which is
    absorbed."
  REFERENCE
    ACM-5.5.1 (possibly being moved to 5.5.3)
  INPUTCLASS
    NotInput
  MINIMUM
    0
  MAXIMUM
    1
  DEFAULT
    0.9
  SIZING
    0.9
  ANNUAL
    z:ExtThrmlAbs
ENDRULE


// ----------------- Exposed Floor Exterior Visible Absorptance ----------------
RULE ExtFlr:ExtVisAbs
  DESCRIPTION
    "The fraction of incident visible wavelength radiation on the floor which is
    absorbed."
  HELP
    "The fraction of incident visible wavelength radiation on the floor which is
    absorbed."
  REFERENCE
    ACM-5.5.1 (possibly being moved to 5.5.3)
  INPUTCLASS
    NotInput
  MINIMUM
    0
  MAXIMUM
    1
  DEFAULT
    0.8
  SIZING
    0.8
  ANNUAL
    z:ExtVisAbs
ENDRULE

// ----------------- Exposed Floor Exterior Surface Roughness ------------------
RULE ExtFlr:ExtRoughness
  DESCRIPTION
    "The relative roughness of the exterior surface.  The roughness affects
    convective heat transfer of the surface."
  HELP
    "The relative roughness of the exterior surface.  The roughness affects
    convective heat transfer of the surface."
  REFERENCE
    ACM-5.11.10 (possibly being moved to 5.5.3)
  INPUTCLASS
    NotInput
  OPTION
    VeryRough
    Rough
    MediumRough
    MediumSmooth
    Smooth
    VerySmooth
  DEFAULT
    MediumRough
;    ConsAssmRef:ExtRoughness
  SIZING
    MediumRough
  ANNUAL
    z:ExtRoughness
ENDRULE


// ----------------- Exposed Floor Interior Solar Absorptance ------------------
RULE ExtFlr:IntSolAbs
  DESCRIPTION
    "The fraction of incident solar radiation on the floor which is absorbed."
  HELP
    "The fraction of incident solar radiation on the floor which is absorbed.
    Solar radiation includes the visible spectrum as well as infrared and
    ultraviolet wavelengths."
  REFERENCE

  INPUTCLASS
    NotInput
  MINIMUM
    0
  MAXIMUM
    1
  DEFAULT
    0.7         // 1 - 0.3
  SIZING
    0.7
  ANNUAL
    z:IntSolAbs
ENDRULE

// ----------------- Exposed Floor Interior Thermal Absorptance ----------------
RULE ExtFlr:IntThrmlAbs
  DESCRIPTION
    "The fraction of incident long wavelength radiation on the floor which is
    absorbed."
  HELP
    "The fraction of incident long wavelength radiation on the floor which is
    absorbed."
  REFERENCE

  INPUTCLASS
    NotInput
  MINIMUM
    0
  MAXIMUM
    1
  DEFAULT
    0.9
  SIZING
    0.9
  ANNUAL_PROPOSED
    u:IntThrmlAbs
  ANNUAL_BASELINE
    z:IntThrmlAbs
ENDRULE

// ----------------- Exposed Floor Interior Visible Absorptance ----------------
RULE ExtFlr:IntVisAbs
  DESCRIPTION
    "The fraction of incident visible wavelength radiation on the floor which is
    absorbed."
  HELP
    "The fraction of incident visible wavelength radiation on the floor which is
    absorbed."
  REFERENCE

  INPUTCLASS
    NotInput
  MINIMUM
    0
  MAXIMUM
    1
  DEFAULT
    0.9         // Change to 0.8 once format tests are complete
  SIZING
    0.9         // Change to 0.8 once format tests are complete
  ANNUAL_PROPOSED
    u:IntVisAbs
  ANNUAL_BASELINE
    z:IntVisAbs
ENDRULE
