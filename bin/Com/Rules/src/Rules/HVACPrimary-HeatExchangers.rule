// HVAC Secondary Systems - Heat Exchangers
//
// -------------------------------------------------------------------------
//  Copyright (c) 2012, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
//
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALifORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN if
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------

//  This rule file addresses the following building descriptors:
//      Heat Exchanger Type
//      Heat Exchanger Rated Capacity
//      Heat Exchanger UA Value
//      Waterside Economizer Integration type
//      Chilled Water Supply Temperature
//      Chilled Water Return Temperature
//      Condenser Water Supply Temperature
//      Condenser Water Return Temperature
//      Chilled Water Flow Rate
//      Condenser Water Flow Rate
//      ..

// -----------------------------------------------------------------------------
// ********** <Heat Exchanger Capacity>
RULE HX:Type
  DESCRIPTION
    "The heat exchanger configuration."
  INPUTCLASS
    Default
  OPTION
    CounterFlow
//    ParallelFlow
//    CrossFlowUnmixed
//    CrossFlowMixed
//    CrossFlowSupplyMixed
//    CrossFlowDemandMixed
  DEFAULT
    "CounterFlow"
ENDRULE


// -----------------------------------------------------------------------------
// ********** <Heat Exchanger Capacity>
RULE HX:CapRtd
  DESCRIPTION
    "The design cooling capacity of the waterside economizer heat exchanger."
  INPUTCLASS
    Required
  MINIMUM
    0
  REPORTPRECISION
    -3
  CHECKSIM
    if( IfValidAnd( CapRtd > 0 ) ) then
      UNCHANGED
    else
      PostError("A non-zero capacity is required for Heat Exchanger '%s'.",Name)
    endif
ENDRULE


// -----------------------------------------------------------------------------
//            <Waterside Economizer Integration Type>
RULE HX:EconoIntegration
  DESCRIPTION
    "Whether the Waterside Economizer is integrated or nonintegrated."
  HELP
    "An integrated WSE is connected such that the outlet of the heat exchanger
     is connected to the chiller inlet.  A nonintegrated WSE is connected in 
     parallel with the chillers."
  INPUTCLASS
    Default
  OPTION
    Integrated
    NonIntegrated
  DEFAULT
    "Integrated"
ENDRULE


// -----------------------------------------------------------------------------
//
//RULE HX:CHWFluidSegOutRef
//  ANNUAL
//    UNCHANGED
//ENDRULE
//
////
//RULE HX:CHWFluidSegInRef
//  ANNUAL
//    UNCHANGED
//ENDRULE
//
////
//RULE HX:CWFluidSegOutRef
//  ANNUAL
//    UNCHANGED
//ENDRULE
//
////
//RULE HX:CWFluidSegInRef
//  ANNUAL
//    UNCHANGED
//ENDRULE


// -----------------------------------------------------------------------------
//            <Status> 
RULE HX:Status
  DESCRIPTION
    "The status of the system or component, used for additions and alterations."
  INPUTCLASS 
    Default
; OPTION
;   * Defined in BEMEnums, shown here for reference. Default is 'New'
;   New
;   Existing 
  DEFAULT
// Status defined from top-down
    if( Parent( IsNew ) ) then "New" else "Existing" endif
  CHECKCODE
    if( ( Proj:IsNewMech .AND. Status != "New" ) .OR.
        ( Proj:IsAlt = 0 .AND. Status = "Altered" ) )
    then
      PostWarning("Waterside Economizer '%s' has a Status of '%s', but 
                   Compliance Type is '%s'.
                   The status of the system will be changed to 'New' for compliance
                   analysis.", Name, Status, Proj:CompType)
    else UNCHANGED
    endif
   SIZING
    if( Proj:IsNewMech )
    then "New"
    else u:Status
    endif
  ANNUAL
    if( Proj:IsNewMech )
    then "New"
    else z:Status
    endif
ENDRULE

//
RULE NEW HX:IsNew
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  SIZING
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
  ANNUAL
    if( Status = "New" .OR. Proj:IsNewMech ) then 1 else 0 endif
ENDRULE

//
RULE NEW HX:IsExisting
  DATATYPE
    Integer
  INPUTCLASS
    NotInput
  DEFAULT
    if( IsNew ) then 0 else 1 endif
  SIZING
    if( IsNew ) then 0 else 1 endif
  ANNUAL
    if( IsNew ) then 0 else 1 endif
ENDRULE


// -----------------------------------------------------------------------------
//            <Chilled Water Supply Temperature Design>
RULE HX:CHWLvgTempDsgn
  DESCRIPTION
    "The temperature of water leaving the heat exchanger on the chilled water 
     side of the system at design conditions."
  HELP
    ""
  INPUTCLASS
    Required
  UNITS
    °F
  REPORTPRECISION
    0
  DEFAULT
    if( IfValidAnd(Parent(DsgnSupWtrTemp) > 32) ) then
      Parent(DsgnSupWtrTemp)
    else UNDEFINED
    endif 
  CHECKSIM
    if( CHWLvgTempDsgn <= CWEntTempDsgn ) then
      PostError("For heat exchanger '%s', the entering condenser water 
                 temperature '%.1f' is not lower than the leaving chilled water 
                 temperature '%.1f'.  This is not possible.  Revise your 
                 inputs.", Name, CWEntTempDsgn, CHWLvgTempDsgn)
    else UNCHANGED
    endif
ENDRULE


//            <Chilled Water Return Temperature Design>
RULE HX:CHWEntTempDsgn
  DESCRIPTION
    "The temperature of water entering the heat exchanger on the chilled water 
     side of the system at design conditions."
  HELP
    ""
  INPUTCLASS
    Required
  UNITS
    °F
  REPORTPRECISION
    0
  DEFAULT
    if( IfValidAnd(Parent(DsgnSupWtrDelT) > 1 ) .AND. 
        IfValidAnd(CHWLvgTempDsgn) > 32 ) then
      Parent(DsgnSupWtrDelT) + CHWLvgTempDsgn
    else UNDEFINED
    endif 
  CHECKSIM
    if( CHWLvgTempDsgn >= CHWEntTempDsgn ) then
      PostError("For heat exchanger '%s', the leaving chilled water temperature 
                 '%.1f' is higher than the entering chilled water 
                 temperature '%.1f'.  This is not correct.  Revise your 
                 inputs.", Name, CHWLvgTempDsgn, CHWEntTempDsgn)
    else if( CHWEntTempDsgn <= CWLvgTempDsgn ) then
      PostError("For heat exchanger '%s', the leaving condenser water temperature 
                 '%.1f' is not lower than the entering chilled water 
                 temperature '%.1f'.  This is not allowed.  Revise your 
                 inputs.", Name, CHWLvgTempDsgn, CHWEntTempDsgn)
    else UNCHANGED
    endif endif
ENDRULE


//            <Condenser Water Supply Temperature Design>
RULE HX:CWLvgTempDsgn
  DESCRIPTION
    "The temperature of water leaving the heat exchanger on the condenser water 
     side of the system at design conditions."
  HELP
    ""
  INPUTCLASS
    Required
  UNITS
    °F
  REPORTPRECISION
    0
  DEFAULT
    if( IfValidAnd(CHWEntTempDsgn > 32) ) then
      CHWEntTempDsgn - 2    // Assumes flow rates are equal on both sides and a 2F approach
    else UNDEFINED
    endif 
  CHECKSIM
    if( CWLvgTempDsgn <= CWEntTempDsgn ) then
      PostError("For heat exchanger '%s', the entering condenser water temperature 
                 '%.1f' is higher than the leaving condenser water 
                 temperature '%.1f'.  This is not correct.  Revise your 
                 inputs.", Name, CWEntTempDsgn, CWLvgTempDsgn)
    else UNCHANGED
    endif
ENDRULE


//            <Condenser Water Supply Temperature Design>
RULE HX:CWEntTempDsgn
  DESCRIPTION
    "The temperature of water entering the heat exchanger on the condenser water 
     side of the system at design conditions."
  HELP
    ""
  INPUTCLASS
    Required
  UNITS
    °F
  REPORTPRECISION
    0
  DEFAULT
    if( IfValidAnd(CHWLvgTempDsgn > 32) ) then
      CHWLvgTempDsgn - 2    // Assumes flow rates are equal on both sides and a 2F approach
    else UNDEFINED
    endif 
ENDRULE


// -----------------------------------------------------------------------------
//            <Chilled Water Side Flow Rate>
RULE HX:CHWFluidFlowCap
  DESCRIPTION
    "The flow rate of the chilled water side of the waterside economizer 
     heat exchanger."
  INPUTCLASS
    NotInput
  UNITS
    gpm
  REPORTPRECISION
    0
  DEFAULT
    CapRtd /( abs(CHWEntTempDsgn - CHWLvgTempDsgn) * 500.19 )
ENDRULE


//            <Condenser Water Side Flow Rate>
RULE HX:CWFluidFlowCap
  DESCRIPTION
    "The flow rate of the condenser water side of the waterside economizer 
     heat exchanger."
  INPUTCLASS
    NotInput
  UNITS
    gpm
  REPORTPRECISION
    0
  DEFAULT
    CapRtd /( abs(CWEntTempDsgn - CWLvgTempDsgn) * 500.19 )
ENDRULE


// -----------------------------------------------------------------------------
//            <Heat Exchanger Log Mean Temperature Difference>
RULE NEW HX:LMTD
  DATATYPE
    Float
  DESCRIPTION
    "The log mean temperature difference of the waterside economizer heat 
     exchanger at the design condition."
  INPUTCLASS
    NotInput
  UNITS
    °F
  DEFAULT
    if( (CHWLvgTempDsgn-CWEntTempDsgn) = (CHWEntTempDsgn-CWLvgTempDsgn) ) then
      CHWLvgTempDsgn-CWEntTempDsgn
    else ( (CHWLvgTempDsgn-CWEntTempDsgn) - (CHWEntTempDsgn-CWLvgTempDsgn) ) / 
      log( (CHWLvgTempDsgn-CWEntTempDsgn) / (CHWEntTempDsgn-CWLvgTempDsgn) )
    endif
ENDRULE


// -----------------------------------------------------------------------------
//            <Heat Exchanger UA Value>
RULE HX:UA
  DESCRIPTION
    "The UA value of the waterside economizer heat exchanger."
  INPUTCLASS
    NotInput
  UNITS
    Btu/hr-°F
  REPORTPRECISION
    0
  DEFAULT
    CapRtd / LMTD
ENDRULE


