// ThermalZone - Zone Level Controls/Setpoints
//
// -------------------------------------------------------------------------
//  Copyright (c) 2014, California Energy Commission
//  All rights reserved.
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are
//  met:
// 
//    * Redistributions of source code must retain the above copyright
//  notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//  notice, this list of conditions and the following disclaimer in
//  the documentation and/or other materials provided with the
//  distribution.
//    * Neither the name of the California Energy Commission nor the names of its
//  contributors may be used to endorse or promote products
//  derived from this software without specific prior written
//  permission.
//
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
//  IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
//  TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
//  PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL CALIFORNIA ENERGY COMMISSION
//  BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//  CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//  SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR
//  BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
//  WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
//  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF
//  ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
// -------------------------------------------------------------------------
//  

//  This rule file addresses the following building descriptors:
//  Section 5.6.1  
//      Control System Type
//      Space Thermostat Throttling Range
//      Space Temperature Schedule


          
// ---------- Section 5.6.1 - Space Temperature Control ------------------------
// ********** Space Thermostat Throttling Range ********************************
RULE ThrmlZn:ThrtlgRng
  DESCRIPTION
    "The number of degrees that the room temperature must change for the
     thermostat to indicate the zone needs heating or cooling; i.e. the deadband
     around the setpoint temperature."
  HELP
    "The setpoint deadband is defined as the setpoint temperature plus or minus
     one half the throttling range. This should not be confused with the deadband
     that typically exists between the heating and cooling setpoints."
  REFERENCE 
    NACM Section 5.6.1
  INPUTCLASS 
    NotInput  
  UNITS 
    °F
  DEFAULT 
    2.0
  SIZING
    if( IsCond )
    then 2.0
    else UNDEFINED
    endif
  ANNUAL  
    z:ThrtlgRng
ENDRULE


// ********** Space Temperature Schedule ***************************************
// Then assign the appropriate thermostat schedules to the thermal zone based on 
// predominant function group determined in ThermalZone-General.rule
RULE ThrmlZn:ClgTstatSchRef
  DESCRIPTION
    "The cooling temperature schedule for the ThermalZone." 
  HELP : T24N
    "The schedules specified in Appendix 5.4A and detailed in Appendix 5.4B shall
     be used as a default."
  REFERENCE 
    NACM Section 5.6.1
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  CHECKCODE : S901G ECBC
    if( IsCond .AND. 
        LocalStatus( ClgTstatSchRef ) = 0 .AND. 
        HasClg != 0 )
    then 
      PostWarning("The cooling thermostat schedule for ThermalZone '%s' is not
                  defined. Since the system(s) serving the zone has cooling capacity,
                  a default schedule will be assigned.", Name)
    else UNCHANGED
    endif
  SIZING_PROPOSED : T24N
    if( LocalStatus( PredominantFuncGrp ) = 0 ) 
    then // No PredominantFunctionGroup defined
      UNDEFINED	
    else if( HtgOnlySysAreaNew > 0 .OR. HtgOnlySysAreaAltered > 0 ) 
    then // Heating only warehouse exception
      UNDEFINED
    else if( HasClg = 0 .AND. IsNoMech = 0 .AND. IsNoAddMech = 0 )
    then
      UNDEFINED
    else if( IsCond ) 
    then // Use default ACM schedule based on PredominantFunctionGroup
      RuleLibrary( Schedule, 
        (SpaceFunctionGroups:ClgTstatSchRef("FuncGroup", PredominantFuncGrp)) )
    else UNDEFINED
    endif endif endif endif
  SIZING_PROPOSED : S901G ECBC
    if( ( LocalStatus( u:ClgTstatSchRef ) > 0 .OR. HasClg = 0 ) .AND.
        IsCond )
    then // User has defined a Tstat schedule or there is no cooling, so pass-through
      u:ClgTstatSchRef
    else if( IsCond ) 
    then // Use default library schedule based on PredominantFunctionGroup
      if( LocalStatus( PredominantFuncGrp ) = 0 ) 
      then UNDEFINED	// No PredominantFunctionGroup defined
      else
        RuleLibrary( Schedule, 
          (SpaceFunctionGroups:ClgTstatSchRef("FuncGroup", PredominantFuncGrp)) )
      endif
    else UNDEFINED
    endif endif
  SIZING_BASELINE
    zp:ClgTstatSchRef
  ANNUAL  
    z:ClgTstatSchRef
ENDRULE
RULE ThrmlZn:HtgTstatSchRef
  DESCRIPTION
    "The heating temperature schedule for the ThermalZone."
  HELP : T24N
    "The schedules specified in Appendix 5.4A and detailed in Appendix 5.4B shall
     be used as a default."
  REFERENCE 
    NACM Section 5.6.1
  INPUTCLASS : T24N
    Prescribed
  INPUTCLASS : S901G ECBC
    Default
  CHECKCODE : S901G ECBC
    if( IsCond .AND. 
        LocalStatus( HtgTstatSchRef ) = 0 .AND.
        HasHtg != 0 )
    then 
      PostWarning("The heating thermostat schedule for ThermalZone '%s' is not
                  defined. Since the system(s) serving the zone has heating capacity,
                  a default schedule will be assigned.", Name)
    else UNCHANGED
    endif
  SIZING_PROPOSED : T24N
    if( LocalStatus( PredominantFuncGrp ) = 0 ) 
    then // No PredominantFunctionGroup defined
      UNDEFINED	
    else if( IsCond ) 
    then // Use default ACM schedule based on PredominantFunctionGroup
      RuleLibrary( Schedule, 
        (SpaceFunctionGroups:HtgTstatSchRef("FuncGroup", PredominantFuncGrp)) )
    else UNDEFINED
    endif endif
  SIZING_PROPOSED : S901G ECBC
    if( ( LocalStatus( u:HtgTstatSchRef ) > 0 .OR. HasHtg = 0 ) .AND.
        IsCond )
    then // User has defined a Tstat schedule or there is no heating, so pass-through
      u:HtgTstatSchRef
    else if( IsCond ) 
    then // Use default library schedule based on PredominantFunctionGroup
      if( LocalStatus( PredominantFuncGrp ) = 0 ) 
      then UNDEFINED	// No PredominantFunctionGroup defined
      else
        RuleLibrary( Schedule, 
          (SpaceFunctionGroups:HtgTstatSchRef("FuncGroup", PredominantFuncGrp)) )
      endif
    else UNDEFINED
    endif endif
  SIZING_BASELINE
    zp:HtgTstatSchRef
  ANNUAL  
    z:HtgTstatSchRef
ENDRULE

// Specify HtgMax/ClgMin schedule values at ThrmlZn
RULE NEW ThrmlZn:HtgTstatSchMaxVal
  DATATYPE
    Float
  LONGFORM
    HeatingThermostatScheduleReferenceMaximumValue
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalCompAssigned( HtgTstatSchRef ) )
    then HtgTstatSchRef:MaxVal
    else 70
    endif
  SIZING
    if( LocalCompAssigned( HtgTstatSchRef ) )
    then HtgTstatSchRef:MaxVal
    else 70
    endif
  ANNUAL
    z:HtgTstatSchMaxVal
ENDRULE
RULE NEW ThrmlZn:ClgTstatSchMinVal
  DATATYPE
    Float
  LONGFORM
    CoolingThermostatScheduleReferenceMinimumValue
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalCompAssigned( ClgTstatSchRef ) )
    then ClgTstatSchRef:MinVal
    else 75
    endif
  SIZING
    if( LocalCompAssigned( ClgTstatSchRef ) )
    then ClgTstatSchRef:MinVal
    else 75
    endif
  ANNUAL
    z:ClgTstatSchMinVal
ENDRULE

// Determine whether the ventilation system is unique form the Primary System
RULE NEW ThrmlZn:UniqueVentSysAssigned
  DATATYPE
    Float
  LONGFORM
    UniqueVentilationSystemAssigned
  INPUTCLASS
    NotInput
  DEFAULT
    if( LocalCompAssigned( VentSysRef ) )
    then if( LocalCompAssigned( PriAirCondgSysRef ) )
      then if( VentSysRef = PriAirCondgSysRef )
        then 0  // Vent Sys and Primary Sys are the same
        else 1  // Vent Sys an Primary System are different
        endif
      else 1  // No Primary System Assigned, Vent System is Unique
      endif
    else 0  // No Vent System Assigned
    endif  
ENDRULE


// ********** System Priority **************************************************
// See issue 1220 for background
RULE ThrmlZn:PriAirCondgSysPriority
  DESCRIPTION
    "The order in which Primary A/C system is used to meet zone loads."
  HELP
    "If there is more than one unique system serving the zone, this value defines
     the order in which the Primary A/C system is used to meet zone loads. If there
     is a conflict in ordering, the program will assume the Ventilation System
     is ordered first, followed by the Primary A/C system."
  INPUTCLASS
    Default
  DEFAULT
    if( IsCond )
    then 
      if( UniqueVentSysAssigned )
      then
        if( VentSysRef:IsExhSys )
        then 1 // VentSys is an exhaust system, set PriSys to #1 priority
        else 2 // Set to second priority, same as before adding priority control
        endif
      else 1 // VentSys and PriSys are the same
      endif
    else UNCHANGED
    endif
  SIZING_PROPOSED
    if( IsCond .AND. IsNewHVAC )
    then
      if( UniqueVentSysAssigned )
      then
        if( VentSysRef:IsExhSys )
        then 1 // VentSys is an exhaust system, set PriSys to #1 priority
        else if( IfValidAnd( PriAirCondgSysPriority = VentSysPriority ) )
        then 2 // Set to 2 if there is a conflict
        else ValidOr( u:PriAirCondgSysPriority, 2 ) // User value or default to 2
        endif endif
      else 1 // VentSys and PriSys are the same
      endif
    else UNCHANGED
    endif
  SIZING_BASELINE
    if( IsExistingHVAC .OR. IsCond = 0 )
    then zp:PriAirCondgSysPriority
    else 1
    endif
  ANNUAL
    z:PriAirCondgSysPriority
ENDRULE
RULE ThrmlZn:VentSysPriority
  DESCRIPTION
    "The order in which Ventilation system is used to meet zone loads."
  HELP
    "If there is more than one unique system serving the zone, this value defines
     the order in which the Ventilation system is used to meet zone loads. If there
     is a conflict in ordering, the program will assume the Ventilation System
     is ordered first, followed by the Primary A/C system."
  INPUTCLASS
    Default
  DEFAULT
    if( VentSrc != "Forced" .OR. LocalCompAssigned( VentSysRef ) = 0 )
    then UNDEFINED
    else if( IsCond )
    then 
      if( UniqueVentSysAssigned )
      then 
        if( VentSysRef:IsExhSys )
        then 2 // VentSys is an exhaust system, set VentSys to #2 priority
        else 1 // VentSys is first priority, same as before adding priority control
        endif
      else 1 // VentSys and PriSys are the same
      endif
    else UNCHANGED
    endif endif
  SIZING_PROPOSED
    if( VentSrc != "Forced" .OR. LocalCompAssigned( VentSysRef ) = 0 )
    then UNDEFINED
    else if( IsCond .AND. IsNewHVAC )
    then 
      if( UniqueVentSysAssigned )
      then
        if( VentSysRef:IsExhSys )
        then 2 // VentSys is an exhaust system, set VentSys to #2 priority
        else if( IfValidAnd( PriAirCondgSysPriority = VentSysPriority ) )
        then 1 // Set to 1 if there is a conflict
        else ValidOr( u:VentSysPriority, 1 ) // User value or default to 1
        endif endif
      else 1 // VentSys and PriSys are the same
      endif
    else UNCHANGED
    endif endif
  SIZING_BASELINE
    if( VentSrc != "Forced" .OR. LocalCompAssigned( VentSysRef ) = 0 )
    then UNDEFINED
    else if( IsExistingHVAC .OR. IsCond = 0 )
    then zp:VentSysPriority
    else if( IsCond .AND. IsRes )
    then UNDEFINED // Baseline VentSys for Res is an exhaust system
    else 1 // VentSys and PriSys are the same
    endif endif endif
  ANNUAL
    z:VentSysPriority
ENDRULE
